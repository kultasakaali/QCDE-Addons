#library "LGTRAIN"
#include "zcommon.acs"

int _ticker = 140;

script "checkPracticeMode" (void) {				//need this for decorate
	SetResultValue(GetCVar("sv_LGPractice"));
}

script "healOnRespawn" RESPAWN {
	if (!GetCVar("sv_LGPractice")) terminate;
	
	Delay(10);
	GiveInventory("QCMegaHealth", 2);
	GiveInventory("QCArmorBonus", 200);
	GiveInventory("NoHealthDegenerationToken", 1);
	GiveInventory("NoArmorDegenerationToken" , 1);
	TakeInventory("QCGauntlet", 1);
}

script "firstSpawnHeal" ENTER {
	ACS_NamedExecuteAlways("healOnRespawn", 0);
	
	//a bot has been replaced by a human player or vice versa, heal the other player too
	Delay(5);
	if(PlayerCount() == 2) {
		ACS_NamedExecuteAlways("healOnFrag", 0);
	}	
}

script "showRules" OPEN CLIENTSIDE {
	Delay(_ticker);
	HudMessage(s:"\cfWork hard and benefit\n\cgSarge only!\n\n\ci1v1\n\cdWait for your turn!\n\ccEveryone is healed to max after each frag";HUDMSG_FADEINOUT,0,CR_RED,1.5,0.5,5.0,0.7,1.0);
}

script "initMap" OPEN {
	//force minplayers, no sense in having more bots
	SetCvar("sv_minplayers", 2);
	
	//give way to MOTD
	if(GetCVarString("sv_motd") != "") {
		_ticker = 35 + GetCVar("cl_motdtime") * 35;
	}
}

script "healOnFrag" DEATH {
	if (!GetCVar("sv_LGPractice")) terminate;
	
	Delay(45);
	int i;
	for(i = 0; i < 64; i++)	{
		if(GetActorProperty(1000 + i, APROP_Health) > 0) {
			GiveActorInventory(1000 + i, "QCMegaHealth", 1);
			GiveActorInventory(1000 + i, "QCMegaHealth", 1);
			GiveActorInventory(1000 + i, "QCArmorBonus", 200);
		}
	}
}