// ----------------------------------------------------------------------------
//
// Nyx's abilities
//
// Active:	Ghost Walk
//			Temporary invisibility and intangibility, telefrag on reappearing.
//			Originally by Saber Interactive.
//
// Passive: Wall Jump
//
// ----------------------------------------------------------------------------


//--------------------------------------------------------
// Active
//--------------------------------------------------------

Actor GhostWalkInProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_ChangeFlag("Solid",0)
		TNT1 A 0 A_ChangeFlag("NOTARGET",1)
		TNT1 A 0 A_ChangeFlag("THRUACTORS",1)
		TNT1 A 0 A_ChangeFlag("SHOOTABLE",0)
		TNT1 A 0 A_ChangeFlag("SILENT",1)
		TNT1 A 0 A_GiveInventory("GhostWalkWindowTint",1)
		TNT1 A 0 A_GiveInventory("GhostTerror",1)
		TNT1 A 0 A_GiveInventory("NoWeaponFireToken",1)
		TNT1 A 0 A_GiveInventory("NoFootstepsToken",1)
		TNT1 A 0 A_SpawnItemEx("GhostwalkFog", 0,0,36, 0,0,0, 0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		//TNT1 A 0 A_ClearSoundTarget
		//TNT1 A 0 A_RadiusGive("TargetForgetter",2048,RGF_MONSTERS)
		TNT1 A 0 A_SetTranslucent(0.0)
		//removes effects of thumper
		TNT1 A 0 SetPlayerProperty(0,0,PROP_FROZEN)
		TNT1 A 0 A_TakeInventory("PowerGrayThumpFlight",1)
		TNT1 A 0 A_GiveInventory("ActiveInProgress")
		TNT1 A 35 
		Stop
	}
}

Actor GhostWalkOutProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_ChangeFlag("Solid",1)
		TNT1 A 0 A_ChangeFlag("NOTARGET",0)
		TNT1 A 0 A_ChangeFlag("THRUACTORS",0)
		TNT1 A 0 A_ChangeFlag("SHOOTABLE",1)
		TNT1 A 0 A_ChangeFlag("SILENT",0)
		TNT1 A 0 A_SetTranslucent(1.0)
		TNT1 A 0 A_TakeInventory("PowerGhostTint",1)
		TNT1 A 0 A_TakeInventory("PowerGhostTerror",1)
		TNT1 A 0 A_TakeInventory("NoWeaponFireToken",1)
		TNT1 A 0 A_TakeInventory("NoFootstepsToken",1)
		TNT1 A 0 A_SpawnItemEx("GhostwalkFog", 0,0,36, 0,0,0, 0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"DMTFrag")
		TNT1 A 0 A_ThrowGrenade("GhostwalkKiller")
		Goto Ender
	DMTFrag:
		TNT1 A 0 A_ThrowGrenade("GhostwalkKillerDM")
		Goto Ender
	Ender:
		TNT1 A 0 A_TakeInventory("ActiveInProgress")
		TNT1 A 35 
		Stop
	}
}

Actor GhostwalkKiller
{
	Obituary "$OB_GhostwalkKiller" // "%o was ghosted by %k."
	ProjectileKickBack 0
	+FORCERADIUSDMG // Imagine getting stuck in a cyber. [This meme brought to you by Dire Orb Gang.]
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Explode(1200,24,0)
		Stop
	}
}



Actor GhostwalkKillerDM
{
	Obituary "$OB_GhostwalkKillerDM" // "%o was ghosted by %k."
	ProjectileKickBack 0
	+FORCERADIUSDMG // Imagine getting stuck in a cyber. [This meme brought to you by Dire Orb Gang.]
	States
	{
	Spawn:
		//Medals start
		TNT1 A 0 NoDelay A_GiveToTarget("AbilityToken",1)
		TNT1 A 10 A_Explode(1200,13,0)
		TNT1 A 0 A_TakeFromTarget("AbilityToken",1)
		TNT1 A 0 A_GiveToTarget("FromTheShadowsToken",1)
		TNT1 A 35
		TNT1 A 0 A_TakeFromTarget("FromTheShadowsToken",1)
		//Medals end
		Stop
	}
}

Actor GhostWalkCleanerProc : CustomInventory	//same as "GhostWalkOutProc" but without the fog
{
	States
	{
	Pickup:
		TNT1 A 0 A_ChangeFlag("Solid",1)
		TNT1 A 0 A_ChangeFlag("NOTARGET",0)
		TNT1 A 0 A_ChangeFlag("THRUACTORS",0)
		TNT1 A 0 A_ChangeFlag("SHOOTABLE",1)
		TNT1 A 0 A_SetTranslucent(1.0)
		TNT1 A 0 A_TakeInventory("PowerGhostTint",1)
		TNT1 A 0 A_TakeInventory("PowerGhostTerror",1)
		TNT1 A 0 A_TakeInventory("NoWeaponFireToken",1)
		TNT1 A 0 A_TakeInventory("NoFootstepsToken",1)
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_TakeInventory("ActiveInProgress")
		TNT1 A 35 
		Stop
	}
}

// To stain the screen, and also add extra speed
Actor PowerGhostTint : PowerSpeed
{
	Speed 1.3
	+POWERSPEED.NOTRAIL
}

Actor GhostWalkWindowTint : PowerupGiver
{
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Powerup.Colormap 0,0,0, .25,.5,1
	Powerup.Duration -100
	Powerup.Type "GhostTint"
}



//For monsters to "be extra wary" of Nyx ghosting around
Actor PowerGhostTerror : PowerFrightener {}
Actor GhostTerror : GhostWalkWindowTint
{
	Powerup.Duration -100
	Powerup.Type "GhostTerror"
}

Actor GhostWalkWeaponShutUp : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_StopSound(5)
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 35 
		Stop
	}
}

Actor TargetForgetter : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_ClearTarget
		TNT1 A 35 
		Stop
	}
}

// Trail spawner, ACS gives this proc several times in a 
// 1 tic interval at the beginning of ghost walk
Actor GhostWalkTrailProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("GhostWalkTrail",0,0,50, 0,0,frandom(0.2,0.6), 0,SXF_NOCHECKPOSITION)
		TNT1 A 10
		Stop
	}
}
	
Actor GhostWalkTrail
{
	+NOINTERACTION +NOTONAUTOMAP
	RenderStyle Add
	Scale 0.13
	Translation "0:255=%[0,0,0]:[0.0,0.0,1.3]"
	States
	{
	Spawn:
		X102 A 0 NODELAY A_Jump(240,2,4,6,8,10,12,14,16,18)
		X102 ABCDEFGHIJKLMNOPQRSTUV 2 Bright A_FadeOut(0.06)
		Goto Spawn+1
	}
}



Actor GhostWalkFog
{
	+NOINTERACTION
	RenderStyle Add
	Scale 1.1
	States
	{
	Spawn:
		DB18 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright
		Stop
	}
}





//-------------------------------------------------------
//-------------------------------------------------------
// This is a trail that Durandal can see as passive
Actor GhostWalkFullTrailProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("GhostWalkFullTrail",0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION)
		TNT1 A 10
		Stop
	}
}
	
Actor GhostWalkFullTrail
{
	+NOINTERACTION +NOTONAUTOMAP
	RenderStyle Add
	XScale 0.79
	YScale 0.905
	Alpha 0.0
	VisibleToPlayerClass QCDurandal
	States
	{
	Spawn:
		QP12 A 0 NODELAY A_Jump(255,1,2,3,4)
		QP12 ABCD 0 A_Jump(255,"Doer")
	Doer:
		QP12 "#" 3
		QP12 "#####" 1 A_FadeIn(0.199)
		QP12 "#" 1 A_FadeOut(0.05)
		Wait
	}
}


//--------------------------------------------------------
// Passive
//--------------------------------------------------------

Actor NyxWallTest
{
	+SOLID
	Species "Players" +THRUSPECIES
	Gravity 0
	Radius 48
	Height 1
	States
	{
	Spawn:
		TNT1 A 100 NoDelay A_ChangeVelocity(2,2,0)
		Stop
	}
}

Actor ChangeVelocityDoerNyxProc : CustomInventory {
	States 
	{ 
	Pickup: 
		TNT1 A 0 A_JumpIfInventory("NoFootstepsToken",1,2)
		TNT1 A 0 A_PlaySound("QC/Abilities/Nyx/Nyx_Walljump",CHAN_BODY,0.6,false,1.7)
		TNT1 A 0 
		Goto CheckSpeed
	CheckSpeed:
		TNT1 A 0 A_JumpIf(velz < 0, "StopFalling")
		Goto AnotherJump
	StopFalling:
		TNT1 A 0
		Goto Replace
	AnotherJump:
		TNT1 A 0
		Goto Add
		
	Replace:
		TNT1 A 0 A_ChangeVelocity(velx/2, vely/2, 5, CVF_REPLACE)
		Stop
	Add:
		TNT1 A 0 A_ChangeVelocity(velx/2, vely/2, sqrt(velz) + 5, CVF_REPLACE)
		Stop
		
	Death:
		TNT1 A 1
		Stop
	}
}