// --------------------------------------------------------------------------
//
// Zedek's abilities
//
// Active:	Quietus
//			Fires quietus projectiles that leave area denial fire on impact
//
// Passive: Iron Feet
//			Takes less damage from damaging floors in DM
//			Takes no damage from damaging floors in coop
//			Deals damage when falling from heights
//
// --------------------------------------------------------------------------

Actor CompatVer30_Zedek {} // for compatibility checks

//--------------------------------------------------------
// Active
//--------------------------------------------------------

Actor QuietusAttackProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_AlertMonsters(96)
		TNT1 A 0 A_GiveInventory("ActiveInProgress", 1, 0, GIF_SKIPOWNER)
		TNT1 A 0 A_GiveInventory("NoWeaponFireToken", 1, 0, GIF_SKIPOWNER)
		TNT1 A 0 A_PlaySound("QC/Abilities/Zedek/QuietusFire",CHAN_7,1)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0)==2,"DM")
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile", 11.5  , false, -4)
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile", 6.625 , false, -4)
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile", 0     , false, -4)
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile", -6.625, false, -4)
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile", -11.5 , false, -4)
		TNT1 A 35
		Stop
	DM:
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile_DM", 11.5  , false, -4, 0, FPF_TRANSFERTRANSLATION)
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile_DM", 6.625 , false, -4, 0, FPF_TRANSFERTRANSLATION)
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile_DM", 0     , false, -4, 0, FPF_TRANSFERTRANSLATION)
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile_DM", -6.625, false, -4, 0, FPF_TRANSFERTRANSLATION)
		TNT1 A 0 A_FireCustomMissile("QuietusFireProjectile_DM", -11.5 , false, -4, 0, FPF_TRANSFERTRANSLATION)
		TNT1 A 35
		Stop
	}
}





Actor QuietusFireProjectile //: FSwordMissile
{
	Projectile
	Speed 40
	Damage 20
	Radius 3
	Height 6
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[0,2,0]"
	Scale 1.2
	+EXTREMEDEATH
	+SEEKERMISSILE
	+MTHRUSPECIES
	+DONTBLAST
	DeathSound "QC/Abilities/Zedek/QuietusExplosion"
	States
	{
	Spawn:
		FSFX ABC 3 Bright
		Loop

	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,170)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont

	DeathCont:
		TNT1 A 0 A_AlertMonsters(384)
		FSFX D 0 A_SpawnItemEx("QuietusFireSpot",0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_NOUNLAGGED|SXF_FORCESERVERSIDE)
		FSFX D 0 A_Explode(64,128,0)
		FSFX D 0 A_RadiusThrust(70,48,RTF_NOIMPACTDAMAGE|RTF_THRUSTZ)
		FSFX DEFGHIJKLM 3 Bright
		Stop
	}
}

Actor QuietusFireProjectile_DM : QuietusFireProjectile
{
	Damage (8) //if fired at point blank, 5 gusts *8dmg = 40dmg on impact + 60dmg over time
	-MTHRUSPECIES
	-DONTBLAST
	States
	{
	Death:
	DeathCont:
		FSFX D 0 A_RadiusThrust(100,48,RTF_NOIMPACTDAMAGE|RTF_THRUSTZ)
		FSFX D 0 ACS_NamedExecuteWithResult("QCDE_QuietusHit")
		FSFX D 0 A_RadiusGive("QuietusVictimProc", 48, RGF_PLAYERS|RGF_FORCESERVERSIDE)
		FSFX D 0 A_SpawnItemEx("QuietusFireSpot_DM",0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_NOUNLAGGED|SXF_FORCESERVERSIDE|SXF_TRANSFERTRANSLATION)
		FSFX DEFGHIJKLM 3 Bright
		Stop
	}
}

Actor QuietusVictimProc : CustomInventory
{
	+SERVERNETID
	States
	{
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("QCDE_QuietusVictim")
		Stop
	}
}


Actor QuietusFireSpot
{
	var int user_dmg;
	Projectile
	+THRUACTORS
	+NOINTERACTION
	+NODAMAGETHRUST
	+BLOODLESSIMPACT
	+SEEKERMISSILE
	+DONTSPLASH
	+DONTBLAST
	+SERVERNETID
	Radius 4
	Height 2
	Damage 0
	Scale 0.28
	var int user_counter;
	//RenderStyle None
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_PlaySound("QC/Abilities/Zedek/FireCrackle",CHAN_BODY,0.3,1,7)
		TNT1 A 0 A_Jump(180,2) // ~30% chance it will not jump, and instead will continue causing pain to monsters
		TNT1 A 0 A_ChangeFlag("Painless",1)
		TNT1 A 0 A_SetUserVar(user_dmg,0)

		Looper:
			TNT1 A 0 A_JumpIf(user_counter>=28,"Ender")	//280 tics, 8 seconds
			TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
			TNT1 A 0 A_JumpIfTargetInLOS("Sounder",360,JLOSF_PROJECTILE,150)
			TNT1 A 0 A_Jump(256,"ContinueLooper")
		ContinueLooper:
			TNT1 A 0 A_SetUserVar(user_counter, user_counter+1)
			TNT1 A 5 Light("WhiteSmallFlicker3") A_Explode(10,96,0,0,96)
			TNT1 A 1 Light("WhiteSmallFlicker3") A_Explode(10,96,0,0,96)
			TNT1 A 3 Light("WhiteSmallFlicker3") A_SpawnItemEx("QuietusADFireFX", random(8,-8),random(8,-8),0, 0,0,frandom(0.1,0.6), 0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE,0,0)
			Goto Looper

	Sounder:
		TNT1 A 0 A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_Jump(255,"ContinueLooper")
		Goto ContinueLooper

	Ender:
		TNT1 A 0 A_PlaySound("QC/Abilities/Zedek/FireCrackleFadeOut",CHAN_BODY,0.3,0,7)
		TNT1 A 10 Light("WhiteSmallFlicker3")
		TNT1 A 2 Light("WhiteSmallPoint2")
		TNT1 A 2 Light("WhiteSmallPoint3")
		TNT1 A 2 Light("WhiteSmallPoint1")
		TNT1 A 2 Light("WhiteSmallPoint2")
		TNT1 A 2 Light("WhiteSmallPoint3")
		Stop
	}
}
Actor QuietusFireSpot_DM : QuietusFireSpot
{
	-DONTBLAST
	Scale 0.08
	States
	{
		ContinueLooper:
			TNT1 A 5 Light("WhiteSmallFlicker3") A_Explode(user_dmg,96,0,0,96)
			TNT1 A 1 Light("WhiteSmallFlicker3")
			TNT1 A 0 A_JumpIf(user_dmg>=12,3)
			TNT1 A 0 A_SetUserVar(user_dmg,user_dmg+3)
			TNT1 A 0 A_SetScale(scalex+0.075)
			TNT1 A 0 A_SetUserVar(user_counter, user_counter+1)
			TNT1 A 3 Light("WhiteSmallFlicker3") A_SpawnItemEx("QuietusADFireFX", random(8,-8),random(8,-8),0, 0,0,frandom(0.1,0.6), 0,SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION,0,0)
			Goto Looper
	}
}

Actor QuietusADFireFX
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NONETID
	RenderStyle Add
	Scale 0.28
	Translation "250:255=%[0,0,0]:[0,2,0]"
	Alpha 0
	States
	{
	Spawn:
		SM08 TUVWX 2 NODELAY A_FadeIn(0.2)
	Looper:
		SM08 ABCDEFGHIJKLMNOPQRSTUVWX 2 Bright A_FadeOut(0.05)
		Loop
	}
}


//--------------------------------------------------------
// Passive: Iron Feet
//--------------------------------------------------------

Actor PowerZedekFeet_DM : PowerProtection
{
	+SERVERNETID
	DamageFactor "Slime", 0.509
	Powerup.Duration 0x7FFFFFFF
	powerup.color 1,1,1,0
}

Actor PowerZedekFeet : PowerIronFeet
{
	+SERVERNETID
	Powerup.Duration 0x7FFFFFFF
	powerup.color 1,1,1,0
}

// This actor is named after a pool jump the point of which is producing the
// biggest splash, soaking everyone around.
// Power is set as vertical speed.

Actor ZedekCannonball
{
	+NOINTERACTION +NOTONAUTOMAP
	+DONTSPLASH
	+SERVERNETID
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Warp(AAPTR_DEFAULT,0,0,0)
		//TNT1 A 0 A_Log("Cannonball force:")
		//TNT1 A 0 A_LogInt(64+velz*13)
		//TNT1 A 0 A_LogInt(velz*14)
		TNT1 A 0 A_AlertMonsters(velz * 16)
		//Medals start
		TNT1 A 0 A_GiveToTarget("AbilityTokenGiver", 1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE|GIF_SKIPOWNER)
		//Medals end
		TNT1 A 0 A_Explode(64 + velz * 1.5, velz * 11, 0, 0, velz * 7)
		TNT1 A 0 A_SpawnItemEx("GroundDust", 0,0,5, 0,0,frandom(0,1.4),0, SXF_NOCHECKPOSITION|SXF_NOUNLAGGED|SXF_SKIPOWNER|SXF_TRANSFERPOINTERS)
		TNT1 A 70 A_PlaySound("QC/Abilities/Zedek/FloorPound", CHAN_AUTO, velz/24, false, 1.6 - sqrt(velz)/12)
		Stop
	}
}

Actor GroundDust
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+NONETID
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"Type1","Type2")	//to define which graphic will use
	Type1:
		X201 E 0
		Goto "Continue"
	Type2:
		X201 F 0
		Goto "Continue"
	Continue:
		"####" "#" 0 A_SetScale(ScaleX+0.05)
		"####" "#" 1 A_FadeOut(0.03)
		Loop
	}
}
