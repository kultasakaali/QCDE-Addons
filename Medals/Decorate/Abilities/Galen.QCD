// ----------------------------------------------------------------------------
//
// Galen's abilities
//
// Active:	Totem
//
// Passive: Cooldown decreases by picking up health (not health bonus)
//
// ----------------------------------------------------------------------------


//--------------------------------------------------------
// Active
//--------------------------------------------------------
Actor TotemProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_FireCustomMissile("TotemProjectile",0, false,-6)
		TNT1 A 0 A_GiveInventory("ActiveInProgress")
		TNT1 A 0 A_GiveInventory("NoWeaponFireToken",1)		
		TNT1 A 0 A_PlaySound("QC/Abilities/Galen/TotemThrow",CHAN_AUTO)
		Stop
	}
}

Actor TotemCleanerProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_TakeInventory("NoWeaponFireToken",1)
		TNT1 A 35 
		Stop
	}
}


Actor TotemProjectile
{
	var int user_counter;
	Projectile
	Radius 11
	Height 12
	Speed 20
	-NOGRAVITY
	+ISMONSTER
	+FRIENDLY
	+SHOOTABLE
	+BRIGHT
	+SKYEXPLODE
	+DONTBLAST
	DamageType "Totem"
	Gravity 0.5
	RenderStyle Add
	Scale 0.75
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck") < 2,"SpawnLoop")
		TNT1 A 0 A_ChangeFlag("DONTBLAST",false) //in DM, can be blasted away by freeman, only while it's in projectile form
	SpawnLoop:
		TOTP ABDE 3 BRIGHT A_SpawnItemEx("TotemTrail", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION)
		Loop
	Death:
	BecomingTotem:
		TNT1 A 0 A_ChangeFlag("DONTBLAST",true) //but once it's become a totem, it cant be blasted even in PvP
		TNT1 A 0 A_PlaySound("QC/Abilities/Galen/TotemArm",CHAN_AUTO)
		TNT1 A 0 A_PlaySound("QC/Abilities/Galen/TotemLoop",5,1,4)
		TNT1 A 0 A_SetTranslucent(1.0,0) //makes it opaque
		TNT1 A 0 A_ChangeFlag("NOGRAVITY",0)
		TNT1 A 0 A_ChangeFlag("MOVEWITHSECTOR",1)
		TNT1 A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_DEFAULT) // Owner of grenade is stored as Master
		TOTM A 0 A_SetScale(0.68,0.001)
		TOTM A 1 A_SetScale(0.68,0.03)
		TOTM A 1 A_SetScale(0.68,0.08)
		TOTM A 1 A_SetScale(0.68,0.13)
		TOTM A 1 A_SetScale(0.68,0.18)
		TOTM A 1 A_SetScale(0.68,0.23)
		TOTM A 1 A_SetScale(0.68,0.28)
		TOTM A 1 A_SetScale(0.68,0.33)
		TOTM A 1 A_SetScale(0.68,0.38)
		TOTM A 1 A_SetScale(0.68,0.43)
		TOTM A 1 A_SetScale(0.68,0.48)
		TOTM A 1 A_SetScale(0.68,0.53)
		TOTM A 1 A_SetScale(0.68,0.58)
		TOTM A 1 A_SetScale(0.68,0.63)
		TOTM A 1 A_SetScale(0.68,0.68)
		TNT1 A 0 A_LookEx(LOF_NOSOUNDCHECK, 1, 92, 1, 360, "Explode") // Check if explode immediately
		TNT1 A 100 A_Jump(256,1) 
		// ^ delay for client to catch up to server deciding if the ting goed bum or it settled and activated biotic field
		TNT1 A 0 ACS_NamedExecuteWithResult("QCDE_TotemSpawned")
		TNT1 A 0 A_SpawnItemEx("TotemHitbox", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck") >= 2,"DMTotem")
		Goto IamTotem
	DMTotem:
		TNT1 A 0 A_ChangeFlag("FRIENDLY", false)
		Goto IamTotem
	IamTotem:
		TNT1 A 0 A_JumpIf(user_counter>=28,"NoMoreHealing")	//8sec
		TNT1 A 0 A_SetUserVar(user_counter,user_counter+1)
		TNT1 A 0 A_JumpIfInTargetInventory("TotemToken", 5, "SuperHealing")
		TOTM A 0 A_RadiusGive("TotemHealth",92,RGF_PLAYERS,1)
		TOTM BBBBB 1 A_LookEx(LOF_NOSOUNDCHECK, 1, 92, 1, 360, "Attack")
		TOTM A 0 A_RadiusGive("TotemHealth",92,RGF_PLAYERS,1)
		TOTM A 0 A_RadiusGive("TotemHealthPvE",192,RGF_PLAYERS,1)
		TOTM BBBBB 1 A_LookEx(LOF_NOSOUNDCHECK, 1, 92, 1, 360, "Attack")
		TNT1 A 0 A_SpawnItemEx("TotemAura",0,0,25, 0,0,0, 0,SXF_NOCHECKPOSITION)
		Loop
	SuperHealing:
		TOTM A 0 A_RadiusGive("TotemSuperHealth",92,RGF_PLAYERS,1)
		TOTM BBBBB 1 A_LookEx(LOF_NOSOUNDCHECK, 1, 92, 1, 360, "Attack")
		TOTM A 0 A_SpawnItemEx("TotemAuraLong",0,0,25, 0,0,0, 0,SXF_NOCHECKPOSITION)
		TOTM A 0 A_RadiusGive("TotemSuperHealth",92,RGF_PLAYERS,1)
		TOTM A 0 A_RadiusGive("TotemSuperHealthPvE",192,RGF_PLAYERS,1)
		TOTM BBBBB 1 A_LookEx(LOF_NOSOUNDCHECK, 1, 92, 1, 360, "Attack")
		TOTM A 0 A_SpawnItemEx("TotemAura",0,0,25, 0,0,0, 0,SXF_NOCHECKPOSITION)
		Goto IamTotem
	NoMoreHealing:
		TOTM AAAAA 1 A_LookEx(LOF_NOSOUNDCHECK, 1, 92, 1, 360, "Attack")
		Loop
	Attack:
		"####" "#" 1 ACS_NamedExecuteWithResult("QCDE_TotemAttacks")
		"####" "#" 0 A_Jump(256,"Explode") // so in truth, it explodes
	NoAttack:
		"####" "#" 10
		Goto IamTotem // but dumb prediction goes the harmless way
	Explode:
		TNT1 A 0 A_PlaySound("QC/Abilities/Galen/TotemExplosion",5)
		TNT1 A 0 A_SpawnItemEx("TotemExplosion",0,0,20, 0,0,0, 0,SXF_NOCHECKPOSITION)
		TNT1 AA 0 A_SpawnItemEx("TotemExplosion",0,0,20, frandom(1,-1),frandom(1,-1),frandom(1,-1), 0,SXF_NOCHECKPOSITION)
		TNT1 A 0 ACS_NamedExecuteWithResult("QCDE_TotemDestroyed")
		TNT1 A 0 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT) //Owner of grenade as "Target" to avoid self damage
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck") >= 2,"DMExplo")
		//SP explo
		TNT1 A 0 A_GiveToTarget("HitBeepTotem",1)
		TNT1 A 0 Thing_ChangeTID(0,0)
		TNT1 A 0 A_JumpIfInTargetInventory("TotemToken", 5, "SuperExplode")
		TNT1 A 100 A_Explode(random(4,8)*100,192,0,0,116)
		Stop
	SuperExplode:
		TNT1 A 0 A_SpawnItemEx("TotemExplosion",0,0,60, frandom(1,-1),frandom(1,-1),frandom(1,-1), 0,SXF_NOCHECKPOSITION)
		TNT1 A 100 A_Explode(random(10,20)*100,192,0,0,116)
		Stop
	DMExplo:
		TNT1 A 0 A_GiveToTarget("HitBeepTotem",1)
		//Medals start
		TNT1 A 0 A_GiveToTarget("TotemExploded",1)
		//Medals end
		TNT1 A 0 Thing_ChangeTID(0,0)
		TNT1 A 0 A_JumpIfInTargetInventory("TotemToken", 5, "SuperDMExplo")
		TNT1 A 10 A_Explode(75,116,0,0,116)
		//Medals start
		TNT1 A 90 A_TakeFromTarget("TotemExploded",1)
		//Medals end
		Stop
	SuperDMExplo:
		TNT1 A 0 A_SpawnItemEx("TotemExplosion",0,0,60, frandom(1,-1),frandom(1,-1),frandom(1,-1), 0,SXF_NOCHECKPOSITION)
		TNT1 A 10 A_Explode(125,128,0,0,128)
		//Medals start
		TNT1 A 90 A_TakeFromTarget("TotemExploded",1)
		//Medals end
		Stop
	}
}

Actor TotemHealth : InventoryClassChecker
{
	Inventory.PickupMessage ""
	Inventory.PickupSound "NoSoundHax"
	States
	{
		DoRanger:
		DoDoomSlayer:
		DoCorvus:
		DoKane:
		DoCaleb:
		DoMenelkir:
		DoTerminator:
		DoZedek:
		DoInquisitor:
		DoGalen:
		DoBitterman:
		DoNyx:
		DoSorgaul:
		DoDurandal:
		DoDukeNukem:
		DoKeel:
		DoSarge:
		DoLoWang:
		DoGrayson:
		DoBlazkowicz:
		DoFreeman:
		DoEradicator:
		DoVorMatur:
		DoEleena:
		DoLucienne:
		DoMajor:
		DoHunter:
		DoSam:
		DoPostal:
		DoPainkiller:
		DoIntruder:
		Default:
			TNT1 A 0 A_JumpIfHealthLower(CallACS("MaxHealth"),1)	Goto Failer
			TNT1 A 0 A_JumpIfInventory("DMToken",1,"HealDoneSuccessPVP")
			TNT1 A 0 A_Jump(256,"HealDoneSuccess")					Goto HealDoneSuccess
		// ^ insert more classes here
		
	HealDoneSuccess:
		TNT1 A 0 ACS_NamedExecuteWithResult("HealthGiver", CallACS("MaxHealth"), 5, 0) 
		Stop
	HealDoneSuccessPVP:
		TNT1 A 0 ACS_NamedExecuteWithResult("HealthGiver", CallACS("MaxHealth"), 8, 0) 
		Stop
	Failer:
		TNT1 A 0
		Fail
	}
}

Actor TotemHealthPvE : TotemHealth
{
	States
	{
	HealDoneSuccess:
		TNT1 A 0 ACS_NamedExecuteWithResult("HealthGiver", CallACS("MaxHealth"), 5, 0) 
		Stop
	HealDoneSuccessPVP:
		TNT1 A 0 // do nothing
		Stop
	}
}

Actor TotemSuperHealth : TotemHealth
{
	States
	{
	HealDoneSuccess:
		TNT1 A 0 ACS_NamedExecuteWithResult("HealthGiver", CallACS("MaxHealth"), 10, 0) 
		Stop
	HealDoneSuccessPVP:
		TNT1 A 0 ACS_NamedExecuteWithResult("HealthGiver", CallACS("MaxHealth"), 14, 0) 
		Stop
	}
}

Actor TotemSuperHealthPvE : TotemHealth
{
	States
	{
	HealDoneSuccess:
		TNT1 A 0 ACS_NamedExecuteWithResult("HealthGiver", CallACS("MaxHealth"), 10, 0) 
		Stop
	HealDoneSuccessPVP:
		TNT1 A 0 // do nothing
		Stop
	}
}

Actor TotemTrail
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	RenderStyle Add
	Scale 0.7
	States
	{
	Spawn:
		TOTP AAABBBCCCDDD 1 Bright A_FadeOut(0.07)
		Loop
	}
}


Actor TotemHitbox
{
	Radius 11
	Height 40
	Mass 0x7FFFFFFF
	RenderStyle None
	Scale 0.8
	Alpha 0.25
	+ALWAYSTELEFRAG
	+SHOOTABLE
	+NOBLOOD
	+INVULNERABLE
	+DONTBLAST
	Health 40
	Tag "$TAG_TotemHitbox" // "Unholy Totem"
	DamageFactor "Totem", 0
	DamageFactor "NailgunWeak", 0.43 //hack for weak nailgun-jump, explained in nailgun.qcd
	Translation "0:255=%[0,0,0]:[0,1.2,1.2]"
	+BRIGHT
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecuteAlways("QCDE_HitboxSpawned")
		TNT1 A 10
		TNT1 A 0 A_ChangeFlag("INVULNERABLE",0) //to prevent it being shot down as it spawns
	Idle:
		TNT1 A 10
		Loop
	Death:
		TNT1 A 0 ACS_NamedExecuteAlways("QCDE_HitboxDestroyed")
		TNT1 A 0 A_SpawnItemEx("TotemVanish",0,0,20, 0,0,0, 0,SXF_NOCHECKPOSITION)
		TNT1 A 5
		Stop
	}
}


Actor TotemAura
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	RenderStyle Add
	Scale 0.25
	Alpha 0.99
	Translation "0:255=%[0,0,0]:[0,1.2,1.2]"
	States
	{
	Spawn:
		X060 F 1 Bright A_SetScale(scaleX*1.1)
		X060 F 0 Bright A_FadeOut(0.07)
		Loop
	}
}


Actor TotemAuraLong : TotemAura
{
	States
	{
	Spawn:
		X060 F 1 Bright A_SetScale(scaleX*1.1)
		X060 F 0 Bright A_FadeOut(0.04)
		Loop
	}
}

Actor TotemExplosion
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	RenderStyle Add
	Scale 0.75
	Translation "0:255=%[0,0,0]:[0,1.2,1.2]"
	States
	{
	Spawn:
		DB54 ABCDEFG 1 Bright
		DB54 HHIIJJKKLLMMNNOOPPQQRR 1 Bright A_FadeOut(0.03)
		Stop
	}
}
Actor TotemVanish : TotemExplosion
{ 
	Scale 0.9
	States
	{
	Spawn:
		DB13 ABCDEFGHIJKLMNOPQRSTUVWXYZ 1 Bright 
		Stop
	}
}


//The FX showing if the totem is friendly or not
Actor TotemFriendlyFX : GenericPowerupFXBackground
{
	-FORCEXYBILLBOARD
	Scale 0.25
	States
	{
	Spawn:
		TOTM D 0 NoDelay A_SetUserVar(user_maxalpha,100)
		"####" "#" 0 A_SetUserVar(user_x,0)
		"####" "#" 0 A_SetUserVar(user_y,0)
		"####" "#" 0 A_SetUserVar(user_z,46)
		Goto LooperIn
	}
}

Actor TotemHostileFX : TotemFriendlyFX
{
	States
	{
	Spawn:
		TOTM C 0 NoDelay A_SetUserVar(user_maxalpha,100)
		"####" "#" 0 A_SetUserVar(user_x,0)
		"####" "#" 0 A_SetUserVar(user_y,0)
		"####" "#" 0 A_SetUserVar(user_z,46)
		Goto LooperIn
	}
}

Actor TotemToken : Token
{
	Inventory.MaxAmount 5
	Inventory.InterHubAmount 0
}


//--------------------------------------------------------
// Passive
//--------------------------------------------------------

//Implemented in the health items themselves
