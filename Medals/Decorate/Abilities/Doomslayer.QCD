// ----------------------------------------------------------------------------
//
// Doomslayer's abilities
//
// Active:	BFG
//			Created by DBT for D4T.
//			BFG sprites by Franco Tieppo
//
// Passive: TBD
//
// ----------------------------------------------------------------------------

//--------------------------------------------------------
// Active
//--------------------------------------------------------

Actor SlayerBFGProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_GiveInventory("SlayerBFG", 1, 0, GIF_SKIPOWNER)
		TNT1 A 0 A_GiveInventory("ActiveInProgress", 1, 0, GIF_SKIPOWNER)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SelectWeapon("SlayerBFG")
		Stop
	}
}

Actor SlayerBFGCleanerProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_TakeInventory("SlayerBFG", 0, TIF_SKIPOWNER)
		TNT1 A 0 A_TakeInventory("ThirdPersonPunch", 0, TIF_SKIPOWNER)
		TNT1 A 0 A_TakeInventory("ActiveInProgress", 0, TIF_SKIPOWNER)
		TNT1 A 0 A_TakeInventory("BFGstarted", 0, TIF_SKIPOWNER)
		//TNT1 A 0 SetPlayerProperty(0,0,PROP_TOTALLYFROZEN)
		Stop
	}
}

Actor BFGstarted : Inventory {}

Actor SlayerBFG : QCWeapon
{
	Weapon.Kickback 0
	Weapon.SelectionOrder 2000
	Obituary "$OB_SlayerBFG" // "%o was obliterated by %k's BFG."
	Tag "$TAG_SlayerBFG" // "BFG"
	+INVENTORY.UNDROPPABLE
	+WEAPON.MELEEWEAPON
	+WEAPON.CHEATNOTWEAPON
	+WEAPON.NOAUTOAIM
	States
	{
	Spawn:
		TNT1 A 0
		Stop
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		W07I A 10 A_WeaponReady(WRF_DISABLESWITCH|WRF_NOFIRE|WRF_NOBOB)
		TNT1 A 0 ACS_NamedExecuteAlways("CheckIfCorrectPlayer",0,2) // 2 = doom
		Goto Fire
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		TNT1 A 0 A_ZoomFactor(1)
		//TNT1 A 0 A_PlaySound("QC/Abilities/Doomslayer/BFGWeaponRaise",6,0.4,0,0.5) //played via ACS instead, to mark start of active ability
		TNT1 A 0 A_GiveInventory("BFGStarted", 1, 0, GIF_SKIPOWNER)
		QP13 X 1 A_Raise
		W07I "####################" 0 A_Raise
		Loop

	Deselect:
		TNT1 A 0 A_ZoomFactor(1.0)
		W07I A 1 A_Lower
		Loop

	//-------------------------------------------------------
	// Fire sequence
	//-------------------------------------------------------
	Fire:
		W07I A 0 A_PlaySound("QC/Abilities/Doomslayer/BFGFire",CHAN_WEAPON,1.0,false,0.8)
		W07D A 1 Bright 			A_ZoomFactor(1.000,ZOOM_NOSCALETURNING)
		W07D A 1 Bright 			A_ZoomFactor(0.998,ZOOM_NOSCALETURNING)
		W07D B 1 Bright 			A_ZoomFactor(0.996,ZOOM_NOSCALETURNING)
		W07D B 1 Bright 			A_ZoomFactor(0.994,ZOOM_NOSCALETURNING)
		W07E A 1 Bright 			A_ZoomFactor(0.990,ZOOM_NOSCALETURNING)
		W07E A 1 Bright 			A_ZoomFactor(0.986,ZOOM_NOSCALETURNING)
		W07E B 1 Bright 			A_ZoomFactor(0.981,ZOOM_NOSCALETURNING)
		W07E B 1 Bright 			A_ZoomFactor(0.975,ZOOM_NOSCALETURNING)
		W07E C 1 Bright 			A_ZoomFactor(0.969,ZOOM_NOSCALETURNING)
		W07E C 1 Bright 			A_ZoomFactor(0.961,ZOOM_NOSCALETURNING)
		W07E D 1 Bright 			A_ZoomFactor(0.953,ZOOM_NOSCALETURNING)
		W07E D 1 Bright 			A_ZoomFactor(0.944,ZOOM_NOSCALETURNING)
		W07E E 1 Bright 			A_ZoomFactor(0.935,ZOOM_NOSCALETURNING)
		W07E E 1 Bright 			A_ZoomFactor(0.925,ZOOM_NOSCALETURNING)
		W07E F 1 Bright 			A_ZoomFactor(0.914,ZOOM_NOSCALETURNING)
		W07E F 1 Bright 			A_ZoomFactor(0.902,ZOOM_NOSCALETURNING)
		W07E G 1 Bright 			A_ZoomFactor(0.889,ZOOM_NOSCALETURNING)
		W07E G 1 Bright 			A_ZoomFactor(0.876,ZOOM_NOSCALETURNING)
		W07E H 1 Bright 			A_ZoomFactor(0.863,ZOOM_NOSCALETURNING)
		W07E I 1 Bright 			A_ZoomFactor(0.833,ZOOM_NOSCALETURNING)
		W07E J 1 Bright 			A_ZoomFactor(0.801,ZOOM_NOSCALETURNING)
		W07E K 1 Bright 			A_ZoomFactor(0.766,ZOOM_NOSCALETURNING)
		W07E L 1 Bright offset(2,37)	A_ZoomFactor(0.729,ZOOM_NOSCALETURNING)
		W07E L 1 Bright offset(4,42)	A_ZoomFactor(0.710,ZOOM_NOSCALETURNING) //24

		TNT1 A 0 A_JumpIfInventory("DMToken",1,"PerformDM")
		Goto PerformSP

		PerformSP:
			"####" "#" 0 A_FireCustomMissile("BFG9000Shot",0,true,3)
			Goto EndFire
		PerformDM:
			"####" "#" 0 A_FireCustomMissile("BFG9000Shot_DM",0,true,3)
			Goto EndFire

	EndFire:
		"####" "#" 0 A_SetBlend("Green",0.25,30)
		"####" "#" 0 A_ZoomFactor(1,ZOOM_NOSCALETURNING)
		"####" "#" 0 A_SetPitch(pitch-6.0)
		W07F A 1  Bright offset( 7,47)
		W07F A 1  Bright offset(10,52)
		W07F B 2  Bright
		W07F CC 1 Bright 				A_SetPitch(pitch+0.5)
		W07F DD 1 Bright offset( 9,50) 	A_SetPitch(pitch+0.5)
		W07F EE 1 Bright offset( 8,48) 	A_SetPitch(pitch+0.5)
		W07F FF 1 Bright offset( 7,46) 	A_SetPitch(pitch+0.5)
		W07G AA 1 Bright offset( 6,44) 	A_SetPitch(pitch+0.5)
		W07G BB 2 Bright offset( 5,42) 	A_SetPitch(pitch+0.5)	//42
		W07G C 2  Bright offset( 4,40) A_WeaponReady(WRF_NOFIRE|WRF_NOBOB|WRF_ALLOWZOOM)
		W07G C 2  Bright offset( 3,38) A_WeaponReady(WRF_NOFIRE|WRF_NOBOB|WRF_ALLOWZOOM)
		W07G D 2  Bright offset( 2,36) A_WeaponReady(WRF_NOFIRE|WRF_NOBOB|WRF_ALLOWZOOM)
		W07G E 2  Bright offset( 1,32) A_WeaponReady(WRF_NOFIRE|WRF_NOBOB|WRF_ALLOWZOOM)
		W07G F 2  Bright               A_WeaponReady(WRF_NOFIRE|WRF_NOBOB|WRF_ALLOWZOOM)	//52
		W07I A 2000
		Goto Ready
	}
}

Actor BFG9000Shot
{
	Projectile
	Speed 25
	RenderStyle Add
	Alpha 0.99
	XScale 0.8
	YScale 0.6
	Radius 4
	Height 8
	Damage (random(8,14)*100)
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	+SEEKERMISSILE
	+EXTREMEDEATH
	+MTHRUSPECIES
	+DONTBLAST
	Obituary "$OB_BFG9000Shot" // "%o felt the power of argent energy at %k's fingertips."
	Decal "D4BFGLightning"
	States
	{
	Spawn:
		DB48 A 0 NODELAY A_PlaySound("QC/Weapons/BFG/BallFlying",7,0.8,true,0.6)
		DB48 AB 1 Bright Light("GreenBigPoint3")
		DB48 CD 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(30,384,0,0,384)
		DB48 EF 1 Bright Light("GreenBigPoint3")
		DB48 GH 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(30,384,0,0,384)
		DB48 IJ 1 Bright Light("GreenBigPoint3")
		DB48 KL 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(30,384,0,0,384)
		DB48 MN 1 Bright Light("GreenBigPoint3")
		DB48 OP 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(30,384,0,0,384)
		DB48 QR 1 Bright Light("GreenBigPoint3")
		DB48 ST 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(30,384,0,0,384)
		DB48 UV 1 Bright Light("GreenBigPoint3")
		DB48 WX 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(30,384,0,0,384)
		Goto Spawn+1

	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMax",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,716)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepBig",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont

	DeathCont:
		TNT1 A 0 A_PlaySound("QC/Abilities/Doomslayer/BFGExplosion",7,1,0,0.4)
		TNT1 A 0 A_Quake(4,20,0,1500)
		TNT1 A 0 A_Explode(random(12,15)*100,768,0,0,384)
		TNT1 A 12 A_SpawnItemEx("BFGImploder",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("BFGExploSmoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("BFGExploRing",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExplo",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 2
		TNT1 AAA 1 A_SpawnItemEx("BFGExploLightning",0,0,0,0,0,0,0,32)
		Stop
	}
}

Actor BFG9000Shot_DM : BFG9000Shot
{
	-MTHRUSPECIES
	-DONTBLAST
	Damage (90)		//direct hit will deal 200 dmg. It's hard to do these
	States
	{
	Spawn:
		DB48 A 0 NODELAY A_PlaySound("QC/Weapons/BFG/BallFlying",7,0.8,true,0.6)
		DB48 A 0 ACS_NamedExecuteWithResult("QCDE_BfgBallSpawned")
		Goto Idle
	Idle:
		DB48 AB 1 Bright Light("GreenBigPoint3")
		DB48 CD 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_RadiusGive("RadiationProc", 384, RGF_PLAYERS|RGF_FORCESERVERSIDE) // <-- if changing radius here, also change in
		DB48 EF 1 Bright Light("GreenBigPoint3")                 //  -- ABILITIS::QCDE_BFGRadiation::int radius
		DB48 GH 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_RadiusGive("RadiationProc", 384, RGF_PLAYERS|RGF_FORCESERVERSIDE)
		DB48 IJ 1 Bright Light("GreenBigPoint3")
		DB48 KL 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_RadiusGive("RadiationProc", 384, RGF_PLAYERS|RGF_FORCESERVERSIDE)
		DB48 MN 1 Bright Light("GreenBigPoint3")
		DB48 OP 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_RadiusGive("RadiationProc", 384, RGF_PLAYERS|RGF_FORCESERVERSIDE)
		DB48 QR 1 Bright Light("GreenBigPoint3")
		DB48 ST 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_RadiusGive("RadiationProc", 384, RGF_PLAYERS|RGF_FORCESERVERSIDE)
		DB48 UV 1 Bright Light("GreenBigPoint3")
		DB48 WX 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_RadiusGive("RadiationProc", 384, RGF_PLAYERS|RGF_FORCESERVERSIDE)
		Goto Idle

	DeathCont:
		//Medals start
		TNT1 A 0 A_GiveToTarget("AbilityTokenGiver", 1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE|GIF_SKIPOWNER)
		//Medals end
		TNT1 A 0 A_PlaySound("QC/Abilities/Doomslayer/BFGExplosion",7,1,0,0.4)
		TNT1 A 0 A_Quake(4,20,0,1500)
		TNT1 A 0 A_Explode(50,160,XF_HURTSOURCE,0)		//damage to self or enemies close
		TNT1 A 0 A_RadiusThrust(120,128,RTF_AFFECTSOURCE|RTF_THRUSTZ)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(60,512,0,0,512)	//damage to enemies far away
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 12 A_SpawnItemEx("BFGImploder",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER)
		TNT1 A 0 A_SpawnItemEx("BFGExploSmoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER)
		TNT1 A 0 A_SpawnItemEx("BFGExploRing",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExplo",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER)
		TNT1 A 2
		TNT1 AAA 1 A_SpawnItemEx("BFGExploLightning",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER)
		Stop
	}
}

Actor RadiationProc : CustomInventory
{
	+SERVERNETID
	States
	{
	Pickup:
		TNT1 A 0 ACS_NamedExecuteWithResult("QCDE_BFGRadiation")
		Stop
	}
}

Actor BFGImploder
{
	+NOINTERACTION
	+NONETID
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 1
	scale 1.5
	Translation "0:255=%[0.08,0.2,0.08]:[0.0,1.8,0.0]"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,"T1","T2")
	T1:
		DB45 A 0
		Goto EndAnim
	T2:
		DB45 A 0 A_SetScale(-1.5)
		Goto EndAnim
	EndAnim:
		"####" AC 1 Bright Light("GreenBigPoint2")
		"####" EG 1 Bright Light("GreenMediumPoint1")
		"####" IK 1 Bright Light("GreenMediumPoint2")
		"####" MO 1 Bright Light("GreenMediumPoint3")
		"####" QS 1 Bright Light("GreenSmallPoint1")
		"####" UW 1 Bright Light("GreenSmallPoint2")
		"####" Y 1 Bright Light("GreenSmallPoint3")
		//ACEGIKMOQSUWY
		Stop
	}
}
// explosion shockwave
Actor BFGExploRing
{
	+NOINTERACTION
	+NONETID
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 1.0
	scale 3.5
	Translation "0:255=%[0,0,0]:[0.0,1.8,0.0]"
	States
	{
	Spawn:
		X060 C 1 Bright A_SetScale(0.3)
		X060 C 1 Bright A_SetScale(0.9)
	Looper:
		X060 C 1 Bright A_SetScale(scalex*1.2)
		TNT1 A 0 A_FadeOut(0.1)
		Loop
	}
}

// Explosion flash
Actor BFGClassicExplo
{
	+NOINTERACTION
	+NONETID
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.99
	scale 1.5
	States
	{
	Spawn:
		DB49 A 1 Bright Light("GreenSmallPoint1") A_SetScale(scalex*1.05)
		DB49 C 1 Bright Light("GreenMediumPoint3") A_SetScale(scalex*1.05)
		DB49 G 1 Bright Light("GreenMediumPoint2") A_SetScale(scalex*1.05)
		DB49 I 1 Bright Light("GreenMediumPoint1") A_SetScale(scalex*1.05)
		DB49 I 1 Bright Light("GreenBigPoint3") A_SetScale(scalex*1.05)
		DB49 M 1 Bright Light("GreenMediumPoint1") A_SetScale(scalex*1.05)
		DB49 O 1 Bright Light("GreenMediumPoint2") A_SetScale(scalex*1.05)
		DB49 S 1 Bright Light("GreenMediumPoint3") A_SetScale(scalex*1.05)
		DB49 U 1 Bright Light("GreenSmallPoint1") A_SetScale(scalex*1.05)
		DB49 Y 1 Bright Light("GreenSmallPoint2") A_SetScale(scalex*1.05)
		Stop
	}
}

Actor BFGExploLightning
{
	+NOINTERACTION
	+NONETID
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.99
	scale 1.5
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"T1","T2","T3","T4","T5")
	T1:	DLI1 A 0
		Goto PlayOff
	T2:	DLI2 A 0
		Goto PlayOff
	T3:	DLI3 A 0
		Goto PlayOff
	T4:	DLI4 A 0
		Goto PlayOff
	T5:	DLI5 A 0
		Goto PlayOff

	PlayOff:
		"####" A 0 A_Jump(128,"FromL")
	FromA:
		"####" ABCDEFGHIJK 1 Bright
		Stop
	FromL:
		"####" LMNOPQRSTUV 1 Bright
		Stop

	}
}
Actor BFGExploSmoke
{
	+NOINTERACTION
	+NONETID
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	Scale 0.35
	Translation "0:255=%[0,0,0]:[0.0,1.8,0.0]"
	Alpha 0.99
	RenderStyle Add
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(240,4,8,12,16,20,24,28,32,26,40,44,48,52,56,60,64,68,72,76,80)
	Looper:
		X102 A 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 A 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 B 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 B 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 C 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 C 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 D 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 D 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 E 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 E 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 F 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 F 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 G 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 G 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 H 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 H 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 I 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 I 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 J 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 J 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 K 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 K 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 L 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 L 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 M 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 M 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 N 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 N 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 O 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 O 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 P 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 P 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 Q 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 Q 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 R 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 R 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 S 1 Bright A_SetScale(scalex+0.03)		TNT1 A 0 A_FadeOut(0.021)
		X102 S 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 T 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 T 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 U 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 U 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 V 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		X102 V 1 Bright A_SetScale(scalex+0.07)		TNT1 A 0 A_FadeOut(0.035)
		Loop
	}
}


Actor D4BFGFog
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	RenderStyle Add
	scale 0.4
	Translation "0:255=%[0,0,0]:[0.3,1.5,0]"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SpawnItemEx("BFGfogLightning")
	Looper:
		X110 A 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 C 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 E 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 G 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 I 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 K 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 M 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 O 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 Q 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 S 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 U 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 W 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		X110 Y 1 Bright A_SetScale(scalex*1.06)	TNT1 A 0 A_FadeOut(0.05)
		Loop
	}
}


//--------------------------------------------------------
// Passive
//--------------------------------------------------------

Actor MultiJumpFX
{
	+NOINTERACTION
	+NOGRAVITY
	+CLIENTSIDEONLY
	+NONETID
	RenderStyle Add
	XScale 0.2
	YScale 0.1
	Alpha 0.4
	Translation "0:255=%[0,0,0]:[0.3,0.9,1.5]"
	States
	{
	Spawn:
		X125 ABCDEFGHIJKL 1 Bright
		Stop
	}
}

Actor PowerBfgSlow1 : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlow2 : PowerSpeed	{ Speed 1.0 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.2  } // <-- this one doesn't slow, it's to indicate armor melt
Actor PowerBfgSlow3 : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlow4 : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlow5 : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlow6 : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlow7 : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlow8 : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlow9 : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlowA : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlowB : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlowC : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlowD : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlowE : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }
Actor PowerBfgSlowF : PowerSpeed	{ Speed 0.9 Inventory.Icon "" +POWERSPEED.NOTRAIL powerup.color "00 FF 00", 0.05 }


Actor BfgSlow1 : PowerupGiver
{
	powerup.duration 35
	powerup.type "BfgSlow1"
	powerup.color "00 FF 00", 0.05
	-COUNTITEM
	+INVENTORY.AUTOACTIVATE
	+SERVERNETID
}
Actor BfgSlow2 : BfgSlow1 { Powerup.Type "BfgSlow2" powerup.color "00 FF 00", 0.2 } // <-- special one, indicates armor melt
Actor BfgSlow3 : BfgSlow1 { Powerup.Type "BfgSlow3" }
Actor BfgSlow4 : BfgSlow1 { Powerup.Type "BfgSlow4" }
Actor BfgSlow5 : BfgSlow1 { Powerup.Type "BfgSlow5" }
Actor BfgSlow6 : BfgSlow1 { Powerup.Type "BfgSlow6" }
Actor BfgSlow7 : BfgSlow1 { Powerup.Type "BfgSlow7" }
Actor BfgSlow8 : BfgSlow1 { Powerup.Type "BfgSlow8" }
Actor BfgSlow9 : BfgSlow1 { Powerup.Type "BfgSlow9" }
Actor BfgSlowA : BfgSlow1 { Powerup.Type "BfgSlowA" }
Actor BfgSlowB : BfgSlow1 { Powerup.Type "BfgSlowB" }
Actor BfgSlowC : BfgSlow1 { Powerup.Type "BfgSlowC" }
Actor BfgSlowD : BfgSlow1 { Powerup.Type "BfgSlowD" }
Actor BfgSlowE : BfgSlow1 { Powerup.Type "BfgSlowE" }
Actor BfgSlowF : BfgSlow1 { Powerup.Type "BfgSlowF" }
