// --------------------------------------------------------------------------
//
// Lightning Gun
// Weapon and Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

const int LGDmgSPMin = 10;
const int LGDmgSPMax = 20;
const int LGDmgDM = 7;

Actor LightningGunPickup : CustomInventory 15606
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "Lightning Gun"
	Height 48
	Radius 20
	Scale 0.62
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCLightningGun" // "Picked up a Lightning Gun."
	Tag "$TAG_QCLightningGun" // "Lightning Gun"
	Inventory.RespawnTics 35
	States
	{
	Spawn:
		TNT1 A 1
		QCG6 Y 35 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		QCG6 Z -1 Bright ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",18,args[4])
		Stop
	Idle:
		QCG6 Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",17,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCLightningGun")
		TNT1 A 0
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 A_GiveInventory("QCLightningGunDM")
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("QCLightningGun")
		TNT1 A 0
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCLightningGun",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCLightningGun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_TakeInventory("QCLightningGun")
		TNT1 A 0 A_JumpIfInventory("QCLightningGunDM",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCLightningGunDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto HideDoomish+1
	}
}

Actor QCLightningGun : QCWeapon
{
	Height 48
	+FLOORCLIP
	+INVENTORY.IGNORESKILL
	Weapon.AmmoType1 "QCCoilAmmo"
	Weapon.AmmoGive1 50
	Weapon.AmmoUse1 1
	Weapon.SelectionOrder 1300
	Weapon.KickBack 200
	Obituary "$OB_QCLightningGun" // "%o was electrocuted by %k."
	Inventory.PickupMessage "$GOT_QCLightningGun" // "Picked up a Lightning Gun."
	Tag "$TAG_QCLightningGun" // "Lightning Gun"
	Scale 0.62
	States
	{
	Spawn:
		QCG6 Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCG6 ZZZZZZZZZZZZZZZ 35 Bright
		Stop
	Idle:
		QCG6 Y -1
		Wait

	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		TNT1 A 0 A_PlaySound("QC/Weapons/LightningGun/Idle", CHAN_WEAPON, 0.45, 1, 1.25)
		QCG8 A 0 A_JumpIfInventory("DMToken",1,"AltFireReady")
		QCG8 A 0 A_JumpIfInventory("LG_UG_Static",1,"AltFireReady")
		QCG6 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG6 AABB 1 A_WeaponReady(WRF_NOSECONDARY)
		QCG6 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG6 CCDD 1 A_WeaponReady(WRF_NOSECONDARY)
		QCG6 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG6 EEFF 1 A_WeaponReady(WRF_NOSECONDARY)
		Goto Ready+1

	AltFireReady:
		TNT1 A 0 A_PlaySound("QC/Weapons/LightningGun/Idle", CHAN_WEAPON, 0.65, 1)
		QCG6 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG6 AABB 1 A_WeaponReady
		QCG6 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG6 CCDD 1 A_WeaponReady
		QCG6 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG6 EEFF 1 A_WeaponReady
		Loop

		ReadyCantFire:
			QCG6 M 0 A_StopSound(CHAN_WEAPON)
			QCG6 M 0 A_StopSound(6)
			"####" "#" 0 A_ZoomFactor(1.0)
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			QCG6 M 4 A_WeaponReady(WRF_NOFIRE)
			QCG6 M 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready

	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		QCG6 A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		QCG6 A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		QCG6 A 0 A_StopSound(CHAN_WEAPON) // Looper
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		QCG6 A 1 A_Raise
		TNT1 AAA 0 A_Raise
		Loop

	Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON)
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_StopSound(7)
		//Medals start
		"####" "#" 0 A_GiveInventory("ComboSwitchLGGiver")
		//Medals end
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:
		QCG6 A 1 A_Lower
		TNT1 AAA 0 A_Lower
		Loop


	//-----------------------
	// Fire Seq
	//-----------------------
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"FireQuadSound")
		Goto FireNormalSound

		FireNormalSound:
			TNT1 A 0 A_PlaySound("QC/Weapons/LightningGun/FireStartLoop", CHAN_WEAPON, 1.0, 1)
			Goto FirePostSoundCheck
		FireQuadSound:
			TNT1 A 0 A_PlaySound("QC/Weapons/LightningGun/Quad/FireStartLoop", CHAN_WEAPON, 1.0, 1)
			Goto FirePostSoundCheck

	FirePostSoundCheck:
			QCG6 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
			QCG6 A 0 A_Jump(255,"CheckSPDM")		//sets regular lg, visible
			Goto CheckSPDM
		CheckIfUpgrade:
			QCG6 A 0 A_JumpIfInventory("LG_UG_Static",1,"LGSet100Purp")
			QCG6 A 0 A_Jump(255,"CheckSPDM")		//sets regular lg, visible
			Goto CheckSPDM

		LGSet100Purp:
			QCGF A 0	//sets purple, visible
			"####" A 0 A_AlertMonsters
			Goto HoldPurple


	CheckSPDM:
		QCG6 A 0 A_AlertMonsters
		"####" A 0 A_JumpIfInventory("DMToken",1,"DMCheckTransparency")
		Goto Hold

		//*******************************
		// Coop: with 3rd person beam
		Hold:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 Offset(0,32)
			"####" A 0 A_GunFlash
			"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactor",-4)
			"####" G 1 Bright A_FireBullets(0,0,1,random(LGDmgSPMin,LGDmgSPMax),"LGPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO,768)
			"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactor",-4)
			"####" A 0 Offset(0,32)
			"####" H 1 Bright
			"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"LGSet100Purp")
			"####" A 0 A_ReFire("Hold2")
			Goto FireEnd
		Hold2:
			"####" A 0 Offset(0,32)
			"####" A 0 A_GunFlash
			"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactor",-4)
			"####" I 1 Bright A_FireBullets(0,0,1,random(LGDmgSPMin,LGDmgSPMax),"LGPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO,768)
			"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactor",-4)
			"####" A 0 Offset(0,32)
			"####" J 1 Bright
			"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"LGSet100Purp")
			"####" A 0 A_ReFire("Hold3")
			Goto FireEnd
		Hold3:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 Offset(0,32)
			"####" A 0 A_GiveInventory("LGcharge",1,0,GIF_SKIPOWNER)
			"####" A 0 A_GunFlash
			"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactor",-4)
			"####" K 1 Bright A_FireBullets(0,0,1,random(LGDmgSPMin,LGDmgSPMax),"LGPuff",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO,768)
			"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactor",-4)
			"####" A 0 Offset(0,32)
			"####" L 1 Bright
			"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"LGSet100Purp")
			"####" A 0 A_ReFire("Hold")
			Goto FireEnd

			//*******************************
			HoldPurple:
				"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
				"####" A 0 Offset(0,32)
				"####" A 0 A_GunFlash
				"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactorPurple",-4)
				"####" G 1 Bright A_FireBullets(0,0,1,0,"LGPuffStatic",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO,768)
				"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactorPurple",-4)
				"####" A 0 Offset(0,32)
				"####" H 1 Bright
				"####" A 0 A_JumpIfInventory("WeaponZoomedToken",-1,"CheckSPDM")
				"####" A 0 A_ReFire("HoldPurple2")
				Goto FireEnd
			HoldPurple2:
				"####" A 0 Offset(0,32)
				"####" A 0 A_GunFlash
				"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactorPurple",-4)
				"####" I 1 Bright A_FireBullets(0,0,1,0,"LGPuffStatic",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO,768)
				"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactorPurple",-4)
				"####" A 0 Offset(0,32)
				"####" J 1 Bright
				"####" A 0 A_JumpIfInventory("WeaponZoomedToken",-1,"CheckSPDM")
				"####" A 0 A_ReFire("HoldPurple3")
				Goto FireEnd
			HoldPurple3:
				"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
				"####" A 0 Offset(0,32)
				"####" A 0 A_GiveInventory("LGcharge",1,0,GIF_SKIPOWNER)
				"####" A 0 A_GunFlash
				"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactorPurple",-4)
				"####" K 1 Bright A_FireBullets(0,0,1,0,"LGPuffStatic",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO,768)
				"####" A 0 A_RailAttack(0,5,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,30,1.0,"LGworldactorPurple",-4)
				"####" A 0 Offset(0,32)
				"####" L 1 Bright
				"####" A 0 A_JumpIfInventory("WeaponZoomedToken",-1,"CheckSPDM")
				"####" A 0 A_ReFire("HoldPurple")
				Goto FireEnd


		//*******************************
		// Deathmatch: rebalance + 3rd person beam
		DMCheckTransparency:
			TNT1 A 0 A_JumpIfInventory("LGTransparency",1,"LGSet100DM")
			QCA6 A 0 A_Jump(255,"DMHold")			//sets invisble beam
			Goto DMHold

		LGSet100DM:	//same as the non-DM state, but leads to DMHold states
			QCG6 A 0 A_Jump(255,"DMHold")		//sets regular lg, visible
			Goto DMHold

		DMHold:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 Offset(0,32)
			"####" A 0 A_GunFlash("DMFlash")
			"####" A 0 A_RailAttack(0,0,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,25,1.0,"LGworldactor",-4)
			"####" G 1 Bright A_FireBullets(0,0,1,LGDmgDM,"LGPuff_DM",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO,768)
			"####" A 0 A_RailAttack(0,0,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,25,1.0,"LGworldactor",-4)
			"####" A 0 Offset(0,32)
			"####" H 1 Bright
			//Medals start
			"####" A 0 A_GiveInventory("LGFiredGiver",1)
			//Medals end
			"####" A 0 A_ReFire("DMHold2")
			Goto FireEnd
		DMHold2:
			"####" A 0 Offset(0,32)
			"####" A 0 A_GunFlash("DMFlash")
			"####" A 0 A_RailAttack(0,0,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,25,1.0,"LGworldactor",-4)
			"####" I 1 Bright A_FireBullets(0,0,1,LGDmgDM,"LGPuff_DM",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO,768)
			"####" A 0 A_RailAttack(0,0,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,25,1.0,"LGworldactor",-4)
			"####" A 0 Offset(0,32)
			"####" J 1 Bright
			//Medals start
			"####" A 0 A_GiveInventory("LGFiredGiver",1)
			//Medals end
			"####" A 0 A_ReFire("DMHold3")
			Goto FireEnd
		DMHold3:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 Offset(0,32)
			"####" A 0 A_GunFlash("DMFlash")
			"####" A 0 A_RailAttack(0,0,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,25,1.0,"LGworldactor",-4)
			"####" K 1 Bright A_FireBullets(0,0,1,LGDmgDM,"LGPuff_DM",FBF_NORANDOM|FBF_NORANDOMPUFFZ|FBF_USEAMMO,768)
			"####" A 0 A_RailAttack(0,0,0,None,"White",RGF_SILENT|RGF_FULLBRIGHT|RGF_NOPIERCING,0,"None",0,0,768,1,25,1.0,"LGworldactor",-4)
			"####" A 0 Offset(0,32)
			"####" L 1 Bright
			//Medals start
			"####" A 0 A_GiveInventory("LGFiredGiver",1)
			//Medals end
			"####" A 0 A_ReFire("DMHold")
			Goto FireEnd


	FireEnd:
		TNT1 A 0 A_JumpIfInventory("LG_UG_Residue",1,"CheckExitCharges")
		TNT1 A 0 A_TakeInventory("LGcharge",32767)	//resets any charge if the threshold wasnt reached (or no upgrade)
		TNT1 A 0 A_PlaySound("QC/Weapons/LightningGun/FireEnd", CHAN_WEAPON, 1.0, 0)
		Goto Ready

		CheckExitCharges:
			TNT1 A 0 A_JumpIfInventory("LGcharge",5,"DoExitLightning")
			Goto FireEnd+1

		DoExitLightning:
			TNT1 A 0 A_FireCustomMissile("LightningResidue")
			TNT1 A 0 A_TakeInventory("LGcharge",32767)
			Goto FireEnd+1


	Flash:
		TNT1 C 1 A_Light2
		TNT1 C 0 A_Light0
		Stop

	DMFlash:	//doesnt do any checks regarding upgrades
		TNT1 C 1 A_Light2
		TNT1 C 0 A_Light0
		Stop
	}
}


Actor LGPuff
{
	+CLIENTSIDEONLY +NONETID
	Radius 10
	+NOINTERACTION +NOTONAUTOMAP
	+ALWAYSPUFF
	+PUFFGETSOWNER
	+PUFFONACTORS
	Scale 0.35
	Renderstyle Add
	Obituary "$OB_LGPuff" // "%o was electrified by %k." // variation line
	+FORCEXYBILLBOARD
	+MTHRUSPECIES
	+DONTSPLASH
	Decal "LGDecal"
	DamageType "Electric"
	States
	{
	Spawn:
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		//Medals start
		TNT1 A 0 A_GiveToTarget("LGDamageToken",1)
		TNT1 A 0 A_GiveToTarget("LGHitGiver",1)
		//Medals end
		Goto Choose
	Crash:
		TNT1 A 0 A_JumpIf(waterlevel > 0, "Water")
	Choose:
		TNT1 A 100
		Stop
	Water:
		TNT1 A 0 A_SpawnItemEx("WaterLightningSparks", 0,0,0, 0,0,0,0, SXF_NOCHECKPOSITION,160)
		TNT1 A 100 A_Explode(waterlevel * 2, waterlevel * 16)
		Stop
	}
}

Actor WaterLightningSparks
{
	Translation "0:255=%[0,0,0]:[0.8,1.2,1.6]"
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY
	+DONTBLAST
	var int user_counter;
	RenderStyle Add
	Alpha 0.99
	Scale 0.35
	States
	{
	Spawn:
	Decider:
		TNT1 A 0 NODELAY A_Jump(256,"T1","T2","T3","T4","T5")

		T1:	DLI1 A 0
			Goto PlayOff
		T2:	DLI2 A 0
			Goto PlayOff
		T3:	DLI3 A 0
			Goto PlayOff
		T4:	DLI4 A 0
			Goto PlayOff
		T5:	DLI5 A 0
			Goto PlayOff

	PlayOff:
		"####" A 0 A_Jump(128,"FromL")

		FromA:
			"####" ABCDE 1 Bright
			"####" FGHIJK 1 Bright
			Goto Destroy
		FromL:
			"####" LMNOP 1 Bright
			"####" QRSTUV 1 Bright
			Goto Destroy


	Destroy:
		TNT1 A 5
		Stop

	}
}

Actor LGPuff_DM : LGPuff { -MTHRUSPECIES }

Actor LGworldactor
{
	+CLIENTSIDEONLY +NONETID
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+INVISIBLE_TO_TARGET
	RenderStyle Add
	Alpha 0.68
	Scale 0.28
	Translation "0:255=%[0,0,0]:[0.8,1.2,1.6]"
	States
	{
	Spawn:
		LGWR Y 2 Bright
		Stop
	}
}



//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Weapon tokens
//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
Actor LG_UG_Static 		: Token {}
Actor LG_UG_Residue		: Token {}


//-----------------------
// Upgrade "electric residue"
Actor LGcharge : Inventory
{
	+SERVERNETID
	Inventory.MaxAmount 10
	Inventory.InterHubAmount 0
}

Actor LightningResidue : FastProjectile
{
	Radius 2
	Height 2
	var int user_counter;
	Speed 768
	ReactionTime 1
	+SEEKERMISSILE
	+DONTSPLASH
	+DONTBLAST
	RenderStyle None
	States
	{
	Spawn:
		PLAY A 1 NODELAY
		PLAY A 0 A_Countdown	//dies after 1 tic = 768mu
		Loop
	Death:
		PLAY A 0 A_PlaySound("QC/Weapons/LightningGun/ElectroLoop",CHAN_5,1,1,2.5)
	Looper:
		PLAY A 0 A_JumpIf(user_counter>35,"Vanish")
		PLAY A 0 A_SetUserVar(user_counter,user_counter+1)
		PLAY A 4 Light("BlueMediumFlicker3") A_SpawnItemEx("LGResidueAttackerFX", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_NOUNLAGGED)
		Loop
	Vanish:
		PLAY A 2 Light("BlueMediumPoint2") A_PlaySound("QC/Weapons/LightningGun/ElectroStop",CHAN_5,1,0,2.5)
		PLAY A 2 Light("BlueMediumPoint3")
		PLAY A 2 Light("BlueSmallPoint1")
		PLAY A 2 Light("BlueSmallPoint2")
		PLAY A 2 Light("BlueSmallPoint3")
		Stop
	}

}

Actor LGResidueAttackerFX
{
	Projectile
	Radius 1
	Height 1
	+SEEKERMISSILE
	Translation "0:255=%[0,0,0]:[0.8,1.2,1.6]"
	+NOINTERACTION +NOTONAUTOMAP
	+DONTSPLASH
	+DONTBLAST
	+SERVERNETID
	var int user_counter;
	RenderStyle Add
	Alpha 0.99
	Scale 0.35
	ProjectileKickBack 0
	DamageType "Electric"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SeekerMissile(90,90,1,256)
		TNT1 A 0 A_JumpIfTargetInLOS("Railer",0,JLOSF_PROJECTILE,256)
		Goto DecideFX
	Railer:
		TNT1 A 0 A_FaceTracer
		TNT1 A 0 A_JumpIfTargetInLOS("ShooterInWay",5,0)
		TNT1 A 0 A_CustomRailgun(random(3,20),0,none,"SkyBlue",RGF_SILENT|RGF_FULLBRIGHT,0,6,"LGResiduePuff", 0,0,256, 6,1.0,1,"none",-5)
		Goto DecideFX
	ShooterInWay:
		TNT1 A 0
		Goto DecideFX


	DecideFX:
		TNT1 A 0 A_Jump(256,"T1","T2","T3","T4","T5")

		T1:	DLI1 A 0
			Goto PlayOff
		T2:	DLI2 A 0
			Goto PlayOff
		T3:	DLI3 A 0
			Goto PlayOff
		T4:	DLI4 A 0
			Goto PlayOff
		T5:	DLI5 A 0
			Goto PlayOff

	PlayOff:
		"####" A 0 A_Jump(128,"FromL")

		FromA:
			"####" ABCDE 1 Bright
			"####" FGHIJK 1 Bright
			Goto Destroy
		FromL:
			"####" LMNOP 1 Bright
			"####" QRSTUV 1 Bright
			Goto Destroy

	Destroy:
		TNT1 A 1
		Stop
	}
}
Actor LGResiduePuff
{
	+NOINTERACTION
	DamageType "NoDamageToPlayers"
	ProjectileKickBack 0
	+DONTSPLASH
	DamageType "Electric"
	States
	{
	Spawn:
		TNT1 A 10
		Stop
	}
}


//-----------------------
// Upgrade "static field"
Actor LGPuffStatic
{
	Projectile
	+SEEKERMISSILE
	+NOINTERACTION
	+PUFFGETSOWNER
	+PUFFONACTORS
	RenderStyle Add
	+FORCEXYBILLBOARD
	+NODAMAGETHRUST
	//-MTHRUSPECIES
	+DONTSPLASH
	+DONTBLAST
	+NONETID
	Alpha 0.99
	Translation "0:255=%[0,0,0]:[0.9,0.74,1.5]"
	Scale 0.4
	Obituary "$OB_LGPuffStatic" // "%o was conductive for %k."
	DamageType "NoDamageToPlayers"
	DamageType "Electric"
	ProjectileKickBack 0
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_ChangeFlag("NOBLOCKMAP",0)
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("Sounder",360,JLOSF_PROJECTILE,140)
		Goto ContinueNormal

		Sounder:
			TNT1 A 0 A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			Goto ContinueNormal

	ContinueNormal:
		TNT1 A 0 A_Explode(12,100,66)
		TNT1 A 0 A_Jump(256,"Type1a","Type1b","Type2a","Type2b","Type3a","Type3b","Type4a","Type4b","Type5a","Type5b")

	Type1a:
		DLI1 A 0
		Goto Perform1
	Type1b:
		DLI1 L 0
		Goto Perform2
	Type2a:
		DLI2 A 0
		Goto Perform1
	Type2b:
		DLI2 L 0
		Goto Perform2
	Type3a:
		DLI3 A 0
		Goto Perform1
	Type3b:
		DLI3 L 0
		Goto Perform2
	Type4a:
		DLI4 A 0
		Goto Perform1
	Type4b:
		DLI4 L 0
		Goto Perform2
	Type5a:
		DLI5 A 0
		Goto Perform1
	Type5b:
		DLI5 L 0
		Goto Perform2

	Perform1:
		"####" BDHI 1 Bright
		Stop
	Perform2:
		"####" MOST 1 Bright
		Stop
	}
}

Actor LGworldactorPurple : LGworldactor
{
	Translation "0:255=%[0,0,0]:[1.2,0.84,1.8]"
}


// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCLightningGunDM : QCLightningGun
{
	+WEAPON.NOAUTOAIM
	+CHEATNOTWEAPON
	Tag "$TAG_QCLightningGun" // "Lightning Gun (DM)"
	Weapon.AmmoGive1 100
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 10
}
