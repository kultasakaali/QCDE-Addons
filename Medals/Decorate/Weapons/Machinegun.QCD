// --------------------------------------------------------------------------
// --------------------------------------------------------------------------
//
// Machinegun
// Weapon sprite by Franco Tieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------
// --------------------------------------------------------------------------
// Single player can fire 2 times per one ammo point (thing is reaaally weak)
//
// --------------------------------------------------------------------------

// This pickup is only for consistency -- it never gives weapon.
Actor MachinegunPickup : CustomInventory
{
	Scale 0.5
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCMachinegun" // "Picked up the Machinegun."
	Tag "$TAG_QCMachinegun" // "Machinegun"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		Stop
	Idle:
		TNT1 A 0
		Stop
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 //A_GiveInventory("QCMachinegun")
		TNT1 A 0 
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 //A_GiveInventory("QCMachinegunDM")
		TNT1 A 0
		TNT1 A 0 //A_TakeInventory("QCMachinegun")
		TNT1 A 0
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCMachinegun",1,"Stay")
		TNT1 A 0 //A_SpawnItemEx("QCMachinegun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_TakeInventory("QCMachinegun")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunDM",1,"Stay")
		TNT1 A 0 //A_SpawnItemEx("QCMachinegunDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto HideDoomish+1
	}
}



Actor QCMachinegun : QCWeapon
{
	Weapon.SwaySpeed 3.4
	Weapon.Kickback 20
	Weapon.SelectionOrder 2000
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType "QCBulletAmmo"
	Obituary "$OB_QCMachinegun" // "%o was riddled with holes by %k."
	+INVENTORY.UNDROPPABLE
	Inventory.PickupMessage "$GOT_QCMachinegun" // "Picked up the Machinegun."
	Tag "$TAG_QCMachinegun" // "Machinegun"
	States
	{
 	Spawn:
		TNT1 A 0 NODELAY A_ChangeFlag("NOBLOCKMAP",1)	//in DM, if a player drops this weapon, cant be picked
		TNT1 A 3	//and disappears
		Stop
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		QCG2 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG2 AAA 1 A_WeaponReady
		Loop
		
		ReadyCantFire:
			QCG2 A 0 A_StopSound(5)
			QCG2 A 0 A_StopSound(6)
			"####" "#" 0 A_ZoomFactor(1.0)
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			QCG2 A 4 A_WeaponReady(WRF_NOFIRE)
			QCG2 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		QCG2 A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		QCG2 A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" "#" 0 A_StopSound(5) //stops any weapon looping sound
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		QCG2 A 1 A_Raise
		"####" "###" 0 A_Raise
		Loop
		
	Deselect:
		QCGB A 0 A_StopSound(5) //stops the looping fire sound
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:	
		QCG2 A 1 A_Lower
		"####" "###" 0 A_Lower
		Loop
		
	//-----------------------
	// Fire Seq
	//-----------------------
	Fire:
		TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"FireQuadSound")
		Goto FireNormalSound
		
		FireNormalSound:
			QCG2 A 0 A_PlaySound("QC/Weapons/Machinegun/FireStartLoop",5,1,1)
			Goto FirePostSoundCheck
		FireQuadSound:
			QCG2 A 0 A_PlaySound("QC/Weapons/Machinegun/Quad/FireStartLoop",5,1,1)
			Goto FirePostSoundCheck
			
	FirePostSoundCheck:	
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"DM_FireLooper1")
		Goto FireLooper
		
		//SP fire
		FireLooper:
			QCG2 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCG2 B 0 A_GunFlash("SP_Flash")
			QCG2 B 1 Offset(2,34) Bright 
			QCG2 B 1 Offset(4,35) Bright
			QCG2 A 1 Offset(3,34)
			QCG2 A 1 Offset(2,33)
			QCG2 A 1 Offset(1,32) A_ReFire("FireLooperNA")
			Goto EndFireLooper
		FireLooperNA: //this one is "No (use of) Ammo"
			QCG2 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCG2 B 0 A_GunFlash("SP_FlashNA")
			QCG2 B 0 A_Jump(255,"NAT1","NAT2")
		NAT1:
			QCG2 D 1 Offset(2,34) Bright 
			QCG2 D 1 Offset(4,35) Bright
			Goto EndLooperNa
		NAT2:
			QCG2 C 1 Offset(2,34) Bright 
			QCG2 C 1 Offset(4,35) Bright
			Goto EndLooperNa
		EndLooperNa:
			QCG2 A 1 Offset(3,34)
			QCG2 A 1 Offset(2,33)
			QCG2 A 1 Offset(1,32) A_ReFire("FireLooper")
			Goto EndFireLooper
			
			
		//DM fire
		DM_FireLooper1:
			QCG2 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCG2 B 0 A_GunFlash("DM_Flash")
			QCG2 B 1 Offset(2,34) Bright 
			QCG2 B 1 Offset(4,35) Bright
			QCG2 A 1 Offset(3,34)
			QCG2 A 1 Offset(2,33)
			QCG2 A 1 Offset(1,32) A_ReFire("DM_FireLooper2")
			Goto EndFireLooper
		DM_FireLooper2:
			QCG2 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCG2 B 0 A_GunFlash("DM_Flash")
			QCG2 C 1 Offset(2,34) Bright 
			QCG2 C 1 Offset(4,35) Bright
			QCG2 A 1 Offset(3,34)
			QCG2 A 1 Offset(2,33)
			QCG2 A 1 Offset(1,32) A_ReFire("DM_FireLooper3")
			Goto EndFireLooper
		DM_FireLooper3:
			QCG2 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCG2 B 0 A_GunFlash("DM_Flash")
			QCG2 D 1 Offset(2,34) Bright 
			QCG2 D 1 Offset(4,35) Bright
			QCG2 A 1 Offset(3,34)
			QCG2 A 1 Offset(2,33)
			QCG2 A 1 Offset(1,32) A_ReFire("DM_FireLooper1")
			Goto EndFireLooper
			
	EndFireLooper:
		QCG2 A 0 A_PlaySound("QC/Weapons/Machinegun/FireEnd",5,1,0)
		QCG2 EEAAEEEAAAAEEEE 1 A_WeaponReady(WRF_NOBOB) 
		Goto Ready
		
	//-----------------------
	// Attack doers
	//-----------------------
	SP_Flash: 
		TNT1 A 0 A_FireBullets(1.75,1.75,1,random(3,5),"QCBulletPuff",FBF_NORANDOM|FBF_USEAMMO)
		TNT1 A 0 A_FireCustomMissile("BulletSmallTracer",frandom(1.75,-1.75),false,8,0,0,frandom(1.75,-1.75))
		Goto EndFlash
	SP_FlashNA:  //this one is "No (use of) Ammo"
		TNT1 A 0 A_FireBullets(1.75,1.75,1,random(3,5),"QCBulletPuff",FBF_NORANDOM)
		TNT1 A 0 A_FireCustomMissile("BulletSmallTracer",frandom(1.75,-1.75),false,8,0,0,frandom(1.75,-1.75))
		Goto EndFlash
		
	DM_Flash:
		TNT1 A 0 A_FireBullets(1.75,1.75,1,9,"QCHMGBulletPuff",FBF_NORANDOM|FBF_USEAMMO)
		TNT1 A 0 A_FireCustomMissile("BulletSmallTracer",frandom(1.75,-1.75),false,7,0,0,frandom(1.75,-1.75))
		Goto EndFlash
			
		EndFlash:
			TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
			TNT1 A 0 A_SetBlend("Yellow",0.03,3)
			TNT1 A 0 A_FireCustomMissile("MachinegunSmokeSpawner",0, false,6,2)
			TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"EndFlashNoRecoil")
			TNT1 A 1 A_SetPitch(Pitch-0.6)
			TNT1 AA 1 A_SetPitch(Pitch+0.3)
			Stop
		EndFlashNoRecoil:
			TNT1 A 1 
			Stop

	//-----------------------
	// Fire Seq / Zoom
	//-----------------------
	AltFire:
		QCG2 A 0 A_JumpIfInventory("DMToken",1,"StartZoomChecks")	//in Deathmatch, allow zoom checks by Default
	NoZoom:
		"####" A 2 A_WeaponReady(WRF_NOSECONDARY)	//dummy state in case player is holding "alt fire" all along
		"####" A 0 A_Refire("NoZoom")
		Goto Ready
	StartZoomChecks:
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken", 1, "ZoomOut")
	ZoomIn:
		"####" AAA 1
		"####" "#" 0 A_ZoomFactor(1.8)
		"####" "#" 0 A_GiveInventory("WeaponZoomedToken",1)
	ZoomInHeld:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 1 
		"####" "#" 1 A_Refire("ZoomInHeld")
		Goto Ready
		
		ZoomOut:
			"####" "#" 0 A_ZoomFactor(1.0) 
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		ZoomOutHeld:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 1 
			"####" "#" 1 A_Refire("ZoomOutHeld")
			Goto Ready
	}
}

Actor MachinegunSmokeSpawner
{
	Radius 1
	Height 1
	Speed 14
	PROJECTILE
	+NOTIMEFREEZE
	+CLIENTSIDEONLY +NONETID
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Die
		Goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("MachinegunSmoke",0,random(1,-1),0,0,0,frandom(0.1,0.25),0,32)
		TNT1 A 2
		Stop
	}
}

Actor MachinegunSmoke
{
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY +NONETID
	Alpha 0.6
	Scale 0.03
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"Type1","Type2")	//to define which graphic will use
	Type1:
		X201 E 0
		Goto "Continue"
	Type2:
		X201 F 0
		Goto "Continue"
	Continue:
		"####" "#" 0 A_Jump(160,2,4,6)
		"####" "#" 0
		"####" "#" 0 A_SetScale(0.03)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.033)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.036)
		"####" "#" 2 A_FadeOut(0.035)
	Looper:
		"####" "#" 0 A_SetScale(ScaleX+0.003)
		"####" "#" 2 A_FadeOut(0.035)
		Loop
	}
}



// --------------------------------------------------------------------------
//	For competitive DM, weapons don't autoaim
// --------------------------------------------------------------------------

Actor QCMachinegunDM : QCMachinegun
{	
	+WEAPON.NOAUTOAIM	
	+CHEATNOTWEAPON
	Tag "$TAG_QCMachinegun" // "Machinegun (DM)"
	Weapon.AmmoGive 100
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 10
}
