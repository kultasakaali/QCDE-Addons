// --------------------------------------------------------------------------
// --------------------------------------------------------------------------
//
// Machinegun
// Weapon sprite by Franco Tieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------
// --------------------------------------------------------------------------
// Single player can fire 2 times per one ammo point (thing is reaaally weak)
//
// --------------------------------------------------------------------------

// This pickup is only for consistency -- it never gives weapon.
Actor MachinegunPickup : CustomInventory
{
	Scale 0.5
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCMachinegun" // "Picked up the Machinegun."
	Tag "$TAG_QCMachinegun" // "Machinegun"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		Stop
	Idle:
		TNT1 A 0
		Stop
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 //A_GiveInventory("QCMachinegun")
		TNT1 A 0
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 //A_GiveInventory("QCMachinegunDM")
		TNT1 A 0
		TNT1 A 0 //A_TakeInventory("QCMachinegun")
		TNT1 A 0
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCMachinegun",1,"Stay")
		TNT1 A 0 //A_SpawnItemEx("QCMachinegun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_TakeInventory("QCMachinegun")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunDM",1,"Stay")
		TNT1 A 0 //A_SpawnItemEx("QCMachinegunDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto HideDoomish+1
	}
}



Actor QCMachinegun : QCWeapon
{
	Weapon.SwaySpeed 3.4
	Weapon.Kickback 20
	Weapon.SelectionOrder 2000
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType "QCBulletAmmo"
	Obituary "$OB_QCMachinegun" // "%o was riddled with holes by %k."
	+INVENTORY.UNDROPPABLE
	Inventory.PickupMessage "$GOT_QCMachinegun" // "Picked up the Machinegun."
	Tag "$TAG_QCMachinegun" // "Machinegun"
	States
	{
 	Spawn:
		TNT1 A 0 NODELAY A_ChangeFlag("NOBLOCKMAP",1)	//in DM, if a player drops this weapon, cant be picked
		TNT1 A 3	//and disappears
		Stop
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 6, "Ready.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "Ready.ETQW")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "Ready.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 3, "Ready.Quake3")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 2, "Ready.Quake2")
	Ready.Default:
		QCG2 A 0
		Goto ReadyCont
	Ready.Quake2:
		Q2G2 A 0
		Goto ReadyCont
	Ready.Quake3:
		Q3G2 A 0
		Goto ReadyCont
	Ready.Quake4:
		Q4G2 A 0
		Goto ReadyCont
	Ready.ETQW:
		QWG2 A 0
		Goto ReadyCont
	Ready.QCDE1:
		Q0G2 A 0
		Goto ReadyCont

	ReadyCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" AAA 1 A_WeaponReady
		Loop

		ReadyCantFire:
			"####" A 0 A_StopSound(CHAN_WEAPON)
			"####" A 0 A_StopSound(6)
			"####" A 0 A_ZoomFactor(1.0)
			"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			"####" A 4 A_WeaponReady(WRF_NOFIRE)
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 6, "Select.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "Select.ETQW")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "Select.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 3, "Select.Quake3")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 2, "Select.Quake2")
	Select.Default:
		QCG2 A 0
		Goto SelectCont
	Select.Quake2:
		Q2G2 A 0
		Goto SelectCont
	Select.Quake3:
		Q3G2 A 0
		Goto SelectCont
	Select.Quake4:
		Q4G2 A 0
		Goto SelectCont
	Select.ETQW:
		QWG2 A 0
		Goto SelectCont
	Select.QCDE1:
		Q0G2 A 0
		Goto SelectCont

	SelectCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		"####" A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		"####" A 1 A_Raise
		"####" AAA 0 A_Raise
		Loop

	Deselect:
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops the looping fire sound
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:
		"####" A 1 A_Lower
		"####" AAA 0 A_Lower
		Loop

	//-----------------------
	// Fire Seq
	//-----------------------
	Fire:
		"####" A 0 A_JumpIfInventory("PowerQuad",1,"FireQuadSound")
		Goto FireNormalSound

		FireNormalSound:
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 6, "FireNormalSound.Default")
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "FireNormalSound.ETQW")
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "FireNormalSound.Quake4")
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 3, "FireNormalSound.Quake3")
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 2, "FireNormalSound.Quake2")
		FireNormalSound.Default:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireNormalSound.Quake2:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireStartLoop_Quake2",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireNormalSound.Quake3:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireStartLoop_Quake3",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireNormalSound.Quake4:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireStartLoop_Quake4",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireNormalSound.ETQW:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireStartLoop_ETQW",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck

		FireQuadSound:
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 6, "FireQuadSound.Default")
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "FireQuadSound.ETQW")
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "FireQuadSound.Quake4")
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 3, "FireQuadSound.Quake3")
			"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 2, "FireQuadSound.Quake2")
		FireQuadSound.Default:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/Quad/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireQuadSound.Quake2:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/Quad/FireStartLoop_Quake2",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireQuadSound.Quake3:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/Quad/FireStartLoop_Quake3",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireQuadSound.Quake4:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/Quad/FireStartLoop_Quake4",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireQuadSound.ETQW:
			"####" A 0 A_PlaySound("QC/Weapons/Machinegun/Quad/FireStartLoop_ETQW",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck

	FirePostSoundCheck:
		"####" A 0 A_AlertMonsters
		"####" A 0 A_JumpIfInventory("DMToken",1,"DM_FireLooper1")
		Goto FireLooper

		//SP fire
		FireLooper:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_GunFlash("SP_Flash")
			"####" B 1 Offset(2,34) Bright
			"####" C 1 Offset(4,36) Bright
			"####" H 1 Offset(2,34)
			"####" I 1 Offset(1,33)
			"####" J 1 Offset(0,32) A_ReFire("FireLooperNA")
			Goto EndFireLooper
		FireLooperNA: //this one is "No (use of) Ammo"
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_GunFlash("SP_FlashNA")
			"####" A 0 A_Jump(255,"NAT1","NAT2")
		NAT1:
			"####" D 1 Offset(2,34) Bright
			"####" E 1 Offset(4,36) Bright
			Goto EndLooperNa
		NAT2:
			"####" F 1 Offset(2,34) Bright
			"####" G 1 Offset(4,36) Bright
			Goto EndLooperNa
		EndLooperNa:
			"####" H 1 Offset(2,34)
			"####" I 1 Offset(1,33)
			"####" J 1 Offset(0,32) A_ReFire("FireLooper")
			Goto EndFireLooper


		//DM fire
		DM_FireLooper1:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_GunFlash("DM_Flash")
			"####" B 1 Offset(2,34) Bright
			"####" C 1 Offset(4,36) Bright
			"####" H 1 Offset(2,34)
			"####" I 1 Offset(1,33)
			"####" J 1 Offset(0,32) A_ReFire("DM_FireLooper2")
			Goto EndFireLooper
		DM_FireLooper2:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_GunFlash("DM_Flash")
			"####" D 1 Offset(2,34) Bright
			"####" E 1 Offset(4,36) Bright
			"####" H 1 Offset(2,34)
			"####" I 1 Offset(1,33)
			"####" J 1 Offset(0,32) A_ReFire("DM_FireLooper3")
			Goto EndFireLooper
		DM_FireLooper3:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_GunFlash("DM_Flash")
			"####" F 1 Offset(2,34) Bright
			"####" G 1 Offset(4,36) Bright
			"####" H 1 Offset(2,34)
			"####" I 1 Offset(1,33)
			"####" J 1 Offset(0,32) A_ReFire("DM_FireLooper1")
			Goto EndFireLooper

	EndFireLooper:
		"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 6, "EndFireLooper.Default")
		"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "EndFireLooper.ETQW")
		"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "EndFireLooper.Quake4")
		"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 3, "EndFireLooper.Quake3")
		"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 2, "EndFireLooper.Quake2")
	EndFireLooper.Default:
		"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireEnd_Default",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.Quake2:
		"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireEnd_Quake2",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.Quake3:
		"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireEnd_Quake3",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.Quake4:
		"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireEnd_Quake4",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.ETQW:
		"####" A 0 A_PlaySound("QC/Weapons/Machinegun/FireEnd_ETQW",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont

	EndFireLooperCont:
		"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 6, 3) // Q4 and ETQW skins don't have a spin down
		"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "Ready")
		"####" A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "Ready")
		"####" AAHHIIIJJJJ 1 A_WeaponReady
		Goto Ready

	//-----------------------
	// Attack doers
	//-----------------------
	SP_Flash:
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "SP_Flash.Default")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "SP_Flash.Quake4")
	SP_Flash.Default:
		TNT1 A 0 A_FireBullets(1.75,1.75,1,random(3,5),"QCBulletPuff",FBF_NORANDOM|FBF_USEAMMO)
		Goto EndFlash
	SP_Flash.Quake4:
		TNT1 A 0 A_FireBullets(1.75,1.75,1,random(3,5),"QCBulletPuff_Quake4",FBF_NORANDOM|FBF_USEAMMO)
		Goto EndFlash

	SP_FlashNA:
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "SP_FlashNA.Default")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "SP_FlashNA.Quake4")
	SP_FlashNA.Default:
		TNT1 A 0 A_FireBullets(1.75,1.75,1,random(3,5),"QCBulletPuff",FBF_NORANDOM)
		Goto EndFlash
	SP_FlashNA.Quake4:
		TNT1 A 0 A_FireBullets(1.75,1.75,1,random(3,5),"QCBulletPuff_Quake4",FBF_NORANDOM)
		Goto EndFlash

	DM_Flash:
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "DM_Flash.Default")
		TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "DM_Flash.Quake4")
	DM_Flash.Default:
		TNT1 A 0 A_FireBullets(1.75,1.75,1,9,"QCBulletPuff_DM",FBF_NORANDOM|FBF_USEAMMO)
		Goto EndFlash
	DM_Flash.Quake4:
		TNT1 A 0 A_FireBullets(1.75,1.75,1,9,"QCBulletPuff_DM_Quake4",FBF_NORANDOM|FBF_USEAMMO)
		Goto EndFlash

		EndFlash:
			TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 5, "EndFlash.Default")
			TNT1 A 0 A_JumpIfInventory("QCMachinegunSkin", 4, "EndFlash.Quake4")
		EndFlash.Default:
			TNT1 A 0 A_FireCustomMissile("BulletSmallTracer",frandom(1.75,-1.75),false,7,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(1.75,-1.75))
			Goto EndFlashCont
		EndFlash.Quake4:
			TNT1 A 0 A_FireCustomMissile("BulletSmallTracer_Quake4",frandom(1.75,-1.75),false,7,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(1.75,-1.75))
			Goto EndFlashCont+1

		EndFlashCont:
			TNT1 A 0 A_FireCustomMissile("MachinegunSmokeSpawner",0, false,6,2)
			TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
			TNT1 A 0 A_SetBlend("Yellow",0.03,3)
			TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"EndFlashNoRecoil")
			TNT1 A 1 A_SetPitch(Pitch-0.6)
			TNT1 AA 1 A_SetPitch(Pitch+0.3)
			Stop
		EndFlashNoRecoil:
			TNT1 A 1
			Stop
	}
}

Actor MachinegunSmokeSpawner
{
	Radius 1
	Height 1
	Speed 14
	PROJECTILE
	+NOTIMEFREEZE
	+CLIENTSIDEONLY +NONETID
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Die
		Goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("MachinegunSmoke",0,random(1,-1),0,0,0,frandom(0.1,0.25),0,32)
		TNT1 A 2
		Stop
	}
}

Actor MachinegunSmoke
{
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY +NONETID
	Alpha 0.6
	Scale 0.03
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"Type1","Type2")	//to define which graphic will use
	Type1:
		X201 E 0
		Goto "Continue"
	Type2:
		X201 F 0
		Goto "Continue"
	Continue:
		"####" "#" 0 A_Jump(160,2,4,6)
		"####" "#" 0
		"####" "#" 0 A_SetScale(0.03)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.033)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.036)
		"####" "#" 2 A_FadeOut(0.035)
	Looper:
		"####" "#" 0 A_SetScale(ScaleX+0.003)
		"####" "#" 2 A_FadeOut(0.035)
		Loop
	}
}


// ==========================================================================
// ==========================================================================
//	Weapon tokens
// ==========================================================================
// ==========================================================================

Actor QCMachinegunSkin	: Token { Inventory.MaxAmount 20 }


// --------------------------------------------------------------------------
//	For competitive DM, weapons don't autoaim
// --------------------------------------------------------------------------

Actor QCMachinegunDM : QCMachinegun
{
	+WEAPON.NOAUTOAIM
	+CHEATNOTWEAPON
	Tag "$TAG_QCMachinegun" // "Machinegun (DM)"
	Weapon.AmmoGive 100
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 10
}
