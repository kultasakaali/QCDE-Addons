// --------------------------------------------------------------------------
//
// Heavy Machinegun
// Weapon and Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor CompatVer30_HMachinegun {} // for compatibility checks

Actor HeavyMachinegunPickup : CustomInventory 15600
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "Heavy Machinegun"
	Height 48
	Radius 20
	Scale 0.64
	Inventory.PickupMessage "$GOT_QCHeavyMachinegun" // "Picked up a Heavy Machinegun."
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.RespawnTics 35
	Tag "$TAG_QCHeavyMachinegun" // "Heavy Machinegun"
	States
	{
	Spawn:
		QCGB Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		QCGB Y 35 A_CheckFlag("DROPPED", "BecomeWeaponItself")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		QCGB Z -1 Bright ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",8,args[4])
		Stop

	BecomeWeaponItself:
		TNT1 A 0 A_SpawnItemEx("QCHeavyMachinegun")
		TNT1 A 0 A_JumpIf(true, "End") Goto Idle // <-- this is to fool client-side predictor
	End:TNT1 A 0
		Stop

	Idle:
		QCGB Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",7,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCHeavyMachinegun")
		TNT1 A 0 A_JumpIf(CallACS("QCDE_SelectUpgradedWeapon",1,0,0,0), "Stay") // never jumps, but had to wrap CallACS or else the weapon will be always picked up
		TNT1 A 0 A_TakeInventory("QCMachinegun")
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 A_GiveInventory("QCHeavyMachinegunDM")
		TNT1 A 0 A_JumpIf(CallACS("QCDE_SelectUpgradedWeapon",1,0,0,0), "Stay") // never jumps, but had to wrap CallACS or else the weapon will be always picked up
		TNT1 A 0 A_TakeInventory("QCMachinegunDM")
		TNT1 A 0 A_TakeInventory("QCHeavyMachinegun")
		TNT1 A 0 A_TakeInventory("QCMachinegun")
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCHeavyMachinegun",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCHeavyMachinegun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_JumpIf(CallACS("QCDE_SelectUpgradedWeapon",1,0,0,0), "Stay") // never jumps, but had to wrap CallACS or else the weapon will be always picked up
		TNT1 A 0 A_TakeInventory("QCHeavyMachinegun")
		TNT1 A 0 A_JumpIfInventory("QCHeavyMachinegunDM",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCHeavyMachinegunDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto Super::HideDoomish+1
	HideDoomish:
		TNT1 A 1
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto Super::HideDoomish+1
	}
}

Actor HeavyMachinegunPickupFloat : HeavyMachinegunPickup 15610
{
	//$Title "Float Heavy Machinegun"
	+NOGRAVITY
}


Actor QCHeavyMachinegun : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 3.2
	Weapon.Kickback 20
	Weapon.SelectionOrder 1000
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType "QCBulletAmmo"
	Obituary "$OB_QCHeavyMachinegun"
	Inventory.PickupMessage "$GOT_QCHeavyMachinegun"
	Tag "$TAG_QCHeavyMachinegun"
	Scale 0.64
	States
	{
	Spawn:
		TNT1 A 1
		QCGB Y 35 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCGB ZZZZZZZZZZZZZZZ 35 Bright
		Stop
	Idle:
		QCGB Y -1
		Wait
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 7, "Ready.HalfLife")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 6, "Ready.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 5, "Ready.ETQW")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 4, "Ready.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 3, "Ready.Quake3")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 2, "Ready.Quake2")
	Ready.Default:
		QCGB A 0
		Goto ReadyCont
	Ready.Quake2:
		Q2GB A 0
		Goto ReadyCont
	Ready.Quake3:
		Q3GB A 0
		Goto ReadyCont
	Ready.Quake4:
		Q4GB A 0
		Goto ReadyCont
	Ready.ETQW:
		QWGB A 0
		Goto ReadyCont
	Ready.QCDE1:
		Q0GB A 0
		Goto ReadyCont
	Ready.HalfLife:
		HLGB A 0
		Goto ReadyCont

	ReadyCont:
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"ReadyZoomed")
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 3 A_WeaponReady
		Loop

	ReadyZoomed:
		TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",-1,"Ready")
		TNT1 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		TNT1 A 3 Bright A_WeaponReady(WRF_NOBOB)
		Loop

		ReadyCantFire:
			"####" A 0 A_StopSound(CHAN_WEAPON)
		RCFLooper:
			"####" A 4 A_WeaponReady(WRF_NOFIRE)
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		TNT1 A 0 ACS_NamedExecuteWithResult("QCDE_ZoomCheckSelect")
		TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken", 0, "Select.Zoomed")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 7, "Select.HalfLife")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 6, "Select.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 5, "Select.ETQW")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 4, "Select.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 3, "Select.Quake3")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 2, "Select.Quake2")
	Select.Zoomed:
		TNT1 A 0
		Goto SelectCont
	Select.Default:
		QCGB A 0
		Goto SelectCont
	Select.Quake2:
		Q2GB A 0
		Goto SelectCont
	Select.Quake3:
		Q3GB A 0
		Goto SelectCont
	Select.Quake4:
		Q4GB A 0
		Goto SelectCont
	Select.ETQW:
		QWGB A 0
		Goto SelectCont
	Select.QCDE1:
		Q0GB A 0
		Goto SelectCont
	Select.HalfLife:
		HLGB A 0
		Goto SelectCont

	SelectCont:
		"####" A 0 A_PlaySound("QC/Weapons/Switch",CHAN_5,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		"####" A 0 A_TakeInventory("QCMachinegunDM")
		"####" A 0 A_TakeInventory("QCMachinegun")
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
	SelectLooper:
		"####" A 1 A_Raise
		"####" AAA 0 A_Raise
		Loop

	Deselect:
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
	DeselectLooper:
		"####" A 1 A_Lower
		"####" AAA 0 A_Lower
		Loop

	//-----------------------
	// Fire Seq
	//-----------------------
	Fire:
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"ZoomedFire")
		"####" A 0 A_JumpIfInventory("PowerQuad",1,"FireQuadSound")
		"####" A 0 A_JumpIfInventory("PowerTerminatorArtifact",1,"FireQuadSound")
		Goto FireNormalSound

		FireNormalSound:
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 7, "FireNormalSound.HalfLife")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 6, "FireNormalSound.QCDE1")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 5, "FireNormalSound.ETQW")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 4, "FireNormalSound.Quake4")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 3, "FireNormalSound.Quake3")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 2, "FireNormalSound.Quake2")
		FireNormalSound.Default:
			QCGB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireNormalSound.Quake2:
			Q2GB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireStartLoop_Quake2",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireNormalSound.Quake3:
			Q3GB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireStartLoop_Quake3",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireNormalSound.Quake4:
			Q4GB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireStartLoop_Quake4",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireNormalSound.ETQW:
			QWGB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireStartLoop_ETQW",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireNormalSound.QCDE1:
			Q0GB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireNormalSound.HalfLife:
			HLGB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireStartLoop_HalfLife",CHAN_WEAPON,1,1)
			Goto FireSoundCont

		FireQuadSound:
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 7, "FireQuadSound.HalfLife")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 6, "FireQuadSound.QCDE1")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 5, "FireQuadSound.ETQW")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 4, "FireQuadSound.Quake4")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 3, "FireQuadSound.Quake3")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 2, "FireQuadSound.Quake2")
		FireQuadSound.Default:
			QCGB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireQuadSound.Quake2:
			Q2GB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireStartLoop_Quake2",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireQuadSound.Quake3:
			Q3GB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireStartLoop_Quake3",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireQuadSound.Quake4:
			Q4GB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireStartLoop_Quake4",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireQuadSound.ETQW:
			QWGB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireStartLoop_ETQW",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireQuadSound.QCDE1:
			Q0GB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FireSoundCont
		FireQuadSound.HalfLife:
			HLGB A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireStartLoop_HalfLife",CHAN_WEAPON,1,1)
			Goto FireSoundCont

		FireSoundCont:
			"####" A 0 A_ReFire("FireLooper")
			Goto EndFireLooper

	FireLooper:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 0 A_GunFlash("PrimaryFlash")
		"####" B 1 Offset(2,34) Bright
		"####" C 1 Offset(4,36) Bright
		"####" D 1 Offset(2,34)
		"####" E 1 Offset(1,33)
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"ZoomedFire")
		"####" F 1 Offset(0,32) A_ReFire("FireLooper2")
		Goto EndFireLooper
	FireLooper2:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 0 A_GunFlash("PrimaryFlash")
		"####" G 1 Offset(2,34) Bright
		"####" H 1 Offset(4,36) Bright
		"####" I 1 Offset(2,34)
		//"####" J 1 Offset(1,33)
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"ZoomedFire")
		"####" K 1 Offset(0,32) A_ReFire("FireLooper3")
		Goto EndFireLooper
	FireLooper3:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 0 A_GunFlash("PrimaryFlash")
		"####" L 1 Offset(2,34) Bright
		"####" M 1 Offset(4,36) Bright
		"####" N 1 Offset(2,34)
		"####" O 1 Offset(1,33)
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"ZoomedFire")
		"####" P 1 Offset(0,32) A_ReFire("FireLooper")
		Goto EndFireLooper

	EndFireLooper:
		"####" A 0 A_JumpIfInventory("QCHMachinegunSkin", 7, "EndFireLooper.HalfLife")
		"####" A 0 A_JumpIfInventory("QCHMachinegunSkin", 6, "EndFireLooper.Default")
		"####" A 0 A_JumpIfInventory("QCHMachinegunSkin", 5, "EndFireLooper.ETQW")
		"####" A 0 A_JumpIfInventory("QCHMachinegunSkin", 4, "EndFireLooper.Quake4")
		"####" A 0 A_JumpIfInventory("QCHMachinegunSkin", 3, "EndFireLooper.Quake3")
		"####" A 0 A_JumpIfInventory("QCHMachinegunSkin", 2, "EndFireLooper.Quake2")
	EndFireLooper.Default:
		"####" A 1 A_PlaySound("QC/Weapons/HeavyMachinegun/FireEnd_Default",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.Quake2:
		"####" A 1 A_PlaySound("QC/Weapons/HeavyMachinegun/FireEnd_Quake2",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.Quake3:
		"####" A 1 A_PlaySound("QC/Weapons/HeavyMachinegun/FireEnd_Quake3",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.Quake4:
		"####" A 1 A_PlaySound("QC/Weapons/HeavyMachinegun/FireEnd_Quake4",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.ETQW:
		"####" A 1 A_PlaySound("QC/Weapons/HeavyMachinegun/FireEnd_ETQW",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.HalfLife:
		"####" A 1 A_PlaySound("QC/Weapons/HeavyMachinegun/FireEnd_HalfLife",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont

	EndFireLooperCont:
		"####" QQRRSSSTTTTUUUU 1 A_WeaponReady
		Goto Ready

	//-----------------------
	// Attack doers
	//-----------------------
	PrimaryFlash:
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"DM_PrimaryFlash")
		TNT1 A 0 A_JumpIfInventory("HMG_UG_Ricochet",1,"PerformRicochet")
		TNT1 A 0 A_FireBullets(1.75,1.75,1,random(10,15),"QCBulletPuff",FBF_NORANDOM|FBF_USEAMMO)
		Goto EndFlash

		PerformRicochet:
			TNT1 A 0 A_FireBullets(1.75,1.75,1,random(9,13),"QCBulletPuffRicochet",FBF_NORANDOM|FBF_USEAMMO|FBF_NORANDOMPUFFZ)
			Goto EndFlash

	DM_PrimaryFlash:
		TNT1 A 0 A_FireBullets(1.75,1.75,1,10,"QCBulletPuff_DM",FBF_NORANDOM|FBF_USEAMMO)
		Goto EndFlash

		EndFlash:
			TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
			TNT1 A 0 A_SetBlend("Yellow",0.03,3)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_FireCustomMissile("BulletSmallTracer",frandom(1.75,-1.75),false,8,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(1.75,-1.75))
			TNT1 A 0 A_FireCustomMissile("MachinegunSmokeSpawner",0, false,7,2,FPF_NOUNLAGGED|FPF_SKIPOWNER)
			TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"EndFlashNoRecoil")
			TNT1 A 1 A_SetPitch(Pitch-0.6)
			TNT1 AA 1 A_SetPitch(Pitch+0.3)
			Stop
		EndFlashNoRecoil:
			TNT1 A 1
			Stop

	ZoomedFire:
		TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"ZoomedFireQuadSound")
		TNT1 A 0 A_JumpIfInventory("PowerTerminatorArtifact",1,"ZoomedFireQuadSound")
		Goto ZoomedFireNormalSound

		ZoomedFireNormalSound:
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 7, "ZoomedFireNormalSound.HalfLife")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 6, "ZoomedFireNormalSound.Default")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 5, "ZoomedFireNormalSound.ETQW")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 4, "ZoomedFireNormalSound.Quake4")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 3, "ZoomedFireNormalSound.Quake3")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 2, "ZoomedFireNormalSound.Quake2")
		ZoomedFireNormalSound.Default:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedStart_Default",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireNormalSound.Quake2:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedStart_Quake2",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireNormalSound.Quake3:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedStart_Quake3",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireNormalSound.Quake4:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedStart_Quake4",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireNormalSound.ETQW:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedStart_ETQW",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireNormalSound.HalfLife:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedStart_HalfLife",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont

		ZoomedFireQuadSound:
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 7, "ZoomedFireQuadSound.HalfLife")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 6, "ZoomedFireQuadSound.Default")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 5, "ZoomedFireQuadSound.ETQW")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 4, "ZoomedFireQuadSound.Quake4")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 3, "ZoomedFireQuadSound.Quake3")
			TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 2, "ZoomedFireQuadSound.Quake2")
		ZoomedFireQuadSound.Default:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireZoomedStart_Default",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireQuadSound.Quake2:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireZoomedStart_Quake2",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireQuadSound.Quake3:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireZoomedStart_Quake3",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireQuadSound.Quake4:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireZoomedStart_Quake4",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireQuadSound.ETQW:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireZoomedStart_ETQW",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont
		ZoomedFireQuadSound.HalfLife:
			TNT1 A 0 A_PlaySound("QC/Weapons/HeavyMachinegun/Quad/FireZoomedStart_HalfLife",CHAN_WEAPON,1,1)
			Goto ZoomedFireSoundCont

		ZoomedFireSoundCont:
			TNT1 A 0 A_ReFire("ZoomedFireLooper")
			TNT1 A 0 A_StopSound(CHAN_WEAPON)
			Goto ReadyZoomed

	ZoomedFireLooper:
		TNT1 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		TNT1 A 0 A_GunFlash("ZoomedFlash")
		TNT1 A 1 Bright
		TNT1 A 1 Bright
		TNT1 A 1 Bright
		TNT1 A 3 Bright
		TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",-1,"Fire")
		TNT1 A 0 A_ReFire("ZoomedFireLooper2")
		Goto EndZoomedFireLooper
	ZoomedFireLooper2:
		TNT1 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		TNT1 A 0 A_GunFlash("ZoomedFlash")
		TNT1 A 1 Bright
		TNT1 A 1 Bright
		TNT1 A 1 Bright
		TNT1 A 2 Bright
		TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",-1,"Fire")
		TNT1 A 0 A_ReFire("ZoomedFireLooper")
		Goto EndZoomedFireLooper

	EndZoomedFireLooper:
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 7, "EndZoomedFireLooper.HalfLife")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 6, "EndZoomedFireLooper.Default")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 5, "EndZoomedFireLooper.ETQW")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 4, "EndZoomedFireLooper.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 3, "EndZoomedFireLooper.Quake3")
		TNT1 A 0 A_JumpIfInventory("QCHMachinegunSkin", 2, "EndZoomedFireLooper.Quake2")
	EndZoomedFireLooper.Default:
		TNT1 A 3 Bright A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedEnd_Default",CHAN_WEAPON,1) //but plays the end so that it keeps ringing
		Goto ReadyZoomed
	EndZoomedFireLooper.Quake2:
		TNT1 A 3 Bright A_PlaySound("QC/Weapons/HeavyMachinegun/FireEnd_Quake2",CHAN_WEAPON,1) //but plays the end so that it keeps ringing
		Goto ReadyZoomed
	EndZoomedFireLooper.Quake3:
		TNT1 A 3 Bright A_PlaySound("QC/Weapons/HeavyMachinegun/FireEnd_Quake3",CHAN_WEAPON,1) //but plays the end so that it keeps ringing
		Goto ReadyZoomed
	EndZoomedFireLooper.Quake4:
		TNT1 A 3 Bright A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedEnd_Quake4",CHAN_WEAPON,1) //but plays the end so that it keeps ringing
		Goto ReadyZoomed
	EndZoomedFireLooper.ETQW:
		TNT1 A 3 Bright A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedEnd_ETQW",CHAN_WEAPON,1) //but plays the end so that it keeps ringing
		Goto ReadyZoomed
	EndZoomedFireLooper.HalfLife:
		TNT1 A 3 Bright A_PlaySound("QC/Weapons/HeavyMachinegun/FireZoomedEnd_HalfLife",CHAN_WEAPON,1) //but plays the end so that it keeps ringing
		Goto ReadyZoomed



	ZoomedFlash:
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"DM_ZoomedFlash")
		TNT1 A 0 A_JumpIfInventory("HMG_UG_Pierce",1,"PerformZoomedPierce")
		TNT1 A 0 A_JumpIfInventory("HMG_UG_Ricochet",1,"PerformZoomedRicochet")
		TNT1 A 0 A_FireBullets(0.5,0.5,1,random(11,22),"QCBulletPuff_ZombieGibber",FBF_NORANDOM|FBF_USEAMMO)
		Goto EndFlashZoomed

		PerformZoomedRicochet:
			TNT1 A 0 A_FireBullets(0.5,0.5,1,random(11,22),"QCBulletPuffRicochet_ZombieGibber",FBF_NORANDOM|FBF_USEAMMO|FBF_NORANDOMPUFFZ)
			Goto EndFlashZoomed

		PerformZoomedPierce:	//zoomed will not add ricochet
			TNT1 A 0 A_FireBullets(0.5,0.5,1,random(11,22),"QCBulletPuff_ZombieGibber",FBF_NORANDOM|FBF_USEAMMO|FBF_NORANDOMPUFFZ)
			TNT1 A 0 A_RailAttack(random(11,22),0,0,"none","b79400",RGF_SILENT|RGF_FULLBRIGHT,0.9,"QCBulletPuff_ZombieGibber",0,0,4096,8)
			Goto EndFlashZoomed

	DM_ZoomedFlash:
		TNT1 A 0 A_FireBullets(0.5,0.5,1,15,"QCBulletPuff_DM",FBF_NORANDOM|FBF_USEAMMO)
		Goto EndFlashZoomed

		EndFlashZoomed:
			TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
			TNT1 A 0 A_SetBlend("Yellow",0.06,3)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_FireCustomMissile("BulletSmallTracer",frandom(0.5,-0.5),false,1,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(0.5,-0.5))
			TNT1 A 0 A_FireCustomMissile("MachinegunSmokeSpawner",0, false,0,2,FPF_NOUNLAGGED|FPF_SKIPOWNER)
			TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"EndFlashNoRecoil") //same as unzoomed, because it does nothing
			TNT1 A 1 A_SetPitch(Pitch-0.4)
			TNT1 AA 1 A_SetPitch(Pitch+0.2)
			Stop
	}
}

Actor QCBulletPuff_ZombieGibber : QCBulletPuff
{
	DamageType "ZombieGibber"
}

// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCHeavyMachinegunDM : QCHeavyMachinegun
{
	+WEAPON.NOAUTOAIM
	+CHEATNOTWEAPON
	Tag "$TAG_QCHeavyMachinegun" // "Heavy Machinegun (DM)"
	Weapon.AmmoGive 100
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 10
}

//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Weapon tokens
//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
Actor HMG_UG_Ricochet	: Token {}
Actor HMG_UG_Pierce		: Token {}
Actor QCHMachinegunSkin	: Token { Inventory.MaxAmount 20 }

Actor QCBulletPuffRicochet
{
	+NOINTERACTION +NOTONAUTOMAP
	+PUFFGETSOWNER
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	+MTHRUSPECIES
	+DONTBLAST
	Renderstyle Add
	Decal "BulletChip"
	DamageType "ZombieGibber"
	Scale 0.35
	States
	{
	Spawn: //hit non bleeding actor
		TNT1 A 0 NODELAY A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		Goto Crash
	XDeath: //hit bleeding actor
		TNT1 A 0 A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_PlaySound("BulletHitFleshRandom",CHAN_7,0.6,0,9)
		Goto Ricochet
	Crash: //hit everything else
		TNT1 A 0 A_PlaySound("BulletHitConcreteRandom",CHAN_7,0.6,0,9)
	Ricochet:
		TNT1 A 0 A_SpawnItemEx("RicochetProjectile", 0,0,0, 12,0,afrandom(0,4), arandom(0,359), SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,128)
		Goto Choose

	Choose:
		TNT1 A 0 A_Jump(256,"Type1","Type2")
	Type1:
		FX57 ABCD 1 Bright
		FX57 EFGHIJ 2 Bright
		Stop
	Type2:
		FX57 KLMN 1 Bright
		FX57 OPQRST 2 Bright
		Stop
	}
}
Actor QCBulletPuffRicochet_ZombieGibber : QCBulletPuffRicochet
{
	DamageType "ZombieGibber"
	States
	{
	Ricochet:
		TNT1 A 0 A_SpawnItemEx("RicochetProjectile", 0,0,0, 12,0,afrandom(0,4), arandom(0,359), SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto Choose
	}
}



Actor RicochetProjectile
{
	Projectile
	+RIPPER
	Speed 0
	Damage 4
	Radius 1
	Height 1
	-NOGRAVITY
	+ROCKETTRAIL
	+BLOODLESSIMPACT
	+PAINLESS
	+DONTBLAST
	RenderStyle Add
	Gravity 0.4
	Scale 0.1
	DamageType "ZombieGibber"
	ProjectileKickBack 0
	States
	{
	Spawn:
		X029 A 1 Bright
		Loop
	Death:
		TNT1 A 3
		Stop
	}
}
