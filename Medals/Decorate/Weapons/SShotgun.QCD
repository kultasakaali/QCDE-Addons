// --------------------------------------------------------------------------
//
// Super Shotgun
// Weapon and Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor SuperShotgunPickup : CustomInventory 15602
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "Super Shotgun"
	Height 48
	Radius 20
	Scale 0.66
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCSuperShotgun" // "Picked up a Super Shotgun."
	Tag "$TAG_QCSuperShotgun" // "Super Shotgun"
	Inventory.RespawnTics 35
	States
	{
	Spawn:
		QCGC Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		QCGC Z -1 Bright ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",10,args[4])
		Stop
	Idle:
		QCGC Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",9,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCSuperShotgun")
		TNT1 A 0
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 A_GiveInventory("QCSuperShotgunDM")
		TNT1 A 0 A_JumpIf(CallACS("QCDE_SelectUpgradedWeapon",2,0,0,0), "Stay") // never jumps, but had to wrap CallACS or else the weapon will be always picked up
		TNT1 A 0 A_TakeInventory("QCShotgunDM")
		TNT1 A 0 A_TakeInventory("QCSuperShotgun")
		TNT1 A 0 A_TakeInventory("QCShotgun")
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCSuperShotgun",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCSuperShotgun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_JumpIf(CallACS("QCDE_SelectUpgradedWeapon",2,0,0,0), "Stay") // never jumps, but had to wrap CallACS or else the weapon will be always picked up
		TNT1 A 0 A_TakeInventory("QCSuperShotgun")
		TNT1 A 0 A_JumpIfInventory("QCSuperShotgunDM",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCSuperShotgunDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto Super::HideDoomish+1
	HideDoomish:
		TNT1 A 1
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto Super::HideDoomish+1
	}
}

Actor SuperShotgunPickupFloat : SuperShotgunPickup 15612
{
	//$Title "Float Super Shotgun"
	+NOGRAVITY
}

Actor SuperShotgunPickupDMvar: CustomInventory 	//this one is for SSGs spawned in DM maps that have 200
{												//SSG spawns in the map. Most of the time, it'll be something else
	//$NotAngled
	//$Color 14
	//$Title "SSG-in-vanilla Replacer"
	var int user_type;
	Height 48
	Scale 0.66
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCSuperShotgun" // "Picked up a Super Shotgun."
	Tag "$TAG_QCSuperShotgun" // "Super Shotgun"
	Inventory.RespawnTics 35
	States
	{
	Spawn:
		TNT1 A 1
		QCGC Y 35 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle

	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		Goto ChooseWeapon

		ChooseWeapon:
			TNT1 A 0 A_Jump(200,"BeTribolt","BeHMG","BeSNG")
			Goto BeSSG //about 1/5th of the time, it'll be ssg
		BeTribolt:
			TNT1 A 0 A_SpawnItemEx("TriboltPickup", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION)
			Stop
		BeHMG:
			TNT1 A 0 A_SpawnItemEx("HeavyMachinegunPickup", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION)
			Stop
		BeSNG:
			TNT1 A 0 A_SpawnItemEx("SuperNailgunPickup", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION)
			Stop
		BeSSG:
			QCGC Z -1 Bright ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",10,args[4])
			Wait

	Idle:
		QCGC Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",9,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCSuperShotgun")
		TNT1 A 0
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 A_GiveInventory("QCSuperShotgunDM")
		TNT1 A 0 A_JumpIf(CallACS("QCDE_SelectUpgradedWeapon",2,0,0,0), "Stay") // never jumps, but had to wrap CallACS or else the weapon will be always picked up
		TNT1 A 0 A_TakeInventory("QCShotgunDM")
		TNT1 A 0 A_TakeInventory("QCSuperShotgun")
		TNT1 A 0 A_TakeInventory("QCShotgun")
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCSuperShotgun",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCSuperShotgun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_JumpIf(CallACS("QCDE_SelectUpgradedWeapon",2,0,0,0), "Stay") // never jumps, but had to wrap CallACS or else the weapon will be always picked up
		TNT1 A 0 A_TakeInventory("QCSuperShotgun")
		TNT1 A 0 A_JumpIfInventory("QCSuperShotgunDM",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCSuperShotgunDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto HideDoomish+1
	}
}


Actor QCSuperShotgun : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 3.0
	Weapon.Kickback 35
	Weapon.SelectionOrder 1100
	Weapon.AmmoUse 2
	Weapon.AmmoGive 16
	Weapon.AmmoType "QCShellAmmo"
	Obituary "$OB_QCSuperShotgun" // "%o was too close to %k's double barreled shotgun."
	Inventory.PickupMessage "$GOT_QCSuperShotgun" // "Picked up a Super Shotgun."
	Tag "$TAG_QCSuperShotgun" // "Super Shotgun"
	Scale 0.66
	States
	{
	Spawn:
		QCGC Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCGC ZZZZZZZZZZZZZZZ 35 Bright
		Stop
	Idle:
		QCGC Y -1
		Wait
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 7, "Ready.HalfLife")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 6, "Ready.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 5, "Ready.ETQW")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 4, "Ready.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 3, "Ready.Quake3")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 2, "Ready.Quake2")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 1, "Ready.Quake1")
	Ready.Default:
		QCGC A 0
		Goto ReadyCont
	Ready.Quake1:
		Q1GC A 0
		Goto ReadyCont
	Ready.Quake2:
		Q2GC A 0
		Goto ReadyCont
	Ready.Quake3:
		Q3GC A 0
		Goto ReadyCont
	Ready.Quake4:
		Q4GC A 0
		Goto ReadyCont
	Ready.ETQW:
		QWGC A 0
		Goto ReadyCont
	Ready.QCDE1:
		Q0GC A 0
		Goto ReadyCont
	Ready.HalfLife:
		HLGC A 0
		Goto ReadyCont

	ReadyCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 3 A_WeaponReady
		Loop

		ReadyCantFire:
			"####" A 0 A_StopSound(CHAN_WEAPON)
			"####" A 0 A_StopSound(6)
			"####" A 0 A_ZoomFactor(1.0)
			"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			"####" A 4 A_WeaponReady(WRF_NOFIRE)
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 7, "Select.HalfLife")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 6, "Select.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 5, "Select.ETQW")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 4, "Select.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 3, "Select.Quake3")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 2, "Select.Quake2")
		TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 1, "Select.Quake1")
	Select.Default:
		QCGC A 0
		Goto SelectCont
	Select.Quake1:
		Q1GC A 0
		Goto SelectCont
	Select.Quake2:
		Q2GC A 0
		Goto SelectCont
	Select.Quake3:
		Q3GC A 0
		Goto SelectCont
	Select.Quake4:
		Q4GC A 0
		Goto SelectCont
	Select.ETQW:
		QWGC A 0
		Goto SelectCont
	Select.QCDE1:
		Q0GC A 0
		Goto SelectCont
	Select.HalfLife:
		HLGC A 0
		Goto SelectCont

	SelectCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		"####" A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" A 0 A_TakeInventory("QCShotgunDM")
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		"####" A 1 A_Raise
		"####" AAA 0 A_Raise
		Loop

	Deselect:
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselecLooper:
		"####" A 1 A_Lower
		"####" AAA 0 A_Lower
		Loop

	//-----------------------
	// Fire Seq
	//-----------------------
	Fire: //35 tics, 1.0 sec
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 0 A_AlertMonsters
		"####" B 0 Bright A_GunFlash("PrimaryFlash")
		"####" B 1 Bright
		"####" B 1 Bright Offset(4,27)
		"####" B 0 A_JumpIfInventory("SSG_UG_Speed",1,"FasterSpeed")
	RegularSpeed:
		"####" C 1 Offset(11,19)
		"####" D 1 Offset(23,11)
		"####" E 1 Offset(32,9)
		"####" F 1 Offset(38,7)
		"####" G 1 Offset(44,7)
		"####" G 1
		"####" H 1
		"####" I 1
		"####" J 2
		"####" K 2
		"####" L 1
		"####" L 1 Offset(42,7)
		"####" M 1 Offset(39,8)
		"####" M 1 Offset(36,9)
		"####" N 1 Offset(33,11)
		"####" O 1 Offset(30,14)
		"####" P 1 Offset(26,17)

		"####" Q 1 Offset(23,20)
		"####" Q 1 Offset(20,23)
		"####" R 1 Offset(18,27)
		"####" R 1 Offset(16,28)
		"####" S 1 Offset(14,29)
		"####" S 1 Offset(12,31)
		"####" T 1 Offset(10,32)
		"####" T 1 Offset(8,33)
		"####" U 1 Offset(6,34)
		"####" U 1 Offset(4,35)
		"####" A 1 Offset(3,35)
		"####" A 1 Offset(2,34)
		"####" A 1 Offset(1,33)
		"####" A 1 Offset(0,32)
		Goto Ready


	FasterSpeed: //including 2 tics of firing, total 28 tics (0.8sec)
		"####" C 1 Offset(11,19)
		"####" D 1 Offset(23,11)
		"####" E 1 Offset(32,9)
		"####" F 1 Offset(38,7)
		"####" G 1 Offset(44,7)
		"####" H 1
		"####" I 1
		"####" J 1
		"####" K 1
		"####" L 1 Offset(42,7)
		"####" M 1 Offset(37,9)
		"####" N 1 Offset(33,11)
		"####" O 1 Offset(30,14)
		"####" P 1 Offset(26,17)
		"####" Q 1 Offset(23,20)
		"####" Q 1 Offset(20,23)
		"####" R 1 Offset(17,27)
		"####" S 1 Offset(14,29)
		"####" S 1 Offset(12,31)
		"####" T 1 Offset(10,32)
		"####" T 1 Offset(7,33)
		"####" U 1 Offset(4,35)
		"####" A 1 Offset(3,35)
		"####" A 1 Offset(2,34)
		"####" A 1 Offset(1,33)
		"####" A 1 Offset(0,32)
		Goto Ready

	PrimaryFlash: //Attacks doer
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"DM_Flash")
		Goto SP_Flash

		SP_Flash:
			TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken", 1, "CheckIfAltMod")
			TNT1 A 0 A_JumpIfInventory("SSG_UG_Speed",1,"DoMoreDamage")
			TNT1 A 0 A_FireBullets(9.2,7.2,15,random(10,14),"QCShotgunPuff",FBF_NORANDOM|FBF_USEAMMO)
			Goto EndSPFlash
		DoMoreDamage:
			TNT1 A 0 A_FireBullets(9.2,7.2,15,random(14,20),"QCShotgunPuff",FBF_NORANDOM|FBF_USEAMMO)
			Goto EndSPFlash
		EndSPFlash:
			TNT1 AAAAAAA 0 A_FireCustomMissile("BulletSmallTracer",frandom(9.2,-9.2),false,3,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(7.2,-7.2))
			Goto FlashCheckSound

			CheckIfAltMod:
				TNT1 A 0 A_JumpIfInventory("SSG_UG_Flame",1,"DoFlame")
				Goto SP_Flash+1
			DoFlame:
				//TNT1 AAAAAAAA 0 A_FireCustomMissile("SSGFlakProjectile",frandom(3.5,-3.5),FALSE,3,0,0,frandom(3.5,-3.5))
				//TNT1 A 0 A_FireCustomMissile("SSGFlakGrenade",0,TRUE)
				TNT1 A 0 A_FireCustomMissile("SSGFlame1",             0, true,3)
				TNT1 A 0 A_FireCustomMissile("SSGFlame2",arandom( 13, 7),false,3)
				TNT1 A 0 A_FireCustomMissile("SSGFlame2",arandom(-13,-7),false,3)
				Goto FlashCheckSound


		DM_Flash:
			//TNT1 A 0 A_FireBullets(5.0,5.0,24,5,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_USEAMMO)
			TNT1 A 0 A_FireBullets(sin(0)*2.1,cos(0)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_USEAMMO|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(0)*3.4,cos(0)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(0)*5.5,cos(0)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(36)*2.1,cos(36)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(36)*3.4,cos(36)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(36)*5.5,cos(36)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(72)*2.1,cos(72)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(72)*3.4,cos(72)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(72)*5.5,cos(72)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(108)*2.1,cos(108)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(108)*3.4,cos(108)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(108)*5.5,cos(108)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(144)*2.1,cos(144)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(144)*3.4,cos(144)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(144)*5.5,cos(144)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(180)*2.1,cos(180)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(180)*3.4,cos(180)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(180)*5.5,cos(180)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(216)*2.1,cos(216)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(216)*3.4,cos(216)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(216)*5.5,cos(216)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(252)*2.1,cos(252)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(252)*3.4,cos(252)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(252)*5.5,cos(252)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(282)*2.1,cos(282)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(282)*3.4,cos(282)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(282)*5.5,cos(282)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(324)*2.1,cos(324)*2.1,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(324)*3.4,cos(324)*3.4,-1,4,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(324)*5.5,cos(324)*5.5,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 AAAAAAA 0 A_FireCustomMissile("BulletSmallTracer",frandom(5.5,-5.5),false,3,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(5.5,-5.5))
			Goto FlashCheckSound
			TNT1 AAAAAAA 0 A_FireCustomMissile("BulletSmallTracer",frandom(5.0,-5.0),false,3,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(5.0,-5.0))
			Goto FlashCheckSound

		FlashCheckSound:
			TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"QuadSound")
			Goto NormalSound

			NormalSound:
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 7, "NormalSound.HalfLife")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 6, "NormalSound.Default")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 5, "NormalSound.ETQW")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 4, "NormalSound.Quake4")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 3, "NormalSound.Quake3")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 2, "NormalSound.Quake2")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 1, "NormalSound.Quake1")
			NormalSound.Default:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Fire_Default",CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.Quake1:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Fire_Quake1",CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.Quake2:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Fire_Quake2",CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.Quake3:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Fire_Quake3",CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.Quake4:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Fire_Quake4",CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.ETQW:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Fire_ETQW",CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.HalfLife:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Fire_HalfLife",CHAN_WEAPON)
				Goto FlashEnder

			QuadSound:
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 7, "QuadSound.HalfLife")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 6, "QuadSound.Default")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 5, "QuadSound.ETQW")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 4, "QuadSound.Quake4")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 3, "QuadSound.Quake3")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 2, "QuadSound.Quake2")
				TNT1 A 0 A_JumpIfInventory("QCSShotgunSkin", 1, "QuadSound.Quake1")
			QuadSound.Default:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Quad/Fire_Default",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.Quake1:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Quad/Fire_Quake1",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.Quake2:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Quad/Fire_Quake2",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.Quake3:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Quad/Fire_Quake3",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.Quake4:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Quad/Fire_Quake4",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.ETQW:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Quad/Fire_ETQW",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.HalfLife:
				TNT1 A 0 A_PlaySound("QC/Weapons/SuperShotgun/Quad/Fire_HalfLife",CHAN_WEAPON)
				Goto FlashEnder

	FlashEnder:
		TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
		TNT1 A 0 A_SetBlend("Orange",0.12,15)
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"EndFlashNoRecoil")
		TNT1 A 0 A_SetPitch(pitch-5)
		TNT1 A 1 A_FireCustomMissile("SSGSmokeSpawner1",0, false,5,1)
		TNT1 A 0 A_FireCustomMissile("SSGSmokeSpawner2",0, false,6,1)
		TNT1 A 1 A_FireCustomMissile("SSGSmokeSpawner1",0, false,7,2)
		TNT1 A 2 A_FireCustomMissile("SSGSmokeSpawner2",0, false,7,3)
		TNT1 AAAAAAAAAA 1 A_SetPitch(pitch+0.5)
		Stop

		EndFlashNoRecoil:
			TNT1 A 1 A_FireCustomMissile("SSGSmokeSpawner1",0, false,5,1)
			TNT1 A 0 A_FireCustomMissile("SSGSmokeSpawner2",0, false,6,1)
			TNT1 A 1 A_FireCustomMissile("SSGSmokeSpawner1",0, false,7,2)
			TNT1 A 2 A_FireCustomMissile("SSGSmokeSpawner2",0, false,7,3)
			Stop
	}
}
Actor SSGSmokeSpawner1
{
	Radius 1
	Height 1
	Speed 14
	PROJECTILE
	+NOTIMEFREEZE
	+NOCLIP
	+CLIENTSIDEONLY +NONETID
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Die
		Goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("SSGSmoke1",0,random(1,-1),0,0,0,frandom(0.1,0.25),0,32)
		TNT1 A 2
		Stop
	}
}
Actor SSGSmokeSpawner2
{
	Radius 1
	Height 1
	Speed 14
	PROJECTILE
	+NOTIMEFREEZE
	+NOCLIP
	+CLIENTSIDEONLY +NONETID
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Die
		Goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("SSGSmoke2",0,random(1,-1),0,0,0,frandom(0.1,0.25),0,32)
		TNT1 A 2
		Stop
	}
}

Actor SSGSmoke1
{
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY +NONETID
	Alpha 0.9
	Scale 0.04
	States
	{
	Spawn:
		X201 E 0
	Continue:
		"####" "#" 0 A_Jump(160,2,4,6)
		"####" "#" 0
		"####" "#" 0 A_SetScale(0.04)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.045)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.05)
		"####" "#" 2 A_FadeOut(0.035)
	Looper:
		"####" "#" 0 A_SetScale(ScaleX+0.005)
		"####" "#" 2 A_FadeOut(0.045)
		Loop
	}
}
Actor SSGSmoke2
{
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY +NONETID
	Alpha 0.9
	Scale 0.04
	States
	{
	Spawn:
		X201 F 0
	Continue:
		"####" "#" 0 A_Jump(160,2,4,6)
		"####" "#" 0
		"####" "#" 0 A_SetScale(0.04)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.045)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.05)
		"####" "#" 2 A_FadeOut(0.035)
	Looper:
		"####" "#" 0 A_SetScale(ScaleX+0.005)
		"####" "#" 2 A_FadeOut(0.045)
		Loop
	}
}


//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Weapon tokens
//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
Actor SSG_UG_Speed 		: Token {}
Actor SSG_UG_Flame		: Token {}
Actor QCSShotgunSkin	: Token { Inventory.MaxAmount 20 }

Actor SSGFlame1
{
	Projectile
	Radius 3
	Height 3
	Speed 25
	+RIPPER
	+BLOODLESSIMPACT
	+SEEKERMISSILE
	+FORCERADIUSDMG
	+DONTBLAST
	ReactionTime 4
	RenderStyle None
	DamageType "NoDamageToPlayers"
	ProjectileKickBack 0
	States
	{
	Spawn:
		PLAY A 0 NODELAY A_Explode(random(3,6)*10,128,0,0,32)
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,100)
	AfterBeepCheck:
		PLAY A 2 A_Explode(random(3,6)*10,64,0,0,40)
		TNT1 A 0 A_SpawnItemEx("FlameFlamelet", random(12,-12),random(12,-12),random(12,-12), 0,0,frandom(0,1.5), 0,SXF_NOCHECKPOSITION|SXF_NOUNLAGGED|SXF_SKIPOWNER)
		PLAY A 2
		TNT1 A 0 A_SpawnItemEx("FlameFlamelet", random(12,-12),random(12,-12),random(12,-12), 0,0,frandom(0,1.5), 0,SXF_NOCHECKPOSITION|SXF_NOUNLAGGED|SXF_SKIPOWNER)
		PLAY A 2
		TNT1 A 0 A_SpawnItemEx("FlameFlamelet", random(12,-12),random(12,-12),random(12,-12), 0,0,frandom(0,1.5), 0,SXF_NOCHECKPOSITION|SXF_NOUNLAGGED|SXF_SKIPOWNER)
		TNT1 A 0 A_CountDown
		Goto Spawn+1

		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			Goto AfterBeepCheck

	Death:
		PLAY BCDE 5
		Stop
	}
}

Actor SSGFlame2 : SSGFlame1
{
	States
	{
	Spawn:
	Spawn2:
		PLAY A 0 NODELAY A_Explode(random(3,6)*10,128,0,0,32)
		PLAY A 2
		TNT1 A 0 A_SpawnItemEx("FlameFlamelet", random(12,-12),random(12,-12),random(12,-12), 0,0,frandom(0,1.5), 0,SXF_NOCHECKPOSITION|SXF_NOUNLAGGED|SXF_SKIPOWNER)
		PLAY A 1
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit2",360,JLOSF_PROJECTILE,100)
	AfterBeepCheck2:
		PLAY A 1  A_Explode(random(3,5)*10,64,0,0,40)
		TNT1 A 0 A_SpawnItemEx("FlameFlamelet", random(12,-12),random(12,-12),random(12,-12), 0,0,frandom(0,1.5), 0,SXF_NOCHECKPOSITION|SXF_NOUNLAGGED|SXF_SKIPOWNER)
		PLAY A 2
		TNT1 A 0 A_SpawnItemEx("FlameFlamelet", random(12,-12),random(12,-12),random(12,-12), 0,0,frandom(0,1.5), 0,SXF_NOCHECKPOSITION|SXF_NOUNLAGGED|SXF_SKIPOWNER)
		TNT1 A 0 A_CountDown
		Goto Spawn2+1

		RadiusHit2:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			Goto AfterBeepCheck2
	}
}

Actor FlameFlamelet 	//:)
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+DONTBLAST
	+CLIENTSIDEONLY
	+NONETID
	RenderStyle Add
	Scale 0.4
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(255,"T1","T2","T3")
		Goto T1
	T1:
		X052 ABCDEFGHIJKLMNOPQRS 1 Bright
		Stop
	T2:
		X056 ABCDEFGHIJKLMNOPQRS 1 Bright
		Stop
	T3:
		X054 ABCDEFGHIJKLMNOPQRS 1 Bright
		Stop
	}
}



// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCSuperShotgunDM : QCSuperShotgun
{
	Weapon.AmmoUse 1
	+WEAPON.NOAUTOAIM
	+CHEATNOTWEAPON
	Tag "$TAG_QCSuperShotgun" // "Super Shotgun (DM)"
	Weapon.AmmoGive 10
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 1
}

