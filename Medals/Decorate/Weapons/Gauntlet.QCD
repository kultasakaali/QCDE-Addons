// --------------------------------------------------------------------------
//
// Gauntlet
// Weapon sprite by Franco Tieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor QCGauntlet : QCWeapon
{
	Weapon.SwaySpeed 3.0
	Weapon.Kickback 0
	Weapon.SelectionOrder 2000
	Inventory.PickupMessage "$GOT_QCGauntlet" // "Picked up the Gauntlet."
	Obituary "$OB_QCGauntlet" // "%o got shredded by %k's gauntlet."
	Tag "$TAG_QCGauntlet" // "Gauntlet"
	+INVENTORY.UNDROPPABLE
	+WEAPON.MELEEWEAPON
	States
	{
	Spawn:
		TNT1 A 0
		Stop
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		TNT1 A 0 A_JumpIfInventory("Class_Painkiller",1,"PKReady")
		TNT1 A 0 A_JumpIfInventory("QCGauntletSkin", 6, "Ready.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCGauntletSkin", 4, "Ready.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCGauntletSkin", 3, "Ready.Quake3")
	Ready.Default:
		QCG1 A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Default",CHAN_WEAPON,1,1)
		Goto ReadyCont
	Ready.Quake3:
		Q3G1 A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Quake3",CHAN_WEAPON,1,1)
		Goto ReadyCont
	Ready.Quake4:
		Q4G1 A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Quake4",CHAN_WEAPON,1,1)
		Goto ReadyCont
	Ready.QCDE1:
		Q0G1 A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Default",CHAN_WEAPON,1,1)
		Goto ReadyCont

	ReadyCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 3 A_WeaponReady
		Loop

		ReadyCantFire:
			"####" A 0 A_StopSound(CHAN_WEAPON)
			"####" A 0 A_StopSound(6)
			"####" A 0 A_ZoomFactor(1.0)
			"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			"####" A 4 A_WeaponReady(WRF_NOFIRE)
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		TNT1 A 0 A_JumpIfInventory("Class_Painkiller",1,"PKSelect")
		TNT1 A 0 A_JumpIfInventory("QCGauntletSkin", 6, "Select.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCGauntletSkin", 4, "Select.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCGauntletSkin", 3, "Select.Quake3")
	Select.Default:
		QCG1 A 0 A_PlaySound("QC/Weapons/Gauntlet/Select_Default",CHAN_6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		Goto SelectCont
	Select.Quake3:
		Q3G1 A 0 A_PlaySound("QC/Weapons/Switch",CHAN_6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		Goto SelectCont
	Select.Quake4:
		Q4G1 A 0 A_PlaySound("QC/Weapons/Switch",CHAN_6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		Goto SelectCont
	Select.QCDE1:
		Q0G1 A 0 A_PlaySound("QC/Weapons/Switch",CHAN_6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		Goto SelectCont

	SelectCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
		"####" A 0 A_TakeInventory("PunchedAlready",32767)
	SelectLooper:
		"####" A 1 A_Raise
		"####" AAA 0 A_Raise
		Loop

	Deselect:
		"####" A 0 A_StopSound(CHAN_WEAPON)
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
		"####" A 0 A_TakeInventory("PunchedAlready",32767)
		"####" A 0 A_JumpIfInventory("PKTipDetached",1,"PKDetachedDeSelect")
	DeselectLooper:
		"####" A 1 A_Lower
		"####" AAA 0 A_Lower
		Loop

	//-----------------------
	// Fire Seq
	//-----------------------
	Fire:
		"####" A 0 A_JumpIfInventory("Class_Painkiller",1,"PKFire")
		"####" A 0 A_JumpIfInventory("PowerQuad",1,"QuadSound")
		Goto NormalSound

		NormalSound:
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "NormalSound.Default")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "NormalSound.Quake4")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "NormalSound.Quake3")
		NormalSound.Default:
			"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		NormalSound.Quake3:
			"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/FireStartLoop_Quake3",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		NormalSound.Quake4:
			"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/FireStartLoop_Quake4",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck

		QuadSound:
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "QuadSound.Default")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "QuadSound.Quake4")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "QuadSound.Quake3")
		QuadSound.Default:
			"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/Quad/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		QuadSound.Quake3:
			"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/Quad/FireStartLoop_Quake3",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		QuadSound.Quake4:
			"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/Quad/FireStartLoop_Quake4",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck

	FirePostSoundCheck:
		"####" A 0 A_AlertMonsters
		"####" B 1 Offset(-3,21)
		"####" A 1 Offset(-12,12)
		"####" A 0 A_JumpIfInventory("DMToken",1,"DMFireLooper")
		"####" A 0 A_JumpIfInventory("GNT_UG_Burn",1,"Burner")
		"####" A 0 A_JumpIfInventory("GNT_UG_Omni",1,"Omnier")
		Goto FireLooper1

		FireLooper1:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "FireLooper1.QCDE1")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "FireLooper1.Quake4")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "FireLooper1.Quake3")
		FireLooper1.Default:
			"####" C 1 A_CustomPunch((random(3,5)*10),1,0,"GauntletPuff",56)
			Goto FireLooper1Cont
		FireLooper1.Quake3:
			"####" C 1 A_CustomPunch((random(3,5)*10),1,0,"GauntletPuff_Quake3",56)
			Goto FireLooper1Cont
		FireLooper1.Quake4:
			"####" C 1 A_CustomPunch((random(3,5)*10),1,0,"GauntletPuff_Quake4",56)
			Goto FireLooper1Cont
		FireLooper1.QCDE1:
			"####" C 1 A_CustomPunch((random(3,5)*10),1,0,"GauntletPuff_QCDE1",56)
			Goto FireLooper1Cont

		FireLooper1Cont:
			"####" DC 1
			"####" C 0 A_ReFire("FireLooper2")
			Goto EndFire
		FireLooper2:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" D 1
			"####" CD 1
			"####" B 0 A_ReFire("FireLooper1")
			Goto EndFire

		Omnier:
			"####" A 0 A_JumpIfInventory("GNT_UG_Burn",1,"BurnOmniLooper1")
			Goto OmniLooper1

			OmniLooper1:
				"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "OmniLooper1.QCDE1")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "OmniLooper1.Quake4")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "OmniLooper1.Quake3")
			OmniLooper1.Default:
				"####" C 0 A_CustomPunch(1,1,0,"GauntletPuff",56)
				"####" A 0 A_FireCustomMissile("OmnislashProjectile",0,0,0,0, FPF_FORCESERVERSIDE)
				"####" A 0 A_FireCustomMissile("OmnislashProjectileFX",0,0)
				Goto OmniLooper1Cont
			OmniLooper1.Quake3:
				"####" C 0 A_CustomPunch(1,1,0,"GauntletPuff_Quake3",56)
				"####" A 0 A_FireCustomMissile("OmnislashProjectile_Quake3",0,0,0,0, FPF_FORCESERVERSIDE)
				"####" A 0 A_FireCustomMissile("OmnislashProjectileFX_Quake3",0,0)
				Goto OmniLooper1Cont
			OmniLooper1.Quake4:
				"####" C 0 A_CustomPunch(1,1,0,"GauntletPuff_Quake4",56)
				"####" A 0 A_FireCustomMissile("OmnislashProjectile_Quake4",0,0,0,0, FPF_FORCESERVERSIDE)
				"####" A 0 A_FireCustomMissile("OmnislashProjectileFX_Quake4",0,0)
				Goto OmniLooper1Cont
			OmniLooper1.QCDE1:
				"####" C 0 A_CustomPunch(1,1,0,"GauntletPuff_QCDE1",56)
				"####" A 0 A_FireCustomMissile("OmnislashProjectile",0,0,0,0, FPF_FORCESERVERSIDE)
				"####" A 0 A_FireCustomMissile("OmnislashProjectileFX",0,0)
				Goto OmniLooper1Cont

			OmniLooper1Cont:
				"####" CDC 1
				"####" C 0 A_ReFire("OmniLooper2")
				Goto EndFire

			OmniLooper2:
				"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "OmniLooper2.Default")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "OmniLooper2.Quake4")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "OmniLooper2.Quake3")
			OmniLooper2.Default:
				"####" A 0 A_FireCustomMissile("OmnislashProjectileFX",0,0)
				Goto OmniLooper2Cont
			OmniLooper2.Quake3:
				"####" A 0 A_FireCustomMissile("OmnislashProjectileFX_Quake3",0,0)
				Goto OmniLooper2Cont
			OmniLooper2.Quake4:
				"####" A 0 A_FireCustomMissile("OmnislashProjectileFX_Quake4",0,0)
				Goto OmniLooper2Cont

			OmniLooper2Cont:
				"####" DCD 1
				"####" B 0 A_ReFire("OmniLooper1")
				Goto EndFire

		Burner:
			"####" A 0 A_JumpIfInventory("GNT_UG_Omni",1,"BurnOmniLooper1")
			Goto BurnLooper1

			BurnLooper1:
				"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "BurnLooper1.QCDE1")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "BurnLooper1.Quake4")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "BurnLooper1.Quake3")
			BurnLooper1.Default:
				"####" A 0 A_CustomPunch((random(3,5)*10),1,0,"GauntletPuffBurn",56)
				Goto BurnLooper1Cont
			BurnLooper1.Quake3:
				"####" A 0 A_CustomPunch((random(3,5)*10),1,0,"GauntletPuffBurn_Quake3",56)
				Goto BurnLooper1Cont
			BurnLooper1.Quake4:
				"####" A 0 A_CustomPunch((random(3,5)*10),1,0,"GauntletPuffBurn_Quake4",56)
				Goto BurnLooper1Cont
			BurnLooper1.QCDE1:
				"####" A 0 A_CustomPunch((random(3,5)*10),1,0,"GauntletPuffBurn_QCDE1",56)
				Goto BurnLooper1Cont

			BurnLooper1Cont:
				"####" A 0 A_Jump(128,2)
				"####" C 0 A_FireCustomMissile("BurnerFlamelet",0,0,random(6,-6),random(4,-4))
				"####" C 1
				"####" DC 1
				"####" C 0 A_ReFire("BurnLooper2")
				Goto EndFire
			BurnLooper2:
				"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
				"####" A 0 A_Jump(128,2)
				"####" D 0 A_FireCustomMissile("BurnerFlamelet",0,0,random(6,-6),random(4,-4))
				"####" D 1
				"####" CD 1
				"####" B 0 A_ReFire("BurnLooper1")
				Goto EndFire

			BurnOmniLooper1:
				"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "BurnOmniLooper1.QCDE1")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "BurnOmniLooper1.Quake4")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "BurnOmniLooper1.Quake3")
			BurnOmniLooper1.Default:
				"####" C 0 A_CustomPunch(1,1,0,"GauntletPuff",56)
				"####" A 0 A_FireCustomMissile("OmnislashFireProjectileFX",0,0)
				Goto BurnOmniLooper1Cont
			BurnOmniLooper1.Quake3:
				"####" C 0 A_CustomPunch(1,1,0,"GauntletPuff_Quake3",56)
				"####" A 0 A_FireCustomMissile("OmnislashFireProjectileFX_Quake3",0,0)
				Goto BurnOmniLooper1Cont
			BurnOmniLooper1.Quake4:
				"####" C 0 A_CustomPunch(1,1,0,"GauntletPuff_Quake4",56)
				"####" A 0 A_FireCustomMissile("OmnislashFireProjectileFX_Quake4",0,0)
				Goto BurnOmniLooper1Cont
			BurnOmniLooper1.QCDE1:
				"####" C 0 A_CustomPunch(1,1,0,"GauntletPuff_QCDE1",56)
				"####" A 0 A_FireCustomMissile("OmnislashFireProjectileFX",0,0)
				Goto BurnOmniLooper1Cont

			BurnOmniLooper1Cont:
				"####" C 0 A_FireCustomMissile("OmnislashFireProjectile",0,0,0,0, FPF_FORCESERVERSIDE)
				"####" C 1
				"####" DC 1
				"####" C 0 A_ReFire("BurnOmniLooper2")
				Goto EndFire

			BurnOmniLooper2:
				"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "BurnOmniLooper2.Default")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "BurnOmniLooper2.Quake4")
				"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "BurnOmniLooper2.Quake3")
			BurnOmniLooper2.Default:
				"####" A 0 A_FireCustomMissile("OmnislashFireProjectileFX",0,0)
				Goto BurnOmniLooper2Cont
			BurnOmniLooper2.Quake3:
				"####" A 0 A_FireCustomMissile("OmnislashFireProjectileFX_Quake3",0,0)
				Goto BurnOmniLooper2Cont
			BurnOmniLooper2.Quake4:
				"####" A 0 A_FireCustomMissile("OmnislashFireProjectileFX_Quake4",0,0)
				Goto BurnOmniLooper2Cont

			BurnOmniLooper2Cont:
				"####" D 1
				"####" CD 1
				"####" B 0 A_ReFire("BurnOmniLooper1")
				Goto EndFire


		DMFireLooper:
			"####" A 0 A_JumpIfInventory("Instagib",1,"IGRefire")

		NormalDMRefire:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "NormalDMRefire.QCDE1")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "NormalDMRefire.Quake4")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "NormalDMRefire.Quake3")
		NormalDMRefire.Default:
			"####" C 1 A_CustomPunch(75,1,0,"GauntletPuff_DM",56)
			Goto NormalDMRefireCont
		NormalDMRefire.Quake3:
			"####" C 1 A_CustomPunch(75,1,0,"GauntletPuff_DM_Quake3",56)
			Goto NormalDMRefireCont
		NormalDMRefire.Quake4:
			"####" C 1 A_CustomPunch(75,1,0,"GauntletPuff_DM_Quake4",56)
			Goto NormalDMRefireCont
		NormalDMRefire.QCDE1:
			"####" C 1 A_CustomPunch(75,1,0,"GauntletPuff_DM_QCDE1",56)
			Goto NormalDMRefireCont

		NormalDMRefireCont:
			"####" D 1
			"####" A 0 A_JumpIfInventory("PunchedAlready",1,"GauntStop")

			"####" C 1 A_CustomPunch(75,1,0,"GauntletPuff_DM_NoFX",56)
			"####" D 1
			"####" A 0 A_JumpIfInventory("PunchedAlready",1,"GauntStop")

			"####" B 0 A_ReFire("NormalDMRefire")
			Goto EndFire

		IGRefire:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "IGRefire.QCDE1")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "IGRefire.Quake4")
			"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "IGRefire.Quake3")
		IGRefire.Default:
			"####" C 1 A_CustomPunch(999,1,0,"GauntletPuff_DM",56)
			Goto IGRefireCont
		IGRefire.Quake3:
			"####" C 1 A_CustomPunch(999,1,0,"GauntletPuff_DM_Quake3",56)
			Goto IGRefireCont
		IGRefire.Quake4:
			"####" C 1 A_CustomPunch(999,1,0,"GauntletPuff_DM_Quake4",56)
			Goto IGRefireCont
		IGRefire.QCDE1:
			"####" C 1 A_CustomPunch(999,1,0,"GauntletPuff_DM_QCDE1",56)
			Goto IGRefireCont

		IGRefireCont:
			"####" D 1
			"####" A 0 A_JumpIfInventory("PunchedAlready",1,"GauntStop")

			"####" C 1 A_CustomPunch(999,1,0,"GauntletPuff_DM_NoFX",56)
			"####" D 1
			"####" A 0 A_JumpIfInventory("PunchedAlready",1,"GauntStop")

			"####" B 0 A_ReFire("IGRefire")
			Goto EndFire

	GauntStop:
		"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "GauntStop.Default")
		"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "GauntStop.Quake4")
		"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "GauntStop.Quake3")
	GauntStop.Default:
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Default",CHAN_WEAPON,1,1)
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/FireEnd_Default",CHAN_5,1,0)
		Goto GauntStopCont
	GauntStop.Quake3:
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Quake3",CHAN_WEAPON,1,1)
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/FireEnd_Quake3",CHAN_5,1,0)
		Goto GauntStopCont
	GauntStop.Quake4:
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Quake4",CHAN_WEAPON,1,1)
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/FireEnd_Quake4",CHAN_5,1,0)
		Goto GauntStopCont

	GauntStopCont:
		"####" CDCDCABB 1
		"####" A 1 Offset(-3,21) A_TakeInventory("PunchedAlready",32767)
		"####" A 11 Offset(-1,31)
		Goto Ready

	EndFire:
		"####" A 0 A_JumpIfInventory("QCGauntletSkin", 6, "EndFire.Default")
		"####" A 0 A_JumpIfInventory("QCGauntletSkin", 4, "EndFire.Quake4")
		"####" A 0 A_JumpIfInventory("QCGauntletSkin", 3, "EndFire.Quake3")
	EndFire.Default:
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Default",CHAN_WEAPON,1,1)
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/FireEnd_Default",CHAN_5,1,0)
		Goto EndFireCont
	EndFire.Quake3:
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Quake3",CHAN_WEAPON,1,1)
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/FireEnd_Quake3",CHAN_5,1,0)
		Goto EndFireCont
	EndFire.Quake4:
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/Idle_Quake4",CHAN_WEAPON,1,1)
		"####" A 0 A_PlaySound("QC/Weapons/Gauntlet/FireEnd_Quake4",CHAN_5,1,0)
		Goto EndFireCont

	EndFireCont:
		"####" A 1 Offset(-3,21) A_TakeInventory("PunchedAlready",32767)
		"####" B 1 Offset(-2,26) A_WeaponReady
		"####" B 1 Offset(-1,30) A_WeaponReady
		Goto Ready

	//-----------------------
	// Fire Seq / Zoom
	//-----------------------
	AltFire:
		"####" A 0 A_JumpIfInventory("Class_Painkiller",1,"PKAltFire")
	NoZoom:
		"####" A 2 A_WeaponReady(WRF_NOSECONDARY)	//dummy state in case player is holding "alt fire" all along
		"####" A 0 A_Refire("NoZoom")
		Goto Ready








	//----------------------------------------------------------------------------
	//----------------------------------------------------------------------------
	// And a whole new section just for the Painkiller weapon.
	// This is why replacing weapons is not easy kids.
	// The definition needs to exists within the QC gauntlet actor, for the
	// gauntlet keybind to still work.
	// And it's fully separate states instead of "#", for custom behaviour
	// and timings. It doesn't have to have 1:1 times to gauntlet
	//----------------------------------------------------------------------------
	//----------------------------------------------------------------------------
	PKReady:
		WP30 A 0
		WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
		WP30 A 0 A_JumpIfInventory("PKTipDetached",1,"PKPlayIsBackSound")
		WP30 A 3 A_WeaponReady
		Goto PKReady+1

		PKReadyCantFire:
			WP30 A 0 A_StopSound(CHAN_WEAPON)
			WP30 A 0 A_StopSound(6)
			"####" "#" 0 A_ZoomFactor(1.0)
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		PKRCFLooper:
			WP30 A 4 A_WeaponReady(WRF_NOFIRE)
			WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKRCFLooper")
			Goto Ready
	PKFire:
		WP30 A 0 A_JumpIfInventory("PKTipDetached",1,"PKPlayIsBackSound")
		WP30 A 0 A_PlaySound("QC/Abilities/Painkiller/Weapon/SpinStartLoop",CHAN_WEAPON,1,1)
		Goto PKFirePostSoundCheck

	PKFirePostSoundCheck:
		WP30 A 0 A_AlertMonsters
		WP30 A 1
		WP30 B 1
		WP30 A 0 A_JumpIfInventory("DMToken",1,"PKDMFireLooper")
		WP30 A 0 A_JumpIfInventory("GNT_UG_Burn",1,"PKBurner")
		WP30 A 0 A_JumpIfInventory("GNT_UG_Omni",1,"PKOmnier")
		Goto PKFireLooper1

		PKFireLooper1:
			WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
			WP30 F 1 A_CustomPunch((random(3,5)*10),1,0,"PKGauntletPuff",56)
			WP30 GH 1
			WP30 C 0 A_ReFire("PKFireLooper2")
			Goto PKEndFire
		PKFireLooper2:
			WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
			WP30 I 1
			WP30 FG 1
			WP30 B 0 A_ReFire("PKFireLooper3")
			Goto PKEndFire
		PKFireLooper3:
			WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
			WP30 H 1 A_CustomPunch((random(3,5)*10),1,0,"PKGauntletPuff",56)
			WP30 IF 1
			WP30 B 0 A_ReFire("PKFireLooper4")
			Goto PKEndFire
		PKFireLooper4:
			WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
			WP30 G 1
			WP30 HI 1
			WP30 B 0 A_ReFire("PKFireLooper1")
			Goto PKEndFire

		PKOmnier:
			WP30 A 0 A_JumpIfInventory("GNT_UG_Burn",1,"PKBurnOmniLooper1")
			Goto PKOmniLooper1

			PKOmniLooper1:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 C 0 A_CustomPunch(1,1,0,"PKGauntletPuff",56)
				WP30 F 0 A_FireCustomMissile("OmnislashProjectileFX",0,0)
				WP30 F 1 A_FireCustomMissile("OmnislashProjectile",0,0,0,0, FPF_FORCESERVERSIDE)
				WP30 GH 1
				WP30 C 0 A_ReFire("PKOmniLooper2")
				Goto PKEndFire
			PKOmniLooper2:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 I 1 A_FireCustomMissile("OmnislashProjectileFX",0,0)
				WP30 FG 1
				WP30 B 0 A_ReFire("PKOmniLooper3")
				Goto PKEndFire
			PKOmniLooper3:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 C 0 A_CustomPunch(1,1,0,"PKGauntletPuff",56)
				WP30 H 0 A_FireCustomMissile("OmnislashProjectileFX",0,0)
				WP30 H 1 A_FireCustomMissile("OmnislashProjectile",0,0,0,0, FPF_FORCESERVERSIDE)
				WP30 IF 1
				WP30 C 0 A_ReFire("PKOmniLooper4")
				Goto PKEndFire
			PKOmniLooper4:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 G 1 A_FireCustomMissile("OmnislashProjectileFX",0,0)
				WP30 HI 1
				WP30 B 0 A_ReFire("PKOmniLooper1")
				Goto PKEndFire

		PKBurner:
			WP30 A 0 A_JumpIfInventory("GNT_UG_Omni",1,"PKBurnOmniLooper1")
			Goto PKBurnLooper1

			PKBurnLooper1:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 C 0 A_CustomPunch((random(3,5)*10),1,0,"PKGauntletPuffBurn",56)
				WP30 A 0 A_Jump(128,2)
				WP30 C 0 A_FireCustomMissile("BurnerFlamelet",0,0,random(6,-6),random(4,-4))
				WP30 F 1
				WP30 GH 1
				WP30 C 0 A_ReFire("PKBurnLooper2")
				Goto PKEndFire
			PKBurnLooper2:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 A 0 A_Jump(128,2)
				WP30 D 0 A_FireCustomMissile("BurnerFlamelet",0,0,random(6,-6),random(4,-4))
				WP30 I 1
				WP30 FG 1
				WP30 B 0 A_ReFire("PKBurnLooper3")
				Goto PKEndFire
			PKBurnLooper3:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 C 0 A_CustomPunch((random(3,5)*10),1,0,"PKGauntletPuffBurn",56)
				WP30 A 0 A_Jump(128,2)
				WP30 C 0 A_FireCustomMissile("BurnerFlamelet",0,0,random(6,-6),random(4,-4))
				WP30 H 1
				WP30 IF 1
				WP30 C 0 A_ReFire("PKBurnLooper4")
				Goto PKEndFire
			PKBurnLooper4:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 A 0 A_Jump(128,2)
				WP30 D 0 A_FireCustomMissile("BurnerFlamelet",0,0,random(6,-6),random(4,-4))
				WP30 G 1
				WP30 HI 1
				WP30 B 0 A_ReFire("PKBurnLooper1")
				Goto PKEndFire


			PKBurnOmniLooper1:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 C 0 A_CustomPunch(1,1,0,"PKGauntletPuff",56)
				WP30 C 0 A_FireCustomMissile("OmnislashFireProjectileFX",0,0)
				WP30 C 0 A_FireCustomMissile("OmnislashFireProjectile",0,0,0,0, FPF_FORCESERVERSIDE)
				WP30 F 1
				WP30 GH 1
				WP30 C 0 A_ReFire("PKBurnOmniLooper2")
				Goto PKEndFire
			PKBurnOmniLooper2:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 D 0 A_FireCustomMissile("OmnislashFireProjectileFX",0,0)
				WP30 I 1
				WP30 FG 1
				WP30 B 0 A_ReFire("PKBurnOmniLooper3")
				Goto PKEndFire
			PKBurnOmniLooper3:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 C 0 A_CustomPunch(1,1,0,"PKGauntletPuff",56)
				WP30 C 0 A_FireCustomMissile("OmnislashFireProjectileFX",0,0)
				WP30 C 0 A_FireCustomMissile("OmnislashFireProjectile",0,0,0,0, FPF_FORCESERVERSIDE)
				WP30 H 1
				WP30 IF 1
				WP30 C 0 A_ReFire("PKBurnOmniLooper4")
				Goto PKEndFire
			PKBurnOmniLooper4:
				WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")
				WP30 D 0 A_FireCustomMissile("OmnislashFireProjectileFX",0,0)
				WP30 G 1
				WP30 HI 1
				WP30 B 0 A_ReFire("PKBurnOmniLooper1")
				Goto PKEndFire


		PKDMFireLooper:
			WP30 A 0 A_JumpIfInventory("Instagib",1,"PKIGRefire")
		PKNormalDMRefire:
			WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")

			WP30 F 1 A_CustomPunch(90,1,0,"PKGauntletPuff_DM",56)
			WP30 G 1
			WP30 A 0 A_JumpIfInventory("PunchedAlready",1,"PKGauntStop")

			WP30 H 1 A_CustomPunch(90,1,0,"PKGauntletPuff_DM_NoFX",56)
			WP30 I 1
			WP30 A 0 A_JumpIfInventory("PunchedAlready",1,"PKGauntStop")

			WP30 B 0 A_ReFire("PKNormalDMRefire")
			Goto PKEndFire

		PKIGRefire:
			WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")

			WP30 F 1 A_CustomPunch(999,1,0,"PKGauntletPuff_DM",56)
			WP30 G 1
			WP30 A 0 A_JumpIfInventory("PunchedAlready",1,"PKGauntStop")

			WP30 H 1 A_CustomPunch(999,1,0,"PKGauntletPuff_DM_NoFX",56)
			WP30 I 1
			WP30 A 0 A_JumpIfInventory("PunchedAlready",1,"PKGauntStop")

			WP30 B 0 A_ReFire("PKIGRefire")
			Goto PKEndFire

	PKGauntStop:
		WP30 A 0 A_StopSound(CHAN_WEAPON)
		WP30 A 0 A_PlaySound("QC/Abilities/Painkiller/Weapon/SpinStop",CHAN_WEAPON,1,0)
		WP30 FGHIFGHI 1
		WP30 E 3 A_TakeInventory("PunchedAlready",32767)
		WP30 A 8
		Goto PKReady
	PKEndFire:
		WP30 C 0 A_PlaySound("QC/Abilities/Painkiller/Weapon/SpinStop",CHAN_WEAPON,1,0)
		WP30 E 1 A_TakeInventory("PunchedAlready",32767)
		WP30 E 1 A_WeaponReady
		WP30 E 1 A_WeaponReady
		Goto PKReady



	//-----------------------
	// Fire Seq / Zoom
	//-----------------------
	PKAltFire:
		WP30 A 0 A_JumpIfInventory("PKTipDetached",1,"PKPlayIsBackSound")
		WP30 A 0 A_PlaySound("QC/Abilities/Painkiller/Weapon/ShootTip",CHAN_WEAPON,1.0)
		WP30 A 0 A_JumpIfInventory("DMToken",1,"PKFireBlade_DM")

		PKFireBlade:
			WP30 N 1 Bright A_GiveInventory("PKTipDetached",1,0, GIF_SKIPOWNER)
			WP30 B 1 A_FireCustomMissile("PKProjectile", 0, false, 0, 5)
			Goto PKResumeFireBlade
		PKFireBlade_DM:
			WP30 N 1 Bright A_GiveInventory("PKTipDetached",1,0, GIF_SKIPOWNER)
			WP30 B 1 A_FireCustomMissile("PKProjectileDM", 0, false, 0, 5)
			Goto PKResumeFireBlade

	PKResumeFireBlade:
		WP30 B 0 A_AlertMonsters
		//detach animation
		WP30 C 0 A_JumpIfInventory("PKTipDetached",-1,"PKPlayIsBackSound")
		WP30 C 2
		WP30 C 0 A_JumpIfInventory("PKTipDetached",-1,"PKPlayIsBackSound")
		WP30 C 2
	PKWaiterLoopIfHoldingAltFire:
		WP30 C 2 A_JumpIfInventory("PKTipDetached",-1,"PKPlayIsBackSound")
		WP30 C 0 A_Refire("PKWaiterLoopIfHoldingAltFire") //if player holds altfire after shooting the tip, it loops to do nothing
		goto WaitingForTip

		//Reached after tip was shot, player releases alt fire, and weapons goes here
		WaitingForTip:
			WP30 C 0 A_JumpIfInventory("PKTipDetached",-1,"PKPlayIsBackSound")
			WP30 C 2 A_WeaponReady(WRF_NOBOB)
			Loop


	PKPlayIsBackSound:
		WP30 A 0 A_TakeInventory("PKTipDetached",32767,TIF_SKIPOWNER)
		WP30 N 0 A_PlaySound("QC/Abilities/Painkiller/Weapon/TipReturn",CHAN_WEAPON,1.0)
		WP30 N 1 Bright Offset(9,41)
		WP30 N 1 Bright Offset(19,51)
		WP30 A 4 Offset(19,51)
		WP30 A 1 Offset(17,49)
		WP30 A 1 Offset(15,47)
		WP30 A 1 Offset(13,45)
		WP30 A 1 Offset(11,43)
		WP30 A 1 Offset(9,41)
		WP30 A 1 Offset(7,39)
		WP30 A 1 Offset(5,37)
		WP30 A 1 Offset(3,35)
		WP30 A 1 Offset(2,34)
		WP30 A 1 Offset(1,33)
		WP30 A 1 Offset(0,32)
		Goto PKReady
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	PKSelect:
		WP30  A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		WP30  A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3)
		"####" "#" 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		"####" "#" 0 A_TakeInventory("PunchedAlready",32767)
		"####" "#" 0 A_JumpIfInventory("PKTipDetached",1,"RaiseDetached")
	PKSelectLooper:
		"####" "#" 1 A_Raise
		"####" "###" 0 A_Raise
		Loop
	RaiseDetached:
		"####" C 1 A_Raise
		"####" "###" 0 A_Raise
		Loop

	PKDetachedDeSelect:
		WP30 C 0 A_Jump(255,"DeselectLooper")
		Goto DeselectLooper
	}
}



//-----------------------------------
// Puff for SP/COOP
Actor GauntletPuff
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY
	+PUFFGETSOWNER
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	+MTHRUSPECIES
	Renderstyle Add
	Decal "GauntletDecal"
	Scale 0.35
	DamageType "ZombieGibber"
	States
	{
	Spawn:	//Hit non bleeder
		TNT1 A 0 NODELAY A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		Goto Crash
	XDeath:	//hit bleeder
		TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 10 A_PlaySound("QC/Weapons/Gauntlet/Hit",7,1,0,9)
		Stop
	Crash:	//hit everything else
		TNT1 A 0 A_PlaySound("BulletHitConcreteRandom",7,0.6,0,9)
	Choose:
		TNT1 A 0 A_Jump(256,"Type1","Type2")
	Type1:
		FX57 ABCDEFGHIJ 2 Bright
		Stop
	Type2:
		FX57 KLMNOPQRST 2 Bright
		Stop
	}
}

Actor GauntletPuff_Quake3 : GauntletPuff
{
	Decal "GauntletDecal_Quake3"
}

Actor GauntletPuff_Quake4 : GauntletPuff
{
	Decal "GauntletDecal_Quake4"
}

Actor GauntletPuff_QCDE1 : GauntletPuff
{
	Decal "GauntletDecal_QCDE1"
}


//-----------------------------------
// Puffs for DM
// See "Decorate/Abilities/Painkiller.QCD" for those puffs

Actor GauntletPuff_DM
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY
	+PUFFGETSOWNER
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	Renderstyle Add
	Decal "GauntletDecal"
	Scale 0.35
	Obituary "$OB_GauntletPuff_DM" // "%o was pummeled by %k." // variation line
	States
	{
	Spawn:	//Hit non bleeder
		TNT1 A 0 NODELAY A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		Goto Crash
	XDeath: //hit bleeder
		TNT1 A 0 A_GiveToTarget("PunchedAlready",1)
		TNT1 A 0 A_GiveToTarget("HitBeepBig",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		//Medals start
		TNT1 A 0 A_GiveToTarget("GDamageToken",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE|GIF_SKIPOWNER)
		//Medals end
		TNT1 A 10 A_PlaySound("QC/Weapons/Gauntlet/Hit",7,1,0,9)
		Stop
	Crash:	//hit everything else
		TNT1 A 0 A_PlaySound("BulletHitConcreteRandom",7,0.6,0,9)
	Choose:
		TNT1 A 0 A_Jump(256,"Type1","Type2")
	Type1:
		FX57 ABCDEFGHIJ 2 Bright
		Stop
	Type2:
		FX57 KLMNOPQRST 2 Bright
		Stop
	}
}

Actor GauntletPuff_DM_Quake3 : GauntletPuff_DM
{
	Decal "GauntletDecal_Quake3"
}

Actor GauntletPuff_DM_Quake4 : GauntletPuff_DM
{
	Decal "GauntletDecal_Quake4"
}

Actor GauntletPuff_DM_QCDE1 : GauntletPuff_DM
{
	Decal "GauntletDecal_QCDE1"
}

Actor GauntletPuff_DM_NoFX
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY
	+PUFFGETSOWNER
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	Obituary "$OB_GauntletPuff_DM_NoFX" // "%o tried to stop %k's rotating blade with a wrong limb." // variation line
	States
	{
	Spawn:
	Crash:
		TNT1 A 0
		TNT1 A 10 A_PlaySound("BulletHitConcreteRandom",7,0.6,0,9)
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("PunchedAlready",1)
		TNT1 A 0 A_GiveToTarget("HitBeepBig",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 10 A_PlaySound("BulletHitFleshRandom",7,0.6,0,9)
		Stop
	}
}



Actor QCGauntlet_Bot : QCGauntlet
{
	States
	{
		DMFireLooper:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")

			"####" B 1 A_CustomPunch(75,1,0,"GauntletPuff_DM",70)
			"####" A 1 A_Recoil(-4)
			"####" A 0 A_JumpIfInventory("PunchedAlready",1,"GauntStop")

			"####" B 1 A_CustomPunch(75,1,0,"GauntletPuff_DM_NoFX",70)
			"####" A 1 A_Recoil(-3)
			"####" A 0 A_JumpIfInventory("PunchedAlready",1,"GauntStop")

			"####" B 0 A_ReFire("DMFireLooper")
			Goto EndFire

		PKDMFireLooper:
			WP30 A 0 A_JumpIfInventory("Instagib",1,"PKIGRefire")
		PKNormalDMRefire:
			WP30 A 0 A_Jump(10,"AltFire")
			WP30 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"PKReadyCantFire")

			WP30 F 1 A_CustomPunch(90,1,0,"PKGauntletPuff_DM",56)
			WP30 G 1 A_Recoil(-4)
			TNT1 A 0 A_JumpIfInventory("PunchedAlready",1,"PKGauntStop")

			WP30 H 1 A_CustomPunch(90,1,0,"PKGauntletPuff_DM_NoFX",56)
			WP30 I 1 A_Recoil(-3)
			TNT1 A 0 A_JumpIfInventory("PunchedAlready",1,"PKGauntStop")

			WP30 B 0 A_ReFire("PKNormalDMRefire")
			Goto PKEndFire

	}
}

//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Weapon tokens
//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
Actor GNT_UG_Omni		: Token {}
Actor GNT_UG_Burn		: Token {}
Actor QCGauntletSkin	: Token { Inventory.MaxAmount 20 }

//---------------------------
// Omnislash
Actor OmnislashProjectile
{
	Projectile
	Speed 30
	Radius 2
	Height 2
	ReactionTime 1
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[0.8,0,1.5]"
	Scale 0.7
	Alpha 0.4
	+BRIGHT
	ProjectileKickBack 0
	States
	{
	Spawn:
		QCG1 X 0
		"####" X 1
		"####" X 0 A_CountDown
		Loop
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,80)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont

	DeathCont:
		"####" X 0 A_Explode(random(4,6)*10,48,0,0,32)
		"####" XXXXXXXXXX 1 A_FadeOut
		Stop
	XDeath:
		"####" X 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		"####" A 0 A_Jump(255,"DeathCont")
		Goto DeathCont
	}
}

Actor OmnislashProjectile_Quake3 : OmnislashProjectile
{
	States
	{
	Spawn:
		Q3G1 X 1
		Goto Super::Spawn+1
	}
}

Actor OmnislashProjectile_Quake4 : OmnislashProjectile
{
	States
	{
	Spawn:
		Q4G1 X 1
		Goto Super::Spawn+1
	}
}

Actor OmnislashProjectileFX
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NONETID
	Projectile
	Speed 30
	Radius 2
	Height 2
	ReactionTime 1
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[0.8,0,1.5]"
	Scale 0.7
	Alpha 0.4
	+BRIGHT
	ProjectileKickBack 0
	States
	{
	Spawn:
		QCG1 X 0
		"####" X 1
		"####" X 0 A_CountDown
		Loop
	Death:
		"####" XXXXXXXXXX 1 A_FadeOut
		Stop
	}
}

Actor OmnislashProjectileFX_Quake3 : OmnislashProjectileFX
{
	States
	{
	Spawn:
		Q3G1 X 0
		Goto Super::Spawn+1
	}
}

Actor OmnislashProjectileFX_Quake4 : OmnislashProjectileFX
{
	States
	{
	Spawn:
		Q4G1 X 0
		Goto Super::Spawn+1
	}
}


//---------------------------
// Burner
Actor BurnerFlamelet
{
	Projectile
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY
	+NONETID
	+FORCEXYBILLBOARD
	RenderStyle Add
	Scale 0.07
	Alpha 0
	Speed 7
	Translation "0:255=%[0,0,0]:[1.5,1.0,0]"
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Stop
		X123 UVWX 1 A_FadeIn(0.25)
	Looper:
		X123 ABCDEFGHIJKLMNOPQRSTUVWX 1 Bright A_FadeOut(0.05)
		Loop
	}
}

Actor GauntletPuffBurn
{
	+NOINTERACTION +NOTONAUTOMAP
	+PUFFGETSOWNER
	+PUFFONACTORS
	+FORCEXYBILLBOARD
	+MTHRUSPECIES
	+SERVERNETID
	Renderstyle Add
	Decal "GauntletDecal"
	DamageType "ZombieGibber"
	ProjectileKickBack 0
	Scale 0.35
	States
	{
	Spawn:	//Hit non bleeder
		TNT1 A 0 NODELAY A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_SpawnItemEx("GauntletFlameBurner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_FORCESERVERSIDE,128)
		Goto Crash
	XDeath:	//hit bleeder
		TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_SpawnItemEx("GauntletFlameBurner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_FORCESERVERSIDE,128)	//if modified, change in Painkiller.QCD too
		TNT1 A 10 A_PlaySound("QC/Weapons/Gauntlet/Hit",7,1,0,9)
		Stop
	Crash:	//hit everything else
		TNT1 A 0 A_PlaySound("BulletHitConcreteRandom",7,0.6,0,9)
	Choose:
		TNT1 A 0 A_Jump(256,"Type1","Type2")
	Type1:
		FX57 ABCDEFGHIJ 2 Bright
		Stop
	Type2:
		FX57 KLMNOPQRST 2 Bright
		Stop
	}
}

Actor GauntletPuffBurn_Quake3 : GauntletPuffBurn
{
	Decal "GauntletDecal_Quake3"
}

Actor GauntletPuffBurn_Quake4 : GauntletPuffBurn
{
	Decal "GauntletDecal_Quake4"
}

Actor GauntletPuffBurn_QCDE1 : GauntletPuffBurn
{
	Decal "GauntletDecal_QCDE1"
}

Actor GauntletFlameBurner
{
	Projectile
	+THRUACTORS
	+NOINTERACTION
	-NOGRAVITY
	Gravity 0.1
	+NODAMAGETHRUST
	+BLOODLESSIMPACT
	+SEEKERMISSILE
	+FORCERADIUSDMG
	+SERVERNETID
	RenderStyle None
	Radius 1
	Height 1
	Damage 0
	Speed 0
	Scale 0.38
	var int user_counter;
	Translation "0:255=%[0,0,0]:[1.5,1.0,0]"
	DamageType "ZombieGibber"
	ProjectileKickBack 0
	States
	{
	Spawn:
		VTFB A 0 NODELAY A_Jump(180,2) // ~30% chance it will not jump, and instead will continue causing pain to monsters
		VTFB A 0 A_ChangeFlag("Painless",1)

		Looper:
			VTFB A 0 A_JumpIf(user_counter>=7,"Ender")	//140 tics, 4 seconds
			VTFB A 0 A_SpawnItemEx("NailFlameFX", random(4,-4),random(4,-4),0, 0,0,frandom(0.05,0.4), 0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
			VTFB A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED ,256)
			VTFB A 0 A_JumpIfTargetInLOS("Sounder",360,JLOSF_PROJECTILE,50)
			VTFB A 0 A_Jump(255,"ContinueLooper")
		ContinueLooper:
			VTFB A 0 A_Explode(15,32,0,0,32)
			VTFB AAAAAAAAAA 1 Light("YellowSmallFlicker1")
			VTFB A 0 A_SetUserVar(user_counter, user_counter+1)
			Goto Looper

	Sounder:
		VTFB A 0 A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		VTFB A 0 A_Jump(255,"ContinueLooper")
		Goto ContinueLooper

	Ender:
		VTFB AAAAAA 2 Light("YellowSmallFlicker1") A_FadeOut(0.1)
		VTFB A 2 Light("YellowSmallFlicker1") A_FadeOut(0.1)
		VTFB A 2 Light("YellowSmallFlicker2") A_FadeOut(0.1)
		VTFB A 2 Light("YellowSmallFlicker2") A_FadeOut(0.1)
		VTFB A 2 Light("YellowSmallFlicker3") A_FadeOut(0.1)
		Stop
	}
}


//---------------------------
// Omnislash + Burner
Actor OmnislashFireProjectile
{
	Projectile
	Speed 30
	Radius 2
	Height 2
	ReactionTime 1
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[1.5,0.5,0]"
	Scale 0.9
	Alpha 0.5
	+BRIGHT
	+SEEKERMISSILE
	DamageType "ZombieGibber"
	ProjectileKickBack 0
	States
	{
	Spawn:
		TNT1 A 0 A_CountDown
		Loop
	Death:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,80)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont

	DeathCont:
		TNT1 A 0 A_Explode(random(4,6)*10,48,0,0,32)
		Stop
	XDeath:
		TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_SpawnItemEx("GauntletFlameBurner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,128)
		TNT1 A 0 A_Jump(255,"DeathCont")
		Goto DeathCont
	}
}

Actor OmnislashFireProjectileFX
{
	Projectile
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NONETID
	Speed 30
	Radius 2
	Height 2
	ReactionTime 1
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[1.5,0.5,0]"
	Scale 0.9
	Alpha 0.5
	+BRIGHT
	States
	{
	Spawn:
		QCG1 X 0
		"####" X 1 A_SpawnItemEx("BurnerFlamelet",random(0,-20),random(7,-7),random(-3,3), 0,0,0, 0,SXF_NOCHECKPOSITION,128)
		"####" X 0 A_CountDown
		Loop
	Death:
		"####" XXXXXXXXXX 1 A_FadeOut(0.13)
		Stop
	}
}

Actor OmnislashFireProjectileFX_Quake3 : OmnislashFireProjectileFX
{
	States
	{
	Spawn:
		Q3G1 X 0
		Goto Super::Spawn+1
	}
}

Actor OmnislashFireProjectileFX_Quake4 : OmnislashFireProjectileFX
{
	States
	{
	Spawn:
		Q4G1 X 0
		Goto Super::Spawn+1
	}
}
