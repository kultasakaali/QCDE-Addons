// --------------------------------------------------------------------------
//
// Shotgun
// Weapon and Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor CompatVer30_Shotgun {} // for compatibility checks

Actor ShotgunPickup : CustomInventory 15601
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "Shotgun (DM becomes ammo)"
	Height 48
	Radius 20
	Scale 0.55
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCShotgun" // "Picked up a Shotgun."
	Tag "$TAG_QCShotgun" // "Shotgun"
	Inventory.RespawnTics 35
	States
	{
	Spawn:
		QCG3 Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		QCG3 Y 35 A_CheckFlag("DROPPED", "BecomeWeaponItself")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		TNT1 A 0 A_Jump(256,"BeHourglass","BeBackpack","BeWeapon")
		Stop
	BeHourglass:
		TNT1 A 0 A_SpawnItemEx("BigHourglass")
		Stop
	BeBackpack:
		TNT1 A 0 A_SpawnItemEx("QCBackpack")
		Stop
	BeWeapon:
		TNT1 A 0 A_Jump(256,"BeHeavyMachinegun","BeSuperNailgun","BeTribolt")
		Stop
		BeHeavyMachinegun:
			TNT1 A 0 A_SpawnItemEx("HeavyMachinegunPickup")
			Stop
		BeSuperNailgun:
			TNT1 A 0 A_SpawnItemEx("SuperNailgunPickup")
			Stop
		BeTribolt:
			TNT1 A 0 A_SpawnItemEx("TriboltPickup")
			Stop

	BecomeWeaponItself:
		TNT1 A 0 A_SpawnItemEx("QCShotgun")
		TNT1 A 0 A_JumpIf(true, "End") Goto Idle // <-- this is to fool client-side predictor
	End:TNT1 A 0
		Stop

	Idle:
		QCG3 Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",3,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCShotgun")
		TNT1 A 0
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCShotgun",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCShotgun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto Super::HideDoomish+1
	HideDoomish:
		TNT1 A 1
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto Super::HideDoomish+1
	}
}

Actor ShotgunPickupFloat : ShotgunPickup 15611
{
	//$Title "Float Shotgun"
	+NOGRAVITY
}


Actor QCShotgun : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 3.2
	Weapon.Kickback 35
	Weapon.SelectionOrder 1940
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "QCShellAmmo"
	Obituary "$OB_QCShotgun"
	Inventory.PickupMessage "$GOT_QCShotgun"
	Tag "$TAG_QCShotgun"
	Scale 0.55
	States
	{
	Spawn:
		TNT1 A 1
		QCG3 Y 35 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCG3 Z 0 Bright A_ChangeFlag("NOBLOCKMAP",1)	//in DM, if a player drops this weapon, cant be picked
		TNT1 Z 3	//and disappears
		Stop
	Idle:
		QCG3 Y -1
		Wait
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 7, "Ready.HalfLife")
		TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 6, "Ready.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 2, "Ready.Quake2")
		TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 1, "Ready.Quake1")
	Ready.Default:
		QCG3 A 0
		Goto ReadyCont
	Ready.Quake1:
		Q1G3 A 0
		Goto ReadyCont
	Ready.Quake2:
		Q2G3 A 0
		Goto ReadyCont
	Ready.QCDE1:
		Q0G3 A 0
		Goto ReadyCont
	Ready.HalfLife:
		HLG3 A 0
		Goto ReadyCont

	ReadyCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 3 A_WeaponReady
		Loop

		ReadyCantFire:
			"####" A 0 A_StopSound(CHAN_WEAPON)
		RCFLooper:
			"####" A 4 A_WeaponReady(WRF_NOFIRE)
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 7, "Select.HalfLife")
		TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 6, "Select.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 2, "Select.Quake2")
		TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 1, "Select.Quake1")
	Select.Default:
		QCG3 A 0
		Goto SelectCont
	Select.Quake1:
		Q1G3 A 0
		Goto SelectCont
	Select.Quake2:
		Q2G3 A 0
		Goto SelectCont
	Select.QCDE1:
		Q0G3 A 0
		Goto SelectCont
	Select.HalfLife:
		HLG3 A 0
		Goto SelectCont

	SelectCont:
		"####" A 0 A_PlaySound("QC/Weapons/Switch",CHAN_5,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
		"####" A 0 ACS_NamedExecuteWithResult("QCDE_ZoomCheckSelect")
	SelectLooper:
		"####" A 1 A_Raise
		"####" AAA 0 A_Raise
		Loop

	Deselect:
	DeselectLooper:
		"####" A 1 A_Lower
		"####" AAA 0 A_Lower
		Loop

	//-----------------------
	// Fire Seq
	//-----------------------
	Fire: 	//28 tics, 0.8sec
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 0 A_AlertMonsters
		"####" B 0 Bright A_GunFlash("PrimaryFlash")
		"####" B 1 Bright
		"####" B 1 Bright Offset(4,27)
		"####" C 1 Offset(11,19)
		"####" D 1 Offset(23,11)
		"####" D 1 Offset(32,9)
		"####" E 1 Offset(38,7)
		"####" E 1 Offset(44,6)
		"####" F 1
		"####" F 1
		"####" F 1 Offset(42,7)  //10
		"####" F 1 Offset(39,8)
		"####" F 1 Offset(36,9)
		"####" F 1 Offset(33,11)
		"####" F 1 Offset(30,14)
		"####" F 1 Offset(26,17)
		"####" G 1 Offset(23,20)
		"####" G 1 Offset(20,23)
		"####" G 1 Offset(18,27)
		"####" H 1 Offset(16,28)
		"####" H 1 Offset(14,29)	//20
		"####" H 1 Offset(12,31)
		"####" I 1 Offset(10,32)
		"####" I 1 Offset(8,33)
		"####" I 1 Offset(6,34)
		"####" A 1 Offset(4,35)
		"####" A 1 Offset(2,34)
		"####" A 1 Offset(1,33)
		"####" A 1 Offset(0,32)	//28
		Goto Ready

	PrimaryFlash: //Attacks doer
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"DM_Flash")
		Goto SP_Flash

		SP_Flash:
			TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfModifier")
			TNT1 A 0 A_JumpIfInventory("SG_UG_Spread",1,"DoSpread")
			TNT1 A 0 A_FireBullets(6,2,7,random(8,12),"QCShotgunPuff",FBF_NORANDOM|FBF_USEAMMO)
			TNT1 AAAA 0 A_FireCustomMissile("BulletSmallTracer",frandom(6,-6),false,3,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(2,-2))
			Goto FlashCheckSound

			DoSpread:
				TNT1 A 0 A_FireBullets(2,1,7,random(10,14),"QCShotgunPuff",FBF_NORANDOM|FBF_USEAMMO)
				TNT1 AAAA 0 A_FireCustomMissile("BulletSmallTracer",frandom(2,-2),false,3,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(1.5,-1.5))
				Goto FlashCheckSound

			CheckIfModifier:
				TNT1 A 0 A_JumpIfInventory("SG_UG_Explosive",1,"DoExplosive")
				Goto SP_Flash+1
			DoExplosive:
				TNT1 A 0 A_JumpIfInventory("SG_UG_Spread",1,"DoExplosiveSpread")
				TNT1 A 0 A_FireCustomMissile("SGExplosiveBullet",frandom(6,-6),true,3,0,0,frandom(2,-2))
				TNT1 AAAAAAA 0 A_FireCustomMissile("SGExplosiveBullet",frandom(6,-6),false,3,0,0,frandom(2,-2))
				Goto FlashCheckSound

				DoExplosiveSpread:
					TNT1 A 0 A_FireCustomMissile("SGExplosiveBullet",frandom(2,-2),true,3,0,0,frandom(1.5,-1.5))
					TNT1 AAAAAAA 0 A_FireCustomMissile("SGExplosiveBullet",frandom(2,-2),false,3,0,0,frandom(1.5,-1.5))
					Goto FlashCheckSound


		DM_Flash:
			TNT1 A 0 A_FireBullets(sin(  0)*2.65  ,cos(  0)*2.65   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_USEAMMO|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin( 36)*2.75  ,cos( 36)*2.75   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin( 72)*2.75  ,cos( 72)*2.75   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(108)*2.75  ,cos(108)*2.75   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(144)*2.75  ,cos(144)*2.75   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(180)*2.75  ,cos(180)*2.75   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(216)*2.75  ,cos(216)*2.75   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(252)*2.75  ,cos(252)*2.75   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(288)*2.75  ,cos(288)*2.75   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(324)*2.75  ,cos(324)*2.75   ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(  0)*5.5  ,cos(  0)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin( 36)*5.5  ,cos( 36)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin( 72)*5.5  ,cos( 72)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(108)*5.5  ,cos(108)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(144)*5.5  ,cos(144)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(180)*5.5  ,cos(180)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(216)*5.5  ,cos(216)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(252)*5.5  ,cos(252)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(288)*5.5  ,cos(288)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(324)*5.5  ,cos(324)*5.5  ,-1,3,"QCShotgunPuff_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 AAAA 0 A_FireCustomMissile("BulletSmallTracer",frandom(5.0,-5.0),false,3,0,FPF_NOUNLAGGED|FPF_SKIPOWNER,frandom(5.0,-5.0))
			Goto FlashCheckSound

		FlashCheckSound:
			TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"QuadSound")
			TNT1 A 0 A_JumpIfInventory("PowerTerminatorArtifact",1,"QuadSound")
			Goto NormalSound

			NormalSound:
				TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 7, "NormalSound.HalfLife")
				TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 6, "NormalSound.Default")
				TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 2, "NormalSound.Quake2")
				TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 1, "NormalSound.Quake1")
			NormalSound.Default:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Fire_Default",CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.Quake1:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Fire_Quake1",CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.Quake2:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Fire_Quake2",CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.HalfLife:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Fire_HalfLife",CHAN_WEAPON)
				Goto FlashEnder

			QuadSound:
				TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 7, "QuadSound.HalfLife")
				TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 6, "QuadSound.Default")
				TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 2, "QuadSound.Quake2")
				TNT1 A 0 A_JumpIfInventory("QCShotgunSkin", 1, "QuadSound.Quake1")
			QuadSound.Default:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Quad/Fire_Default",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.Quake1:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Quad/Fire_Quake1",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.Quake2:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Quad/Fire_Quake2",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.HalfLife:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Quad/Fire_HalfLife",CHAN_WEAPON)
				Goto FlashEnder

	FlashEnder:
		TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
		TNT1 A 0 A_SetBlend("Orange",0.10,10)
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"FlashEnderNoRecoil")
		TNT1 A 0 A_SetPitch(pitch-3.5)
		TNT1 AA 1 A_FireCustomMissile("SGSmokeSpawner",0, false,4,4)
		TNT1 A 2
		TNT1 AAAAAAA 1 A_SetPitch(pitch+0.5)
		Stop
	FlashEnderNoRecoil:
		TNT1 AA 1 A_FireCustomMissile("SGSmokeSpawner",0, false,4,4)
		Stop
	}
}



Actor SGSmokeSpawner
{
	Radius 1
	Height 1
	Speed 14
	PROJECTILE
	+NOTIMEFREEZE
	+NOCLIP
	+CLIENTSIDEONLY +NONETID
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Die
		Goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("SGSmoke",0,random(1,-1),0,0,0,frandom(0.1,0.25),0,32)
		TNT1 A 2
		Stop
	}
}
Actor SGSmoke
{
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY +NONETID
	Alpha 0.9
	Scale 0.04
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"Type1","Type2")	//to define which graphic will use
	Type1:
		X201 E 0
		Goto "Continue"
	Type2:
		X201 F 0
		Goto "Continue"
	Continue:
		"####" "#" 0 A_Jump(160,2,4,6)
		"####" "#" 0
		"####" "#" 0 A_SetScale(0.04)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.045)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.05)
		"####" "#" 2 A_FadeOut(0.035)
	Looper:
		"####" "#" 0 A_SetScale(ScaleX+0.005)
		"####" "#" 2 A_FadeOut(0.045)
		Loop
	}
}




//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Weapon tokens
//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
Actor SG_UG_Spread 		: Token {}
Actor SG_UG_Explosive	: Token {}
Actor QCShotgunSkin		: Token { Inventory.MaxAmount 20 }


Actor SGExplosiveBullet : FastProjectile
{
	Projectile
	+RANDOMIZE
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+NOEXTREMEDEATH
	+DONTBLAST
	+BLOODSPLATTER
	+SEEKERMISSILE
	+MTHRUSPECIES
	damage (random(12,18))
	radius 2
	height 2
	speed 90
	renderstyle ADD
	alpha 0.9
	scale 0.025
	Translation "0:255=%[0,0,0]:[1.4,0.7,0]"
	DamageType "NoDamageToPlayers"
	ProjectileKickBack 0
	states
	{
	Spawn:
		G3EX A 1 BRIGHT
		G3EX A 70 BRIGHT A_PlaySound("QCDE/Other/BulletWhoosh",CHAN_AUTO,1.0,false,3.0)
		Wait
	XDeath:
		TNT1 A 1 A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		Stop

	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,180)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont

	DeathCont:
		TNT1 A 0 A_SetScale(0.35)
		TNT1 A 0 A_Explode(30,112,0,1,0)
		TNT1 A 0 A_Jump(256,"Type1","Type2")
	Type1:
		X006 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	Type2:
		TNT1 A 0 A_SetScale(-0.35)
		X006 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	}
}




// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCShotgunDM : QCShotgun
{
	+WEAPON.NOAUTOAIM
	+CHEATNOTWEAPON
	Tag "$TAG_QCShotgun" // "Shotgun (DM)"
	Weapon.AmmoGive 10
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 1
}
