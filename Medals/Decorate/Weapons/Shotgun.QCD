// --------------------------------------------------------------------------
//
// Shotgun
// Weapon and Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor ShotgunPickup : CustomInventory 15601
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "Shotgun (DM becomes ammo)"
	Height 48
	Radius 20
	Scale 0.55
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCShotgun" // "Picked up a Shotgun."
	Tag "$TAG_QCShotgun" // "Shotgun"
	Inventory.RespawnTics 35
	States
	{
	Spawn:
		QCG3 Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		QCG3 Y 35 A_CheckFlag("DROPPED", "BecomeWeaponItself")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		TNT1 A 0 A_Jump(256,"BeHourglass","BeBackpack","BeWeapon")
		Stop
	BeHourglass:
		TNT1 A 0 A_SpawnItemEx("BigHourglass")
		Stop
	BeBackpack:
		TNT1 A 0 A_SpawnItemEx("QCBackpack")
		Stop
	BeWeapon:
		TNT1 A 0 A_Jump(256,"BeHeavyMachinegun","BeSuperNailgun","BeTribolt")
		Stop
		BeHeavyMachinegun:
			TNT1 A 0 A_SpawnItemEx("HeavyMachinegunPickup")
			Stop
		BeSuperNailgun:
			TNT1 A 0 A_SpawnItemEx("SuperNailgunPickup")
			Stop
		BeTribolt:
			TNT1 A 0 A_SpawnItemEx("TriboltPickup")
			Stop
	
	BecomeWeaponItself:
		TNT1 A 0 A_SpawnItemEx("QCShotgun")
		TNT1 A 0 A_JumpIf(true, "End") Goto Idle // <-- this is to fool client-side predictor
	End:TNT1 A 0
		Stop
	
	Idle:
		QCG3 Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",3,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCShotgun")
		TNT1 A 0 
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCShotgun",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCShotgun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto HideDoomish+1
	}
}


Actor QCShotgun : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 1.6
	Weapon.Kickback 35
	Weapon.SelectionOrder 1940
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "QCShellAmmo"
	Obituary "$OB_QCShotgun" // "%o was shot down by %k."
	Inventory.PickupMessage "$GOT_QCShotgun" // "Picked up a Shotgun."
	Tag "$TAG_QCShotgun" // "Shotgun"
	Scale 0.55
	States
	{
	Spawn:
		QCG3 Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCG3 Z 0 Bright A_ChangeFlag("NOBLOCKMAP",1)	//in DM, if a player drops this weapon, cant be picked
		TNT1 Z 3	//and disappears
		Stop
	Idle:
		QCG3 Y -1
		Wait
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		QCG3 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG3 A 3 A_WeaponReady
		Loop
		
		ReadyCantFire:
			QCG3 A 0 A_StopSound(5)
			QCG3 A 0 A_StopSound(6)
			QCGC A 0 A_StopSound(CHAN_WEAPON)
			"####" "#" 0 A_ZoomFactor(1.0)
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			QCG3 A 4 A_WeaponReady(WRF_NOFIRE)
			QCG3 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		QCG3 A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		QCG3 A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" "#" 0 A_StopSound(5) //stops any weapon looping sound
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		QCG3 A 1 A_Raise
		"####" "###" 0 A_Raise
		Loop
		
	Deselect:
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:
		QCG3 A 1 A_Lower
		"####" "###" 0 A_Lower
		Loop
		
	//-----------------------
	// Fire Seq
	//-----------------------
	Fire: 	//28 tics, 0.8sec
		QCG3 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		TNT1 A 0 A_AlertMonsters
		QCG3 B 0 Bright A_GunFlash("PrimaryFlash")
		"####" B 1 Bright 
		"####" B 1 Bright Offset(5,28) 
		"####" C 1 Offset(12,20) 
		"####" D 1 Offset(24,12) 
		"####" D 1 Offset(33,10) 
		"####" E 1 Offset(39,8) 
		"####" E 1 Offset(45,7) 
		"####" F 1
		"####" F 1
		"####" F 1 Offset(43,8)  //10
		"####" F 1 Offset(40,9) 
		"####" F 1 Offset(37,10)
		"####" F 1 Offset(34,12)
		"####" F 1 Offset(31,15)
		"####" F 1 Offset(27,18)
		"####" G 1 Offset(24,21)
		"####" G 1 Offset(21,24)
		"####" G 1 Offset(19,28)
		"####" H 1 Offset(17,29)
		"####" H 1 Offset(15,30)	//20
		"####" H 1 Offset(13,32)
		"####" A 1 Offset(11,33)
		"####" A 1 Offset(9,34)
		"####" A 1 Offset(7,35)
		"####" A 1 Offset(5,36)
		"####" A 1 Offset(3,35)
		"####" A 1 Offset(2,34)
		"####" A 1 Offset(1,33)	//28
		Goto Ready
		
	PrimaryFlash: //Attacks doer
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"DM_Flash")
		Goto SP_Flash
			
		SP_Flash:
			TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfModifier")
			TNT1 A 0 A_JumpIfInventory("SG_UG_Spread",1,"DoSpread")
			TNT1 A 0 A_FireBullets(6,2,7,random(8,12),"QCShotgunPuff",FBF_NORANDOM|FBF_USEAMMO)
			TNT1 AAAA 0 A_FireCustomMissile("BulletSmallTracer",frandom(6,-6),false,3,0,0,frandom(2,-2))
			Goto FlashCheckSound
			
			DoSpread:
				TNT1 A 0 A_FireBullets(2,1,7,random(10,14),"QCShotgunPuff",FBF_NORANDOM|FBF_USEAMMO)
				TNT1 AAAA 0 A_FireCustomMissile("BulletSmallTracer",frandom(2,-2),false,3,0,0,frandom(1.5,-1.5))
				Goto FlashCheckSound
			
			CheckIfModifier:
				TNT1 A 0 A_JumpIfInventory("SG_UG_Explosive",1,"DoExplosive")
				Goto SP_Flash+1
			DoExplosive:
				TNT1 A 0 A_JumpIfInventory("SG_UG_Spread",1,"DoExplosiveSpread") 
				TNT1 A 0 A_FireCustomMissile("SGExplosiveBullet",frandom(6,-6),true,3,0,0,frandom(2,-2))
				TNT1 AAAAAAA 0 A_FireCustomMissile("SGExplosiveBullet",frandom(6,-6),false,3,0,0,frandom(2,-2))
				Goto FlashCheckSound
				
				DoExplosiveSpread:
					TNT1 A 0 A_FireCustomMissile("SGExplosiveBullet",frandom(2,-2),true,3,0,0,frandom(1.5,-1.5))
					TNT1 AAAAAAA 0 A_FireCustomMissile("SGExplosiveBullet",frandom(2,-2),false,3,0,0,frandom(1.5,-1.5))
					Goto FlashCheckSound
			
			
		DM_Flash:
			//Medals start
			//TNT1 A 0 ACS_NamedExecuteAlways("QCDE_PointBlank",0) //we are no longer executing this from here as we do the checks on frag event
			//Medals end
			TNT1 A 0 A_FireBullets(sin(  0)*2.65  ,cos(  0)*2.65   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_USEAMMO|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin( 36)*2.75  ,cos( 36)*2.75   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin( 72)*2.75  ,cos( 72)*2.75   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(108)*2.75  ,cos(108)*2.75   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(144)*2.75  ,cos(144)*2.75   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(180)*2.75  ,cos(180)*2.75   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(216)*2.75  ,cos(216)*2.75   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(252)*2.75  ,cos(252)*2.75   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(288)*2.75  ,cos(288)*2.75   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(324)*2.75  ,cos(324)*2.75   ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 A 0 A_FireBullets(sin(  0)*5.5  ,cos(  0)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin( 36)*5.5  ,cos( 36)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin( 72)*5.5  ,cos( 72)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(108)*5.5  ,cos(108)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(144)*5.5  ,cos(144)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(180)*5.5  ,cos(180)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(216)*5.5  ,cos(216)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(252)*5.5  ,cos(252)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(288)*5.5  ,cos(288)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)
			TNT1 A 0 A_FireBullets(sin(324)*5.5  ,cos(324)*5.5  ,-1,3,"QCShotgunPuffHit_DM",FBF_NORANDOM|FBF_EXPLICITANGLE)

			TNT1 AAAA 0 A_FireCustomMissile("BulletSmallTracer",frandom(5.0,-5.0),false,3,0,0,frandom(5.0,-5.0))
			Goto FlashCheckSound
			
		FlashCheckSound:
			TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"QuadSound")
			Goto NormalSound
			
			NormalSound:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Fire",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound:
				TNT1 A 0 A_PlaySound("QC/Weapons/Shotgun/Quad/Fire",CHAN_WEAPON)
				Goto FlashEnder
			
	FlashEnder:
		TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
		TNT1 A 0 A_SetBlend("Orange",0.10,10)
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"FlashEnderNoRecoil")		
		TNT1 A 0 A_SetPitch(pitch-3.5)
		TNT1 AA 1 A_FireCustomMissile("SGSmokeSpawner",0, false,4,4)
		TNT1 A 2
		TNT1 AAAAAAA 1 A_SetPitch(pitch+0.5)
		Stop
	FlashEnderNoRecoil:
		TNT1 AA 1 A_FireCustomMissile("SGSmokeSpawner",0, false,4,4)
		Stop
		
	//-----------------------
	// Fire Seq / Zoom
	//-----------------------
	AltFire:
		QCG3 A 0 A_JumpIfInventory("DMToken",1,"StartZoomChecks")	//in Deathmatch, allow zoom checks by Default
		"####" A 0 A_JumpIfInventory("SG_UG_Explosive", 1, "StartZoomChecks")	//also if upgrade present, allow checks
	NoZoom:
		"####" A 2 A_WeaponReady(WRF_NOSECONDARY)	//dummy state in case player is holding "alt fire" all along
		"####" A 0 A_Refire("NoZoom")
		Goto Ready
	StartZoomChecks:
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken", 1, "ZoomOut")
	ZoomIn:
		"####" AAA 1
		"####" A 0 A_JumpIfInventory("DMToken",1,"ZoomDM")
		"####" "#" 0 A_PlaySound("QC/Weapons/Upgrade/SGBeep",CHAN_WEAPON)
		"####" "#" 0 ACS_NamedExecuteAlways("QCDE_ModSelectedText",0,3)
		"####" "#" 0 A_ZoomFactor(1.1)
		Goto ContinueZoom
	ZoomDM:
		"####" "#" 0 A_ZoomFactor(1.8)
	ContinueZoom:
		"####" "#" 0 A_GiveInventory("WeaponZoomedToken",1)
	ZoomInHeld:	
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 1 
		"####" "#" 1 A_Refire("ZoomInHeld")
		Goto Ready
		
		ZoomOut:
			"####" "#" 0 A_ZoomFactor(1.0) 
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		ZoomOutHeld:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 1 
			"####" "#" 1 A_Refire("ZoomOutHeld")
			Goto Ready
	}
}

	
	
Actor SGSmokeSpawner
{
	Radius 1
	Height 1
	Speed 14
	PROJECTILE
	+NOTIMEFREEZE
	+NOCLIP
	+CLIENTSIDEONLY +NONETID
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 0 A_Die
		Goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("SGSmoke",0,random(1,-1),0,0,0,frandom(0.1,0.25),0,32)
		TNT1 A 2
		Stop
	}
}
Actor SGSmoke
{
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY +NONETID
	Alpha 0.9
	Scale 0.04
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Jump(256,"Type1","Type2")	//to define which graphic will use
	Type1:
		X201 E 0	
		Goto "Continue"
	Type2:
		X201 F 0	
		Goto "Continue"
	Continue:
		"####" "#" 0 A_Jump(160,2,4,6)
		"####" "#" 0
		"####" "#" 0 A_SetScale(0.04)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.045)
		"####" "#" 2 A_FadeOut(0.035)
		"####" "#" 0 A_SetScale(0.05)
		"####" "#" 2 A_FadeOut(0.035)
	Looper:
		"####" "#" 0 A_SetScale(ScaleX+0.005)
		"####" "#" 2 A_FadeOut(0.045)
		Loop
	}
}




//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Weapon tokens
//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
Actor SG_UG_Spread 		: Token {}
Actor SG_UG_Explosive	: Token {}


Actor SGExplosiveBullet : FastProjectile
{
	Projectile
	+RANDOMIZE
	+FORCEXYBILLBOARD
	+DONTSPLASH
	+NOEXTREMEDEATH
	+DONTBLAST
	+BLOODSPLATTER
	+SEEKERMISSILE
	+MTHRUSPECIES
	damage (random(12,18))
	radius 2
	height 2
	speed 90
	renderstyle ADD
	alpha 0.9
	scale 0.1
	Translation "0:255=%[0,0,0]:[1.4,0.7,0]"
	DamageType "NoDamageToPlayers"
	ProjectileKickBack 0
	states
	{
	Spawn:
		X029 A 1 BRIGHT
		X029 A 70 BRIGHT A_PlaySound("QCDE/Other/BulletWhoosh",5,1.0,false,3.0)
		Wait
	XDeath:
		TNT1 A 1 A_GiveToTarget("HitBeepLow",1)
		Stop
		
	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,180)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont		
		
	DeathCont:	
		TNT1 A 0 A_SetScale(0.35)
		TNT1 A 0 A_Explode(30,112,0,1,0)
		TNT1 A 0 A_Jump(256,"Type1","Type2")
	Type1:
		X006 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	Type2:
		TNT1 A 0 A_SetScale(-0.35)
		X006 ABCDEFGHIJKLMNOP 1 Bright
		Stop
	}
}




// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCShotgunDM : QCShotgun
{	
	+WEAPON.NOAUTOAIM	
	+CHEATNOTWEAPON
	Tag "$TAG_QCShotgun" // "Shotgun (DM)"
	Weapon.AmmoGive 10
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 1
}
