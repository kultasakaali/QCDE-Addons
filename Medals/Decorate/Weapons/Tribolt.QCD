// --------------------------------------------------------------------------
//
// Tribolt
// Sprite bases by Chronoteeth
// Sprite edit by Captain J
// Muzzle flashes & animation by DBT
// Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor TriboltPickup : CustomInventory 15604
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "Tribolt"
	Height 48
	Radius 20
	Scale 0.6
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCTribolt" // "Picked up a Tribolt."
	Tag "$TAG_QCTribolt" // "Tribolt"
	Inventory.RespawnTics 35
	States
	{
	Spawn:
		QCGE Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		QCGE Z -1 Bright ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",14,args[4])
		Stop
	Idle:
		QCGE Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",13,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCTribolt")
		TNT1 A 0 
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 A_GiveInventory("QCTriboltDM")
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("QCTribolt")
		TNT1 A 0
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCTribolt",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCTribolt", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_TakeInventory("QCTribolt")
		TNT1 A 0 A_JumpIfInventory("QCTriboltDM",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCTriboltDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto HideDoomish+1
	}
}


Actor QCTribolt : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 1.35
	Weapon.Kickback 0
	Weapon.SelectionOrder 1950
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "QCBoltAmmo"
	Inventory.PickupMessage "$GOT_QCTribolt" // "Picked up a Tribolt."
	Obituary "$OB_QCTribolt" // "%o was bolted by %k."
	Tag "$TAG_QCTribolt" // "Tribolt"
	Scale 0.6
	States
	{
	Spawn:
		QCGE Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCGE ZZZZZZZZZZZZZZZ 35 Bright
		Stop
	Idle:
		QCGE Y -1
		Wait
		
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		QCGE A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCGE A 3 A_WeaponReady
		Loop
		
		ReadyCantFire:
			QCGE A 0 A_StopSound(5)
			QCGE A 0 A_StopSound(6)
			QCGE A 0 A_StopSound(CHAN_WEAPON)
			"####" "#" 0 A_ZoomFactor(1.0)
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			QCGE A 4 A_WeaponReady(WRF_NOFIRE)
			QCGE A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		QCGE A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		QCGE A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" "#" 0 A_StopSound(5) //stops any weapon looping sound
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		QCGE A 1 A_Raise
		QCGE "###" 0 A_Raise
		Loop	
	
	Deselect:
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:
		QCGE A 1 A_Lower
		"####" "###" 0 A_Lower
		Loop
		
	//-----------------------
	// Fire Seq
	//-----------------------
	Fire: //38 tics, 1.1sec
		TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"QuadSound")
		Goto NormalSound
			
		NormalSound:
			TNT1 A 0 A_PlaySound("QC/Weapons/Tribolt/Fire", CHAN_WEAPON, 1.0, 0)
			Goto ContinueFiringSeq
		QuadSound:
			TNT1 A 0 A_PlaySound("QC/Weapons/Tribolt/Quad/Fire", CHAN_WEAPON, 1.0, 0)
			Goto ContinueFiringSeq
		
	ContinueFiringSeq:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_FireCustomMissile("AmmoSpender", 0, true, 5, 2) //it fires 3 projectiles, but only uses 1 ammo
		
		QCGE A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		TNT1 A 0 A_GunFlash("PrimaryFlash")
		QCGE B 1 Offset(3,35) Bright 
		QCGE C 1 Offset(5,37) Bright
		QCGE D 1 Offset(7,39)
		QCGE D 1 Offset(8,40)
		QCGE D 1
		
		QCGE A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		TNT1 A 0 A_GunFlash("PrimaryFlash")
		QCGE E 1 Offset(11,43) Bright 
		QCGE F 1 Offset(13,45) Bright
		QCGE G 1 Offset(15,47)
		QCGE H 1 Offset(16,48)
		QCGE H 1
		
		QCGE A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		TNT1 A 0 A_GunFlash("PrimaryFlash")
		QCGE I 1 Offset(19,51) Bright 
		QCGE J 1 Offset(21,53) Bright
		QCGE K 1 Offset(23,55)
		QCGE K 1 Offset(24,56)
		QCGE L 1 //15
		
		QCGE L 1	
		
		QCGE M 9	//25
		
		QCGE N 1 Offset(23,55)
		QCGE N 1 Offset(21,53)
		QCGE N 1 Offset(19,51)
		QCGE O 1 Offset(16,48)
		QCGE O 1 Offset(13,45)
		QCGE O 1 Offset(10,42)
		QCGE P 1 Offset(8,40)
		QCGE P 1 Offset(6,38)
		QCGE P 1 Offset(4,36)
		QCGE Q 2 Offset(3,35)
		QCGE Q 2 Offset(2,34)
		QCGE R 2 Offset(1,33) 
		QCGE S 4 Offset(1,32)
		QCGE A 2 Offset(1,32) 	//46 tics, 1.314s instead of 1.3s as in QC. Close enough
		Goto "Ready"
		
	PrimaryFlash: //Attacks doer
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"PerformDM")
		Goto PerformSP
		
		PerformSP:
			TNT1 A 0 A_JumpIfInventory("TB_UG_Grenade",1,"GrenadeCheckIfZoomed")
			TNT1 A 0 A_JumpIfInventory("TB_UG_Long",1,"FireLongBolt")
			TNT1 A 0 A_FireCustomMissile("TriboltBolt", 0, false, 1, 5)
			Goto EndFlash
			
			FireLongBolt:
				TNT1 A 0 A_FireCustomMissile("TriboltBoltLong", 0, false, 1, 5)
				Goto EndFlash
				
			GrenadeCheckIfZoomed:
				TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"FireGrenades")
				Goto PerformSP+1
			FireGrenades:
				TNT1 A 0 A_FireCustomMissile("TriboltBoltGrenade", 0, false, 1, 5)
				Goto EndFlash
				
			
		PerformDM:
			TNT1 A 0 A_FireCustomMissile("TriboltBoltDM", 0, false, 1, 5)
			Goto EndFlash
			
	EndFlash:
		TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
		TNT1 A 0 A_SetBlend("Orange",0.055,10)
		TNT1 A 0 
		TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"EndFlashNoRecoil")
		TNT1 A 2 A_SetPitch(pitch-1.2)
		TNT1 AAA 1 A_SetPitch(pitch+0.4)
		Stop
	EndFlashNoRecoil:
		TNT1 A 1
		Stop
		
	//-----------------------
	// Fire Seq / Zoom
	//-----------------------
	AltFire:
		QCGE A 0 A_JumpIfInventory("DMToken",1,"StartZoomChecks")	//in Deathmatch, allow zoom checks by Default
		"####" A 0 A_JumpIfInventory("TB_UG_Grenade", 1, "StartZoomChecks")	//also if upgrade present, allow checks
	NoZoom:
		"####" A 2 A_WeaponReady(WRF_NOSECONDARY)	//dummy state in case player is holding "alt fire" all along
		"####" A 0 A_Refire("NoZoom")
		Goto Ready
	StartZoomChecks:
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken", 1, "ZoomOut")
	ZoomIn:
		"####" AAA 1
		"####" A 0 A_JumpIfInventory("DMToken",1,"ZoomDM")
		"####" "#" 0 A_PlaySound("QC/Weapons/Upgrade/TBBeep",CHAN_WEAPON)
		"####" "#" 0 ACS_NamedExecuteAlways("QCDE_ModSelectedText",0,13)
		"####" "#" 0 A_ZoomFactor(1.1)
		Goto ContinueZoom
	ZoomDM:
		"####" "#" 0 A_ZoomFactor(1.8)
	ContinueZoom:
		"####" "#" 0 A_GiveInventory("WeaponZoomedToken",1)
	ZoomInHeld:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 1 
		"####" "#" 1 A_Refire("ZoomInHeld")
		Goto Ready
		
		ZoomOut:
			"####" "#" 0 A_ZoomFactor(1.0) 
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		ZoomOutHeld:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 1 
			"####" "#" 1 A_Refire("ZoomOutHeld")
			Goto Ready
	}
}

Actor AmmoSpender
{
	+NOINTERACTION
	RenderStyle None
	States
	{
	Spawn:
		TNT1 A 3
		Stop
	}
}

Actor TriboltBolt 
{
	Projectile
	+EXTREMEDEATH
	+SEEKERMISSILE //Not actually used for anything other than checking radius.
	//-NOGRAVITY
	+MTHRUSPECIES
	+DONTSPLASH
	+DONTBLAST
	Gravity 0.2
	Radius 4
	Height 8
	Speed 37
	Damage (random(3,7)*8)
	Scale 0.35
	Obituary "$OB_QCTribolt" // "%o was bolted by %k."
	SeeSound ""
	BounceType "Hexen"
	+USEBOUNCESTATE
	Reactiontime 21
	DamageType "RocketDamage"
	States
	{
	Spawn:
		TNT1 A 0 A_PlaySound("quake4/Weapons/RocketLauncher/FlyWhoosh",7,1,1,5)
		GRNL A 1 Light(YellowSmallPoint2) Bright 
		GRNL A 0 A_CountDown
		GRNL A 1 Light(YellowSmallPoint2) Bright 
		GRNL A 0 A_ChangeFlag("NOGRAVITY",0)
		GRNL A 0 A_CountDown
	Looper:
		GRNL A 0 A_SpawnItemEx("TriboltTrailFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		GRNL A 1 Light(YellowSmallPoint2) Bright 
		GRNL A 0 A_CountDown
		Loop

	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMid",1)
		//Medals start
		TNT1 A 0 A_GiveToTarget("TBDirectDamageToken",1) 
		//Medals end
		TNT1 A 0 A_Jump(256,"DeathCont")
		Goto DeathCont
	
	Bounce:	//for when it hits floor/planes, it doesnt get destroyed, but stays there for 0.8 sec
		GRNL A 0 A_PlaySound("QC/Weapons/Nailgun/Impact",CHAN_AUTO,1,0,1.2) 
		GRNL A 0 A_ChangeFlag("NOGRAVITY",1)
		GRNL A 0 A_Stop
	BounceLoop:
		GRNL B 1 Light(YellowSmallFlicker2) A_CountDown
		Loop
		
	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,180)
		TNT1 A 0 A_Jump(256,"DeathCont")
		Goto DeathCont
		
		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepLow",1)
			//Medals start
			TNT1 A 0 A_GiveToTarget("TBSplashDamageToken",1)
			//Medals end
			TNT1 A 0 A_Jump(256,"DeathCont")
			Goto DeathCont
		
	DeathCont:
		TNT1 A 0 A_PlaySound("QC/Weapons/Tribolt/Explo",CHAN_AUTO,1,0,0.8)
		TNT1 A 0 A_CustomMissile("BoltDecalMaker",0,0,  180)	//to attempt to leave a scorch mark
		TNT1 A 0 A_Explode(20,128,1|XF_QROCKETJUMP)	//self damage, to do some jumps
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
 		TNT1 A 0 A_Explode(50,128,XF_QROCKETJUMP,0,32)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_SpawnItemEx("BoltExplosion",-2,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 2 Light("YellowSmallFlicker1")
		TNT1 A 2 Light("YellowMediumFlicker3")
		TNT1 A 2 Light("YellowMediumFlicker2")
		TNT1 A 2 Light("YellowMediumFlicker3")
		TNT1 A 2 Light("YellowSmallFlicker1")
		TNT1 A 2 Light("YellowSmallFlicker2")
		TNT1 A 2 Light("YellowSmallFlicker3")
		Stop
	}
}


Actor BoltDecalMaker
{
	Projectile
	Height 2
	Radius 2
	Speed 4
	+THRUACTORS
	Damage 0
	Decal "D4ExploScorchSmall"
	States
	{
	Spawn:
		TNT1 A 2
		Stop
	Death:
		TNT1 A 4
		Stop
	}
}

Actor TriboltBoltDM : TriboltBolt
{
	Damage 0
	-MTHRUSPECIES
	-DONTBLAST
	States
	{
	DeathCont:
		TNT1 A 0 A_PlaySound("QC/Weapons/Tribolt/Explo",CHAN_AUTO,1,0,0.8)
		TNT1 A 0 A_Explode(20,128,1|XF_QROCKETJUMP,0,18)	//self damage, to do some jumps
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
 		TNT1 A 0 A_Explode(25,128,XF_QROCKETJUMP)	//Then the rest to do 45dmg
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_SpawnItemEx("BoltExplosion",-2,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 2 Light("YellowSmallFlicker1")
		TNT1 A 2 Light("YellowMediumFlicker3")
		TNT1 A 2 Light("YellowMediumFlicker2")
		TNT1 A 2 Light("YellowMediumFlicker3")
		TNT1 A 2 Light("YellowSmallFlicker1")
		TNT1 A 2 Light("YellowSmallFlicker2")
		TNT1 A 2 Light("YellowSmallFlicker3")
		Stop
	}
}


Actor TriboltTrailFire 
{
	+CLIENTSIDEONLY +NONETID
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+DONTBLAST
	RenderStyle Add
	Alpha 0.7
	Scale 0.03
	States
	{
	Spawn:
		TNT1 A 2
		TNT1 A 0 A_Jump(256,"One","Two")
	One:
		X007 CE 1 Bright 
		X007 GI 1 Bright 
		Stop
	Two:
		X007 MO 1 Bright 
		X007 QS 1 Bright 
		Stop		
	}
}


Actor BoltSmokeTrail
{
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY +NONETID
	+DONTBLAST
	Scale 0.05
	RenderStyle Add
	Alpha 0.8
	States
	{
	Spawn:
		X102 M 1 A_FadeOut(0.035)
		Loop
	}
}

Actor BoltExplosion
{
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY +NONETID
	+DONTBLAST
	Scale 0.1
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(255,"MainT1","MainT2")
		
	MainT1:
		X112 A 1 Bright A_SetScale(scalex+0.07)
		X112 A 0 A_FadeOut(0.05)
		X112 A 1 Bright A_SetScale(scalex+0.07)
		X112 A 0 A_FadeOut(0.05)
		X112 B 1 Bright A_SetScale(scalex+0.07)
		X112 A 0 A_FadeOut(0.05)
		X112 B 1 Bright A_SetScale(scalex+0.07)
		X112 A 0 A_FadeOut(0.05)
		X112 C 1 Bright A_SetScale(scalex+0.07)
		X112 A 0 A_FadeOut(0.05)
		X112 C 1 Bright A_SetScale(scalex+0.07)
		X112 A 0 A_FadeOut(0.05)
		X112 D 1 Bright A_SetScale(scalex+0.07)
		X112 A 0 A_FadeOut(0.05)
		X112 D 1 Bright A_SetScale(scalex+0.07)
		X112 A 0 A_FadeOut(0.05)
		X112 E 2 Bright A_SetScale(scalex+0.03)
		X112 A 0 A_FadeOut(0.05)
		X112 F 2 Bright A_SetScale(scalex+0.03)
		X112 A 0 A_FadeOut(0.05)
		X112 G 2 Bright A_SetScale(scalex+0.03)
		X112 A 0 A_FadeOut(0.05)
		X112 H 2 Bright A_SetScale(scalex+0.03)
		X112 A 0 A_FadeOut(0.05)
	LooperT1:
		X112 H 1 Bright A_SetScale(scalex+0.03)
		X112 H 0 A_FadeOut(0.03)		
		Loop
		
	MainT2:
		X112 I 1 Bright A_SetScale(scalex+0.07)
		X112 I 0 A_FadeOut(0.05)
		X112 I 1 Bright A_SetScale(scalex+0.07)
		X112 I 0 A_FadeOut(0.05)
		X112 J 1 Bright A_SetScale(scalex+0.07)
		X112 J 0 A_FadeOut(0.05)
		X112 J 1 Bright A_SetScale(scalex+0.07)
		X112 J 0 A_FadeOut(0.05)
		X112 K 1 Bright A_SetScale(scalex+0.07)
		X112 K 0 A_FadeOut(0.05)
		X112 K 1 Bright A_SetScale(scalex+0.07)
		X112 K 0 A_FadeOut(0.05)
		X112 L 1 Bright A_SetScale(scalex+0.07)
		X112 L 0 A_FadeOut(0.05)
		X112 L 1 Bright A_SetScale(scalex+0.07)
		X112 L 0 A_FadeOut(0.05)
		X112 M 2 Bright A_SetScale(scalex+0.03)
		X112 M 0 A_FadeOut(0.05)
		X112 N 2 Bright A_SetScale(scalex+0.03)
		X112 N 0 A_FadeOut(0.05)
		X112 O 2 Bright A_SetScale(scalex+0.03)
		X112 O 0 A_FadeOut(0.05)
		X112 P 2 Bright A_SetScale(scalex+0.03)
		X112 P 0 A_FadeOut(0.05)
	LooperT2:
		X112 P 1 Bright A_SetScale(scalex+0.03)
		X112 P 0  A_FadeOut(0.03)
		Loop
	}
}

// ==========================================================================
// ==========================================================================

//	Upgrades

// ==========================================================================
// ==========================================================================

Actor TB_UG_Long    : Token {}
Actor TB_UG_Grenade : Token {}


//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Upgrade related actors

Actor TriboltBoltLong : TriboltBolt
{
	Damage (random(3,7)*10)
	States
	{
	Spawn:
		TNT1 A 0 A_PlaySound("quake4/Weapons/RocketLauncher/FlyWhoosh",7,1,1,5)
		GRNL A 1 Light(YellowSmallPoint2) Bright 
		GRNL A 1 Light(YellowSmallPoint2) Bright A_SpawnItemEx("BoltSmokeTrail",0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION)
		GRNL A 0 A_ChangeFlag("NOGRAVITY",0)
	Looper:
		GRNL A 0 A_SpawnItemEx("TriboltTrailFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		GRNL A 1 Light(YellowSmallPoint2) Bright A_SpawnItemEx("BoltSmokeTrail",0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION)
		Loop
	}
}


Actor TriboltBoltGrenade
{
    Radius 4
    Height 6
    Speed 27
    Scale 0.45
    Gravity 0.5
	Damage (random(3,7)*10)
    PROJECTILE
    -NOGRAVITY
	+EXTREMEDEATH
    +CANBOUNCEWATER 
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	+SEEKERMISSILE
	+MTHRUSPECIES
	+DONTBLAST
	BounceType "Doom"
	BounceFactor 0.6
	WallBounceFactor 0.6
	ReactionTime 23 //~4 seconds
    BounceSound "QC/Weapons/Tribolt/Bounce"
    DeathSound "QC/Weapons/Tribolt/Explo"
	DamageType "RocketDamage"
	Obituary "$OB_QCTribolt" 
	+NOGRAVITY
    States
    {
    Spawn:
		GRNL B 3 BRIGHT NODELAY A_SpawnItemEx("PlayerGrenSmokeParticle",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		GRNL N 0 BRIGHT A_ChangeFlag("NOGRAVITY",0)
	Idle:
		GRNL BB 3 Bright Light("RedSmallPoint1") A_SpawnItemEx("PlayerGrenSmokeParticle",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)		
		TNT1 A 0 A_CountDown
        Loop
		
	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepBig",1)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
		
	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,180)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont
		
	DeathCont:
		TNT1 A 0 A_Explode(20,128,1|XF_QROCKETJUMP)	//self damage, to do some jumps
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
 		TNT1 A 0 A_Explode(50,128,XF_QROCKETJUMP)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
	FinishExplosion:
		TNT1 A 15 A_SpawnItemEx("BoltExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
    }
}




// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCTriboltDM : QCTribolt
{	
	+WEAPON.NOAUTOAIM	
	+CHEATNOTWEAPON
	Tag "$TAG_QCTribolt" // "Tribolt (DM)"
	Weapon.AmmoGive 10
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 1
}



