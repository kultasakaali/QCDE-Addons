// --------------------------------------------------------------------------
//
// Super Nailgun
// Weapon and Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor SuperNailgunPickup : CustomInventory 15603
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "Super Nailgun"
	Height 48
	Radius 20
	Scale 0.67
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCSuperNailgun" // "Picked up a Super Nailgun."
	Tag "$TAG_QCSuperNailgun" // "Super Nailgun"
	Inventory.RespawnTics 35
	States
	{
	Spawn:
		QCGD Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		QCGD Y 35 A_CheckFlag("DROPPED", "BecomeWeaponItself")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		QCGD Z -1 Bright ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",12,args[4])
		Stop
	
	BecomeWeaponItself:
		TNT1 A 0 A_SpawnItemEx("QCSuperNailgun")
		TNT1 A 0 A_JumpIf(true, "End") Goto Idle // <-- this is to fool client-side predictor
	End:TNT1 A 0
		Stop
		
	Idle:
		QCGD Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",11,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCSuperNailgun")
		TNT1 A 0 A_TakeInventory("QCNailgun")
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 A_GiveInventory("QCSuperNailgunDM")
		TNT1 A 0 A_TakeInventory("QCNailgunDM")
		TNT1 A 0 A_TakeInventory("QCSuperNailgun")
		TNT1 A 0 A_TakeInventory("QCNailgun")
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCSuperNailgun",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCSuperNailgun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_TakeInventory("QCSuperNailgun")
		TNT1 A 0 A_JumpIfInventory("QCSuperNailgunDM",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCSuperNailgunDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto HideDoomish+1
	}
}


Actor QCSuperNailgun : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 2.7
	Weapon.Kickback 40
	Weapon.SelectionOrder 1400
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType "QCNailAmmo"
	Obituary "$OB_QCSuperNailgun" // "%o was perforated by %k."
	Inventory.PickupMessage "$GOT_QCSuperNailgun" // "Picked up a Super Nailgun."
	Tag "$TAG_QCSuperNailgun" // "Super Nailgun"
	Scale 0.67
	States
	{
	Spawn:
		QCGD Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCGD ZZZZZZZZZZZZZZZ 35 Bright
		Stop
	Idle:
		QCGD Y -1
		Wait
		
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		QCGD A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		QCGC A 0 A_TakeInventory("QCNailgunDM")
		QCGC A 0 A_TakeInventory("QCNailgun")
		"####" "#" 0 A_StopSound(5) //stops any weapon looping sound
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		QCGD A 1 A_Raise
		"####" "###" 0 A_Raise
		Loop
		
	Deselect:
		QCGD A 0 A_StopSound(5) //stops the looping fire sound
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:
		QCGD A 1 A_Lower
		"####" "###" 0 A_Lower
		Loop
		
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCGD A 3 A_WeaponReady
		Loop
		
		ReadyCantFire:
			QCGD A 0 A_StopSound(5)
			QCGD A 0 A_StopSound(6)
			"####" "#" 0 A_ZoomFactor(1.0)
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			QCGD A 4 A_WeaponReady(WRF_NOFIRE)
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Fire Seq
	//-----------------------
	Fire:
		TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"SeekerFireCheck")
		
	ContinueNormalFire:	//if it didnt have seekers, continues normal
		TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"FireQuadSound")
		Goto FireNormalSound
		
		FireNormalSound:
			QCGD A 0 A_PlaySound("QC/Weapons/SuperNailgun/FireStartLoop",5,1,1)
			Goto FirePostSoundCheck
		FireQuadSound:
			QCGD A 0 A_PlaySound("QC/Weapons/SuperNailgun/Quad/FireStartLoop",5,1,1)
			Goto FirePostSoundCheck
			
	FirePostSoundCheck:	
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"DM_FireLooper")
		Goto FireLooper
		
		FireLooper:
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCGD B 0 A_GunFlash("SP_Flash")
			QCGD J 1 Bright 	Offset(1,33)
			QCGD M 1 		 	Offset(2,34)
			QCGD N 1 			Offset(1,33)
			QCGD A 0 A_ReFire("FireLooper2")
			Goto EndFireLooper
		FireLooper2:
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCGD B 0 A_GunFlash("SP_Flash")
			QCGD K 1 Bright 	Offset(1,33)
			QCGD M 1 		 	Offset(2,34)
			QCGD N 1 			Offset(1,33)
			QCGD A 0 A_ReFire("FireLooper3")
			Goto EndFireLooper
		FireLooper3:
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCGD B 0 A_GunFlash("SP_Flash")
			QCGD L 1 Bright 	Offset(1,33)
			QCGD M 1 		 	Offset(2,34)
			QCGD N 1 			Offset(1,33)
			QCGD A 0 A_ReFire("FireLooper")
			Goto EndFireLooper
			
		DM_FireLooper:
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCGD B 0 A_GunFlash("DM_Flash")
			QCGD J 1 Bright 	Offset(1,33)
			QCGD M 1 		 	Offset(2,34)
			QCGD N 1 			Offset(1,33)
			QCGD A 0 A_ReFire("DM_FireLooper2")
			Goto EndFireLooper
		DM_FireLooper2:
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCGD B 0 A_GunFlash("DM_Flash")
			QCGD K 1 Bright 	Offset(1,33)
			QCGD M 1 		 	Offset(2,34)
			QCGD N 1 			Offset(1,33)
			QCGD A 0 A_ReFire("DM_FireLooper3")
			Goto EndFireLooper
		DM_FireLooper3:
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCGD B 0 A_GunFlash("DM_Flash")
			QCGD L 1 Bright 	Offset(1,33)
			QCGD M 1 		 	Offset(2,34)
			QCGD N 1 			Offset(1,33)
			QCGD A 0 A_ReFire("DM_FireLooper")
			Goto EndFireLooper
			
	EndFireLooper:
		QCG2 A 0 A_PlaySound("QC/Weapons/SuperNailgun/FireEnd",5,1,0)
		QCGD ABCCDDEEEFFFAAAABBBBCCCC 1 A_WeaponReady(WRF_NOBOB)
		Goto Ready
		
		
	SeekerFireCheck:	
			TNT1 A 0 A_JumpIfInventory("SNG_UG_Seeker",1,"FireSeekerLooper")
			Goto ContinueNormalFire	//goes to normal firemode
	ContinueNormalFire:	//if it didnt have seekers, continues normal
		TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"FireQuadSound")
		Goto FireNormalSound
			
		FireSeekerLooper:
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCGD B 0 A_AlertMonsters
			QCGD B 0 A_GunFlash("SP_FlashSeeker")
			QCGD J 2 Bright 	Offset(1,33)
			QCGD M 1 		 	Offset(2,34)
			QCGD N 1 			Offset(1,33)
			QCGD A 1  
			QCGD A 0 A_ReFire("FireSeekerLooper2")
			Goto EndFireLooper
		FireSeekerLooper2:
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCGD B 0 A_GunFlash("SP_FlashSeeker")
			QCGD K 2 Bright 	Offset(1,33)
			QCGD M 1 		 	Offset(2,34)
			QCGD N 1 			Offset(1,33)
			QCGD A 1 		 	
			QCGD A 0 A_ReFire("FireSeekerLooper3")
			Goto EndFireLooper
		FireSeekerLooper3:
			QCGD A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			QCGD B 0 A_GunFlash("SP_FlashSeeker")
			QCGD L 2 Bright 	Offset(1,33)
			QCGD M 1 		 	Offset(2,34)
			QCGD N 1 			Offset(1,33)
			QCGD A 1 		 	
			QCGD A 0 A_ReFire("FireSeekerLooper")
			Goto EndFireLooper
		
		
		
	//-----------------------
	// Attack doers
	//-----------------------
	
	//Single player
	SP_Flash: 
		TNT1 A 0 A_JumpIfInventory("SNG_UG_Lava",1,"ShootLavaNail")
		TNT1 A 0 A_FireCustomMissile("QCNailStrong",0,true,2,5)
		Goto EndFlash
		
		ShootLavaNail:
			TNT1 A 0 A_FireCustomMissile("QCNailStrongLava",0,true,2,5)
			Goto EndFlash
		
		SP_FlashSeeker: 	
			TNT1 A 0 A_JumpIfInventory("SNG_UG_Lava",1,"ShootSeekerLava")
			
			ShootNormal:
				TNT1 A 0 A_FireCustomMissile("QCNailStrongSeeker",0,true,2,5)
				Goto EndSeekerFlash
			ShootSeekerLava:
				TNT1 A 0 A_FireCustomMissile("QCNailStrongLavaSeeker",0,true,2,5)
				Goto EndSeekerFlash
			
		EndSeekerFlash:
			TNT1 A 0 A_JumpIfInventory("PowerQuad",1,2)
			TNT1 A 0 A_PlaySound("QC/Weapons/SuperNailgun/FireSeeker",CHAN_WEAPON)
			Goto EndFlash
			TNT1 A 0 A_PlaySound("QC/Weapons/SuperNailgun/Quad/FireSeeker",CHAN_WEAPON)
			Goto EndFlash
			
		
	//Deathmatch	
	DM_Flash:	
		TNT1 A 0 A_FireCustomMissile("QCNailStrongDM",0,true,2,5)
		Goto EndFlash
		
		EndFlash:
			TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
			TNT1 A 0 A_SetBlend("Yellow",0.03,3)
			TNT1 A 0 A_FireCustomMissile("MachinegunSmokeSpawner",0, false,5,3)
			TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"EndFlashNoRecoil")
			TNT1 A 1 A_SetPitch(Pitch-0.6)
			TNT1 AA 1 A_SetPitch(Pitch+0.3)
			Stop
		EndFlashNoRecoil:
			TNT1 A 1
			Stop
			
			
	//-----------------------
	// Fire Seq / Zoom
	//-----------------------
	AltFire:
		QCGD A 0 A_JumpIfInventory("DMToken",1,"StartZoomChecks")	//in Deathmatch, allow zoom checks by Default
		"####" A 0 A_JumpIfInventory("SNG_UG_Seeker", 1, "StartZoomChecks")	//also if upgrade present, allow checks
	NoZoom:
		"####" A 2 A_WeaponReady(WRF_NOSECONDARY)	//dummy state in case player is holding "alt fire" all along
		"####" A 0 A_Refire("NoZoom")
		Goto Ready
	StartZoomChecks:
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken", 1, "ZoomOut")
	ZoomIn:
		"####" AAA 1
		"####" A 0 A_JumpIfInventory("DMToken",1,"ZoomDM")
		"####" "#" 0 A_PlaySound("QC/Weapons/Upgrade/SNGBeep",CHAN_WEAPON)
		"####" "#" 0 ACS_NamedExecuteAlways("QCDE_ModSelectedText",0,11)
		"####" "#" 0 A_ZoomFactor(1.1)
		Goto ContinueZoom
	ZoomDM:
		"####" "#" 0 A_ZoomFactor(1.8)
	ContinueZoom:
		"####" "#" 0 A_GiveInventory("WeaponZoomedToken",1)
	ZoomInHeld:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 1 
		"####" "#" 1 A_Refire("ZoomInHeld")
		Goto Ready
		
		ZoomOut:
			"####" "#" 0 A_ZoomFactor(1.0) 
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		ZoomOutHeld:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 1 
			"####" "#" 1 A_Refire("ZoomOutHeld")
			Goto Ready
	}
}

Actor QCNailStrong : FastProjectile
{
	Projectile
	+FORCEXYBILLBOARD
	+BLOODSPLATTER
	+MTHRUSPECIES
	+DONTBLAST
	Speed 52
	Radius 3
	Height 6
	Damage (random(8,12)*2)
	Scale 0.42
	DamageType "ZombieGibber"
	Obituary "$OB_QCSuperNailgun" // "%o was perforated by %k."
	Decal "NailgunChip"
	States
	{
	Spawn:
		TNT1 A 1
		NAIL A 1 Bright
		Wait
		
		
	XDeath:
		TNT1 A 10 A_GiveToTarget("HitBeepLow",1)
		//Medals start
		TNT1 A 10 A_GiveToTarget("SNGDamageToken",1)
		//Medals end
		Stop
		
	Death:
		DB09 A 0 A_PlaySound("QC/Weapons/Nailgun/Impact",CHAN_AUTO,0.7,0,1.5) 
		DB09 A 0 A_Explode(7,32,XF_HURTSOURCE,0,32)
		DB09 A 0 A_SetScale(0.55)
		DB09 A 0 A_Jump(256,"T1","T2","T3","T4")
		
		T1:		DB09 A 0 A_Jump(255,"EndDeath")
			Goto EndDeath
		T2:		DB10 A 0 A_Jump(255,"EndDeath")
			Goto EndDeath
		T3:		DB11 A 0 A_Jump(255,"EndDeath")
			Goto EndDeath
		T4:		DB12 A 0 A_Jump(255,"EndDeath")
			Goto EndDeath
		
	EndDeath:
		"####" ABCDEF 1 Light("YellowSmallFlicker3") Bright
		"####" GH 1 Bright
		"####" IJKLMNOPQRST 2 Bright
		Stop
	}
}


Actor QCNailStrongDM : QCNailStrong
{
	Damage (20)
	-MTHRUSPECIES
	-DONTBLAST
	Obituary "$OB_QCSuperNailgun" // "%o was perforated by %k."
}



// ==========================================================================
// ==========================================================================

//	Upgrades

// ==========================================================================
// ==========================================================================

Actor SNG_UG_Lava   : Token {}
Actor SNG_UG_Seeker : Token {}

//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Upgrade related actors

// Lava nail
Actor QCNailStrongLava : QCNailStrong
{
	//same damage as regular nail, but with chance to spawn ember
	Scale 0.6
	States
	{
	Spawn:
		TNT1 A 1
		NAIL B 1 Bright A_SpawnItemEx("LavaNailTrail",0,0,0, 0,0,frandom(-0.5,-1.5),0,SXF_NOCHECKPOSITION,230)
		Wait
	XDeath:
		TNT1 A 0 A_GiveToTarget("HitBeepLow",1)
		Goto EndDeath
	EndDeath:
		"####" A 0 A_Jump(170,2) //chance to NOT spawn the flame, 66%
		"####" A 0 A_SpawnItemEx("LavaNailFlameBurner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		"####" ABCDEF 1 Light("YellowSmallFlicker3") Bright
		"####" GH 1 Bright
		"####" IJKLMNOPQRST 2 Bright
		Stop
	}
}


Actor LavaNailFlameBurner
{
	Projectile
	+THRUACTORS
	+NOINTERACTION
	-NOGRAVITY
	Gravity 0.1
	+NODAMAGETHRUST
	+BLOODLESSIMPACT
	+SEEKERMISSILE
	+DONTSPLASH
	+DONTBLAST
	Radius 1
	Height 1
	Damage 0
	Speed 0
	Scale 0.38
	var int user_counter;
	Translation "0:255=%[0,0,0]:[1.8,0.8,0]"
	States
	{
	Spawn:
		VTFB A 0 NODELAY A_Jump(180,2) // ~30% chance it will not jump, and instead will continue causing pain to monsters
		VTFB A 0 A_ChangeFlag("Painless",1)
		
		Looper:
			VTFB A 0 A_JumpIf(user_counter>=14,"Ender")	//140 tics, 4 seconds
			VTFB A 0 A_SpawnItemEx("NailFlameFX", random(4,-4),random(4,-4),0, 0,0,frandom(0.05,0.4), 0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
			VTFB A 0 A_SeekerMissile(0,0,SMF_LOOK/*|SMF_CURSPEED*/,256)
			VTFB A 0 A_JumpIfTargetInLOS("Sounder",360,JLOSF_PROJECTILE,50)
			VTFB A 0 A_Jump(255,"ContinueLooper")
		ContinueLooper:
			VTFB A 0 A_CheckFloor("NoWarp")
			VTFB A 0 A_Explode(15,32,0,0,32)
			VTFB AAAAAAAAAA 1 Light("OrangeSmallFlicker1") A_Warp(AAPTR_DEFAULT,0,0,-1)
			VTFB A 0 A_SetUserVar(user_counter, user_counter+1) 
			Goto Looper
		NoWarp:
			VTFB A 0 A_Explode(8,32,0,0,32)
			VTFB A 10 Light("OrangeSmallFlicker1") 
			VTFB A 0 A_SetUserVar(user_counter, user_counter+1) 
			Goto Looper
		
	Sounder:
		VTFB A 0 A_GiveToTarget("HitBeepLow",1)
		VTFB A 0 A_Jump(255,"ContinueLooper")
		Goto ContinueLooper
		
	Ender:
		VTFB AAAAAA 2 Light("OrangeSmallFlicker1") A_FadeOut(0.1)
		VTFB A 2 Light("OrangeSmallFlicker1") A_FadeOut(0.1)
		VTFB A 2 Light("OrangeSmallFlicker2") A_FadeOut(0.1)
		VTFB A 2 Light("OrangeSmallFlicker2") A_FadeOut(0.1)
		VTFB A 2 Light("OrangeSmallFlicker3") A_FadeOut(0.1)
		Stop
	}
}



// Seeker nail
Actor QCNailStrongSeeker : QCNailStrong
{
	+SEEKERMISSILE
	+SCREENSEEKER
	Speed 45
	Damage (random(8,12)*3)
	Scale 0.49
	States
	{
	Spawn:
		TNT1 A 1
	Looper:
		TNT1 A 0 A_SpawnItemEx("NailSeekerSmoke",0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		NAIL CC 1 Bright A_SeekerMissile(6,6,SMF_LOOK|SMF_PRECISE|SMF_CURSPEED)
		Loop
	}
}

// Lava + Seeker Nail
Actor QCNailStrongLavaSeeker : QCNailStrongLava
{
	+SEEKERMISSILE
	+SCREENSEEKER
	Speed 45
	Damage (random(8,12)*3)
	Scale 0.49
	States
	{
	Spawn:
		TNT1 A 1
	Looper:
		TNT1 A 0 A_SpawnItemEx("NailSeekerLavaNSmoke",0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		NAIL D 1 Bright A_SeekerMissile(6,6,SMF_LOOK|SMF_PRECISE|SMF_CURSPEED)
		TNT1 A 0 A_SpawnItemEx("LavaNailTrail",0,0,0, 0,0,frandom(-0.5,-1.5),0,SXF_NOCHECKPOSITION,230)
		NAIL D 1 Bright A_SeekerMissile(6,6,SMF_LOOK|SMF_PRECISE|SMF_CURSPEED)
		TNT1 A 0 A_SpawnItemEx("LavaNailTrail",0,0,0, 0,0,frandom(-0.5,-1.5),0,SXF_NOCHECKPOSITION,230)
		Loop
	}
}

// VFX
Actor LavaNailTrail 
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NOCLIP
	+NONETID
	+FORCEXYBILLBOARD
	+DONTBLAST
	Radius 1
	Height 1
	Scale 0.18
	RenderStyle Add
	Alpha 0.99
	States
	{
	Spawn:
		VTFB AABBAABB 2 Bright A_FadeOut(0.05)
		Loop
	}
}

Actor NailFlameFX
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	+DONTBLAST
	RenderStyle Add
	Scale 0.18
	Alpha 0
	States
	{
	Spawn:
		X123 TUVWX 2 NODELAY A_FadeIn(0.2)
	Looper:
		X123 ABCDEFGHIJKLMNOPQRSTUVWX 2 Bright A_FadeOut(0.05)
		Loop
	}
}




Actor NailSeekerSmoke 
{
	+CLIENTSIDEONLY
	+NOINTERACTION
	+NONETID
	+FORCEXYBILLBOARD
	+DONTBLAST
	Scale 0.08
	RenderStyle Add
	Alpha 0.99
	States
	{
	Spawn:
		X103 J 2 Bright A_FadeOut(0.05)
		Loop
	}
}

Actor NailSeekerLavaNSmoke : NailSeekerSmoke 
{
	Translation "0:255=%[0,0,0]:[1.3,0.4,0]"
}


// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCSuperNailgunDM : QCSuperNailgun
{	
	+WEAPON.NOAUTOAIM	
	+CHEATNOTWEAPON
	Tag "$TAG_QCSuperNailgun" // "Super Nailgun (DM)"
	Weapon.AmmoGive 100
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 10
}


