// --------------------------------------------------------------------------
//
// BFG10K
// Weapon and Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor BFGPickup : CustomInventory 15608
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "BFG 10k"
	Height 48
	Radius 20
	Scale 0.65
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCBFG" // "Picked up the BFG10k."
	Tag "$TAG_QCBFG" // "BFG 10k"
	Inventory.RespawnTics 2100	//60sec

	// Zandronum's client-server disagreement over whether the #dress is under
	// the balcony or on the ledge has really wasted quite a bit of my time.
	// Here's to 2018, and here's to like maybe 16 hours I spent in both D4T
	// and QCDE trying to get BFG to show its pickup where you can get it,
	// despite pickup being one actor.
	//
	// This flag is here to keep us from it until it's fixed in engine.
	// In some cases... :joy::gun:
	//
	// ~Michaelis
	+NOGRAVITY

	States
	{
	Spawn:
		QCG8 A 1
		QCG8 Y 0 ACS_NamedExecuteWithResult("QCDE_RemoveIfDuel")
		QCG8 Y 35 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCG8 Z 0 A_ChangeFlag("DROPPED", false)
		QCG8 Z 70 Bright ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",22,args[4])
		QCG8 Z 0 A_ChangeFlag("NOGRAVITY", false)
		QCG8 Z -1 Bright
		Stop
	Idle:
		QCG8 Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",21,args[4])
		Wait
	Pickup:
		QCG8 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		QCG8 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		QCG8 A 0 A_GiveInventory("QCBFG")
		QCG8 A 0
		Stop
	DMWeaponStay: // BFG doesn't stay even if it wants to in DM
	DMPickup:
		QCG8 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		QCG8 A 0 A_GiveInventory("QCBFGDM")
		QCG8 A 0
		QCG8 A 0 A_TakeInventory("QCBFG")
		QCG8 A 0
		Stop
	WeaponStay:
		QCG8 A 0 A_JumpIfInventory("QCBFG",1,"Stay")
		QCG8 A 0 A_SpawnItemEx("QCBFG", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		QCG8 A 0
		Fail
	}
}

Actor BFGPickupFloat : BFGPickup 15618
{
	//$Title "Float BFG 10k"
	+NOGRAVITY
}

Actor QCBFG : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 2.6
	Weapon.SelectionOrder 6666 // lowest
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "QCBFGAmmo"
	Obituary "$OB_QCBFG" // "%o was melted by %k's BFG."
	+WEAPON.WIMPY_WEAPON
	Inventory.PickupMessage "$GOT_QCBFG" // "Picked up the BFG 10k."
	Tag "$TAG_QCBFG" // "BFG 10k"
	Scale 0.65
	States
	{
	Spawn:
		QCG8 Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCG8 Z 0 ACS_NamedExecuteAlways("QCDE_RemoveIfDuel")
		QCG8 ZZZZZZZZZZZZZZZ 35 Bright
		Stop
	Idle:
		QCG8 Y -1
		Wait

	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		TNT1 A 0 A_JumpIfInventory("QCBFGSkin", 6, "Ready.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCBFGSkin", 4, "Ready.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCBFGSkin", 3, "Ready.Quake3")
		TNT1 A 0 A_JumpIfInventory("QCBFGSkin", 2, "Ready.Quake2")
	Ready.Default:
		QCG8 A 0 A_PlaySound("QC/Weapons/BFG/Idle_Default",	CHAN_WEAPON,0.7,1)
		Goto ReadyCont
	Ready.Quake2:
		Q2G8 A 0 A_PlaySound("QC/Weapons/BFG/Idle_Default",	CHAN_WEAPON,0.7,1)
		Goto ReadyCont
	Ready.Quake3:
		Q3G8 A 0 A_PlaySound("QC/Weapons/BFG/Idle_Default",	CHAN_WEAPON,0.7,1)
		Goto ReadyCont
	Ready.Quake4:
		Q4G8 A 0 A_PlaySound("QC/Weapons/BFG/Idle_Quake4",	CHAN_WEAPON,0.7,1)
		Goto ReadyCont
	Ready.QCDE1:
		Q0G8 A 0 A_PlaySound("QC/Weapons/BFG/Idle_Default",	CHAN_WEAPON,0.7,1)
		Goto ReadyCont

	ReadyCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" ABCDEF 3 A_WeaponReady(WRF_NOSECONDARY)
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" GH 3 A_WeaponReady(WRF_NOSECONDARY)
		"####" A 0 A_JumpIfInventory("QCBFGSkin", 6, 2) // Jump over skin specific jumps
		"####" A 0 A_JumpIfInventory("QCBFGSkin", 3, "ReadyCont") // Q3 and Q4 skins play animation differently
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" AAAA 3 A_WeaponReady(WRF_NOSECONDARY)
		Loop

	PostFireReady:
		"####" BCDEFG 3 A_WeaponReady(WRF_NOSECONDARY)
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" HAAAAA 3 A_WeaponReady(WRF_NOSECONDARY)
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		Goto Ready

		ReadyCantFire:
			"####" A 0 A_StopSound(CHAN_WEAPON)
			"####" A 0 A_StopSound(6)
			"####" A 0 A_ZoomFactor(1.0)
			"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
			"####" A 0 A_TakeInventory("BFG_Charges",32767)
		RCFLooper:
			"####" A 4 A_WeaponReady(WRF_NOFIRE)
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready

	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		TNT1 A 0 A_JumpIfInventory("QCBFGSkin", 6, "Select.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCBFGSkin", 4, "Select.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCBFGSkin", 3, "Select.Quake3")
		TNT1 A 0 A_JumpIfInventory("QCBFGSkin", 2, "Select.Quake2")
	Select.Default:
		QCG8 A 0
		Goto SelectCont
	Select.Quake2:
		Q2G8 A 0
		Goto SelectCont
	Select.Quake3:
		Q3G8 A 0
		Goto SelectCont
	Select.Quake4:
		Q4G8 A 0
		Goto SelectCont
	Select.QCDE1:
		Q0G8 A 0
		Goto SelectCont

	SelectCont:
		"####" A 0 A_PlaySound("QC/Weapons/BFG/Raise_Default",	CHAN_6,0.75,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		"####" A 1 A_Raise
		"####" A 0 A_Raise
		Loop

	Deselect:
		"####" A 0 A_StopSound(CHAN_WEAPON)
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:
		"####" A 1 A_Lower
		"####" A 0 A_Lower
		Loop

	//-----------------------
	// Fire Seq
	//-----------------------
	Fire:
		"####" A 0 A_AlertMonsters
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		Goto FIRAH

		CheckIfUpgrade:
			"####" A 0 A_JumpIfInventory("BFG_UG_Charge",1,"StartCharge")
			Goto FIRAH

	//-----------
	//Regular fire
	FIRAH:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 0 A_StopSound(6) //stops raise sound
		"####" I 1 Bright offset(6,47) A_GunFlash("PrimaryFlash")
		"####" IJ 1 Bright
		"####" K 1 offset(7,48)
		"####" K 1 offset(6,46)
		"####" L 1 offset(5,44)
		"####" L 1 offset(4,42)
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		"####" A 1 offset(3,40) A_Refire("FIRAH")
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		"####" A 1 offset(2,38) A_Refire("FIRAH")
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		"####" A 1 offset(1,36) A_Refire("FIRAH")
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		"####" A 1 offset(0,32) A_Refire("FIRAH")
		Goto PostFireReady


	PrimaryFlash: //Attacks doer
		"####" A 0 A_JumpIfInventory("DMToken",1,"DM_Flash")
		Goto SP_Flash

		SP_Flash:
			"####" A 0 A_FireCustomMissile("QCBFGShot",0, true,1,5)
			Goto FlashCheckSound

		DM_Flash:
			"####" A 0 A_FireCustomMissile("QCBFGShot_DM",0, true,1,5)
			Goto FlashCheckSound

		FlashCheckSound:
			"####" A 0 A_JumpIfInventory("PowerQuad",1,"QuadSound")
			Goto NormalSound

			NormalSound:
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 6, "NormalSound.Default")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 4, "NormalSound.Quake4")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 3, "NormalSound.Quake3")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 2, "NormalSound.Quake2")
			NormalSound.Default:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Fire_Default",	CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.Quake2:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Fire_Quake2",	CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.Quake3:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Fire_Quake3",	CHAN_WEAPON)
				Goto FlashEnder
			NormalSound.Quake4:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Fire_Quake4",	CHAN_WEAPON)
				Goto FlashEnder

			QuadSound:
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 6, "QuadSound.Default")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 4, "QuadSound.Quake4")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 3, "QuadSound.Quake3")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 2, "QuadSound.Quake2")
			QuadSound.Default:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire_Default",	CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.Quake2:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire_Quake2",	CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.Quake3:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire_Quake3",	CHAN_WEAPON)
				Goto FlashEnder
			QuadSound.Quake4:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire_Quake4",	CHAN_WEAPON)
				Goto FlashEnder

	FlashEnder:
		"####" A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
		"####" A 0 A_SetBlend("Green",0.14,20)
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken",1,"FlashEnderZoomed")
		"####" A 0 A_JumpIfInventory("WepNoRecoilToken",1,"FlashEnderNoRecoil")
		"####" A 0 A_SetPitch(pitch-2.5)
		"####" A 2
		"####" A 1 A_SetPitch(pitch+0.5)
		"####" A 1 A_SetPitch(pitch+0.5)
		"####" A 1 A_SetPitch(pitch+0.5)
		"####" A 1 A_SetPitch(pitch+0.5)
		"####" A 1 A_SetPitch(pitch+0.5)
		Stop

			FlashEnderNoRecoil:
				"####" A 1
				Stop

	FlashEnderZoomed:
		"####" A 0 A_JumpIfInventory("WepNoRecoilToken",1,"FlashEnderNoRecoil")
		"####" A 0 A_SetPitch(pitch-1.25)
		"####" A 2
		"####" A 1 A_SetPitch(pitch+0.25)
		"####" A 1 A_SetPitch(pitch+0.25)
		"####" A 1 A_SetPitch(pitch+0.25)
		"####" A 1 A_SetPitch(pitch+0.25)
		"####" A 1 A_SetPitch(pitch+0.25)
		Stop


	//-----------
	//Charge fire
	StartCharge:
		"####" A 0 A_JumpIfInventory("QCBFGSkin", 6, "StartCharge.Default")
		"####" A 0 A_JumpIfInventory("QCBFGSkin", 4, "StartCharge.Quake4")
	StartCharge.Default:
		"####" A 0 A_PlaySound("QC/Weapons/BFG/Charge_Default",	CHAN_WEAPON,1)
		Goto Part1
	StartCharge.Quake4:
		"####" A 0 A_PlaySound("QC/Weapons/BFG/Charge_Quake4",	CHAN_WEAPON,1)
		Goto Part1

	Part1:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" B 1 Bright Offset(1,34)
		"####" C 1 Bright Offset(2,33)
		"####" D 1 Bright Offset(1,33)
		"####" E 1 Bright Offset(1,34)
		"####" A 0 A_Refire("Part2")
		Goto Release
	Part2:
		"####" F 1 Bright Offset(2,33)
		"####" G 1 Bright Offset(1,33)
		"####" H 1 Bright Offset(1,34)
		"####" A 1 Bright Offset(2,34)
		"####" A 0 A_GiveInventory("BFG_Charges",1)
		"####" A 0 A_JumpIfInventory("BFG_Charges",15,"MaxCharge")
		"####" A 0 A_Refire("Part1")
		Goto Release
	MaxCharge:
		"####" A 0 A_PlaySound("QC/Weapons/BFG/ChargeLoop_Default",	CHAN_WEAPON,0.5,1)
		Goto MaxCharge1

	MaxCharge1:
		"####" M 1 Bright Offset(1,33)
		"####" M 1 Bright Offset(2,34)
		"####" A 0 A_Refire("MaxCharge2")
		Goto Release
	MaxCharge2:
		"####" M 1 Bright Offset(1,34)
		"####" M 1 Bright Offset(2,33)
		"####" A 0 A_Refire("MaxCharge1")
		Goto Release

	Release:
		"####" I 1 Bright offset(6,47) A_GunFlash("ChargedFlash")
		"####" IJ 1 Bright
		"####" K 1 offset(7,48)
		"####" K 1 offset(6,46)
		"####" L 1 offset(5,44)
		"####" L 1 offset(4,42)
		"####" A 1 offset(3,40)
		"####" A 1 offset(2,38)
		"####" A 1 offset(1,36)
		"####" A 1 offset(0,32)
		Goto PostFireReady


	ChargedFlash: //Attacks doer
		"####" A 0 A_JumpIfInventory("BFG_Charges",15,"FirePower6")
		"####" A 0 A_JumpIfInventory("BFG_Charges",12,"FirePower5")
		"####" A 0 A_JumpIfInventory("BFG_Charges",9,"FirePower4")
		"####" A 0 A_JumpIfInventory("BFG_Charges",6,"FirePower3")
		"####" A 0 A_JumpIfInventory("BFG_Charges",3,"FirePower2")
		Goto FirePower1

		FirePower1:
			"####" A 0 A_FireCustomMissile("QCBFGChargeShot",0, true,1,5)
			Goto GenericFlashChecks

		FirePower2:
			"####" A 0 A_FireCustomMissile("QCBFGChargeShot2",0, true,1,5)
			Goto FirePower2Cont

		FirePower2Cont:
			"####" A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			"####" A 0 A_TakeInventory("QCBFGAmmo",1,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks

		FirePower3:
			"####" A 0 A_FireCustomMissile("QCBFGChargeShot3",0, true,1,5)
			Goto FirePower3Cont

		FirePower3Cont:
			"####" A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			"####" A 0 A_TakeInventory("QCBFGAmmo",2,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks

		FirePower4:
			"####" A 0 A_FireCustomMissile("QCBFGChargeShot4",0, true,1,5)
			Goto FirePower4Cont

		FirePower4Cont:
			"####" A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			"####" A 0 A_TakeInventory("QCBFGAmmo",3,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks

		FirePower5:
			"####" A 0 A_FireCustomMissile("QCBFGChargeShot5",0, true,1,5)
			Goto FirePower5Cont

		FirePower5Cont:
			"####" A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			"####" A 0 A_TakeInventory("QCBFGAmmo",4,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks

		FirePower6:
			"####" A 0 A_FireCustomMissile("QCBFGChargeShot6",0, true,1,5)
			Goto FirePower6Cont

		FirePower6Cont:
			"####" A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			"####" A 0 A_TakeInventory("QCBFGAmmo",5,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks

		GenericFlashChecks:
			"####" A 0 A_JumpIfInventory("PowerQuad",1,"ChargeQuadSound")
			Goto ChargeNormalSound
			ChargeNormalSound:
				"####" A 0 A_JumpIfInventory("BFG_Charges",15,"PlayChargeMaxSound")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 6, "ChargeNormalSound.Default")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 4, "ChargeNormalSound.Quake4")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 3, "ChargeNormalSound.Quake3")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 2, "ChargeNormalSound.Quake2")
			ChargeNormalSound.Default:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Fire_Default",	CHAN_WEAPON)
				Goto ChargeFlashEnder
			ChargeNormalSound.Quake2:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Fire_Quake2",	CHAN_WEAPON)
				Goto ChargeFlashEnder
			ChargeNormalSound.Quake3:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Fire_Quake3",	CHAN_WEAPON)
				Goto ChargeFlashEnder
			ChargeNormalSound.Quake4:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Fire_Quake4",	CHAN_WEAPON)
				Goto ChargeFlashEnder

			PlayChargeMaxSound:
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 6, "PlayChargeMaxSound.Default")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 4, "PlayChargeMaxSound.Quake4")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 3, "PlayChargeMaxSound.Quake3")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 2, "PlayChargeMaxSound.Quake2")
			PlayChargeMaxSound.Default:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/ChargedShot_Default",	CHAN_WEAPON)
				Goto ChargeFlashEnder
			PlayChargeMaxSound.Quake2:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/ChargedShot_Quake2",		CHAN_WEAPON)
				Goto ChargeFlashEnder
			PlayChargeMaxSound.Quake3:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/ChargedShot_Quake3",		CHAN_WEAPON)
				Goto ChargeFlashEnder
			PlayChargeMaxSound.Quake4:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/ChargedShot_Quake4",		CHAN_WEAPON)
				Goto ChargeFlashEnder

			ChargeQuadSound:
				"####" A 0 A_JumpIfInventory("BFG_Charges",15,"PlayChargeMaxQuadSound")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 6, "ChargeQuadSound.Default")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 4, "ChargeQuadSound.Quake4")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 3, "ChargeQuadSound.Quake3")
				"####" A 0 A_JumpIfInventory("QCBFGSkin", 2, "ChargeQuadSound.Quake2")
			ChargeQuadSound.Default:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire_Default",	CHAN_WEAPON)
				Goto ChargeFlashEnder
			ChargeQuadSound.Quake2:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire_Quake2",	CHAN_WEAPON)
				Goto ChargeFlashEnder
			ChargeQuadSound.Quake3:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire_Quake3",	CHAN_WEAPON)
				Goto ChargeFlashEnder
			ChargeQuadSound.Quake4:
				"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire_Quake4",	CHAN_WEAPON)
				Goto ChargeFlashEnder

				PlayChargeMaxQuadSound:
					"####" A 0 A_JumpIfInventory("QCBFGSkin", 6, "PlayChargeMaxQuadSound.Default")
					"####" A 0 A_JumpIfInventory("QCBFGSkin", 4, "PlayChargeMaxQuadSound.Quake4")
					"####" A 0 A_JumpIfInventory("QCBFGSkin", 3, "PlayChargeMaxQuadSound.Quake3")
					"####" A 0 A_JumpIfInventory("QCBFGSkin", 2, "PlayChargeMaxQuadSound.Quake2")
				PlayChargeMaxQuadSound.Default:
					"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/ChargedShot_Default",	CHAN_WEAPON)
					Goto ChargeFlashEnder
				PlayChargeMaxQuadSound.Quake2:
					"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/ChargedShot_Quake2",	CHAN_WEAPON)
					Goto ChargeFlashEnder
				PlayChargeMaxQuadSound.Quake3:
					"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/ChargedShot_Quake3",	CHAN_WEAPON)
					Goto ChargeFlashEnder
				PlayChargeMaxQuadSound.Quake4:
					"####" A 0 A_PlaySound("QC/Weapons/BFG/Quad/ChargedShot_Quake4",	CHAN_WEAPON)
					Goto ChargeFlashEnder

	ChargeFlashEnder:
		"####" A 0 A_TakeInventory("BFG_Charges",32767)
		"####" A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
		"####" A 0 A_SetBlend("Green",0.14,20)
		"####" A 0 A_JumpIfInventory("WepNoRecoilToken",1,"FlashEnderNoRecoil")
		"####" A 0 A_SetPitch(pitch-2.5)
		"####" A 2
		"####" A 1 A_SetPitch(pitch+0.5)
		"####" A 1 A_SetPitch(pitch+0.5)
		"####" A 1 A_SetPitch(pitch+0.5)
		"####" A 1 A_SetPitch(pitch+0.5)
		"####" A 1 A_SetPitch(pitch+0.5)
		Stop
	}
}


Actor QCBFGShot
{
	Projectile
	Speed 45
	RenderStyle Add
	Alpha 0.99
	Scale 0.27
	Radius 4
	Height 8
	Damage (random(80,120))
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	+MTHRUSPECIES
	+SEEKERMISSILE
	+EXTREMEDEATH
	+DONTBLAST
	Obituary "$OB_QCBFGShot" // "%o felt the power of argent energy at %k's fingertips."
	Decal "D4BFGScorchSmall"
	DamageType "BFGDamage"
	States
	{
	Spawn:
		DB48 A 0 NODELAY A_PlaySound("QC/Weapons/BFG/BallFlying",7,0.6,true,2.5)
		"####" AB 1 Bright Light("GreenBigPoint3")
		"####" CD 1 Bright Light("GreenBigPoint3")
		"####" EF 1 Bright Light("GreenBigPoint3")
		"####" A 0 A_Explode(16,128,0)
		"####" GH 1 Bright Light("GreenBigPoint3")
		"####" IJ 1 Bright Light("GreenBigPoint3")
		"####" KL 1 Bright Light("GreenBigPoint3")
		"####" A 0 A_Explode(16,128,0)
		"####" MN 1 Bright Light("GreenBigPoint3")
		"####" OP 1 Bright Light("GreenBigPoint3")
		"####" QR 1 Bright Light("GreenBigPoint3")
		"####" A 0 A_Explode(16,128,0)
		"####" ST 1 Bright Light("GreenBigPoint3")
		"####" UV 1 Bright Light("GreenBigPoint3")
		"####" WX 1 Bright Light("GreenBigPoint3")
		"####" A 0 A_Explode(16,128,0)
		Goto Spawn

	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMax",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,268)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont

	DeathCont:
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage. Beware
		TNT1 A 0 A_RadiusThrust(88,64,RTF_AFFECTSOURCE|RTF_THRUSTZ)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(180,192,0,0,64)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",5,40)
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath

	EndDeath:
		TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Explo",7,1,0,0.75)
		TNT1 A 0 A_Quake(4,20,0,1500)
		TNT1 A 0 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExploSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 2
		TNT1 AAA 1 A_SpawnItemEx("BFGExploLightningSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		Stop
	}
}





Actor BFGExploRingSmall //shockwave
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 1.0
	Translation "0:255=%[0,0,0]:[0.0,1.8,0.0]"
	States
	{
	Spawn:
		X060 C 1 Bright A_SetScale(0.15)
		X060 C 1 Bright A_SetScale(0.45)
	Looper:
		X060 C 1 Bright A_SetScale(scalex*1.2)
		TNT1 A 0 A_FadeOut(0.1)
		Loop
	}
}

Actor BFGClassicExploSmall  //explosion
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.99
	scale 0.9
	States
	{
	Spawn:
		DB49 A 1 Bright Light("GreenSmallPoint1") A_SetScale(scalex*1.05)
		DB49 C 1 Bright Light("GreenMediumPoint3") A_SetScale(scalex*1.05)
		DB49 G 1 Bright Light("GreenMediumPoint2") A_SetScale(scalex*1.05)
		DB49 I 1 Bright Light("GreenMediumPoint1") A_SetScale(scalex*1.05)
		DB49 I 1 Bright Light("GreenBigPoint3") A_SetScale(scalex*1.05)
		DB49 M 1 Bright Light("GreenMediumPoint1") A_SetScale(scalex*1.05)
		DB49 O 1 Bright Light("GreenMediumPoint2") A_SetScale(scalex*1.05)
		DB49 S 1 Bright Light("GreenMediumPoint3") A_SetScale(scalex*1.05)
		DB49 U 1 Bright Light("GreenSmallPoint1") A_SetScale(scalex*1.05)
		DB49 Y 1 Bright Light("GreenSmallPoint2") A_SetScale(scalex*1.05)
		Stop
	}
}

Actor BFGExploLightningSmall
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.99
	scale 0.7
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"T1","T2","T3","T4","T5")
	T1:	DLI1 A 0
		Goto PlayOff
	T2:	DLI2 A 0
		Goto PlayOff
	T3:	DLI3 A 0
		Goto PlayOff
	T4:	DLI4 A 0
		Goto PlayOff
	T5:	DLI5 A 0
		Goto PlayOff

	PlayOff:
		"####" A 0 A_Jump(128,"FromL")
	FromA:
		"####" ABCDEFGHIJK 1 Bright
		Stop
	FromL:
		"####" LMNOPQRSTUV 1 Bright
		Stop

	}
}

Actor BFGfogLightning
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	var int user_counter;
	RenderStyle Add
	Alpha 0.99
	scale 0.65
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(200,2,3,4,5,6,7,8)	//for sliiiigh randomization of when it spawns
		TNT1 AAAAAAAAAA 1
		TNT1 A 0 A_Jump(256,"T1","T2","T3","T4","T5")

		T1:	DLI1 A 0
			Goto PlayOff
		T2:	DLI2 A 0
			Goto PlayOff
		T3:	DLI3 A 0
			Goto PlayOff
		T4:	DLI4 A 0
			Goto PlayOff
		T5:	DLI5 A 0
			Goto PlayOff

	PlayOff:
		"####" A 0 A_Jump(128,"FromL")

		FromA:
			"####" ABCDE 1 Bright
			"####" FGHIJK 1 Bright
			Goto Repeater
		FromL:
			"####" LMNOP 1 Bright
			"####" QRSTUV 1 Bright
			Goto Repeater

	Repeater:
		TNT1 A 0 A_JumpIf(user_counter>=2,"Destroy")
		TNT1 A 0 A_SetUserVar(user_counter,user_counter+1)
		Goto Spawn

	Destroy:
		TNT1 A 5
		Stop

	}
}


// ==========================================================================
// ==========================================================================
//	Weapon tokens
// ==========================================================================
// ==========================================================================

Actor BFG_UG_Rays	: Token {}
Actor BFG_UG_Charge	: Token {}
Actor QCBFGSkin		: Token { Inventory.MaxAmount 20 }

Actor BFG_Charges : Inventory
{
	+SERVERNETID
	Inventory.MaxAmount 15
	Inventory.InterHubAmount 0
}

Actor QCBFGChargeShot
{
	Projectile
	Speed 45
	RenderStyle Add
	Alpha 0.99
	Scale 0.4
	Radius 4
	Height 8
	Damage (random(80,120))
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	+MTHRUSPECIES
	+SEEKERMISSILE
	+EXTREMEDEATH
	+DONTBLAST
	Obituary "$OB_QCBFGChargeShot" // "%o felt the power of argent energy at %k's fingertips."
	Decal "D4BFGScorchSmall"
	DamageType "BFGDamage"
	States
	{
	Spawn:
		DB67 A 0 NODELAY A_PlaySound("QC/Weapons/BFG/BallFlying",7,0.6,true,2.5)
		DB67 AB 1 Bright Light("GreenBigPoint3")
		DB67 CD 1 Bright Light("GreenBigPoint3")
		DB67 EF 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB67 GH 1 Bright Light("GreenBigPoint3")
		DB67 IJ 1 Bright Light("GreenBigPoint3")
		DB67 KL 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB67 MN 1 Bright Light("GreenBigPoint3")
		DB67 OP 1 Bright Light("GreenBigPoint3")
		DB67 QR 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB67 ST 1 Bright Light("GreenBigPoint3")
		DB67 UV 1 Bright Light("GreenBigPoint3")
		DB67 WX 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB67 YZ 1 Bright Light("GreenBigPoint3")
		DB68 AB 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB68 CD 1 Bright Light("GreenBigPoint3")
		DB68 EF 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		Goto Spawn+1

	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMax",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,268)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont

	DeathCont:
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage. Beware
		TNT1 A 0 A_RadiusThrust(88,64,RTF_AFFECTSOURCE|RTF_THRUSTZ)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(180,192,0,0,64)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",5,40)
		Goto EndDeath

	EndDeath:
		TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Explo",7,1,0,0.75)
		TNT1 A 0 A_Quake(4,20,0,1500)
		TNT1 A 0 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExploSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 2
		TNT1 AAA 1 A_SpawnItemEx("BFGExploLightningSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		Stop
	}
}


Actor QCBFGChargeShot2 : QCBFGChargeShot
{
	Scale 0.48	//original is 0.4
	Damage (random(80,120)*2)
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage. Beware
		TNT1 A 0 A_RadiusThrust(88,64,RTF_AFFECTSOURCE|RTF_THRUSTZ)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(360,256,0,0,64)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",5,45)
		Goto EndDeath
	}
}

Actor QCBFGChargeShot3 : QCBFGChargeShot
{
	Scale 0.56
	Damage (random(80,120)*3)
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage. Beware
		TNT1 A 0 A_RadiusThrust(88,64,RTF_AFFECTSOURCE|RTF_THRUSTZ)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(540,320,0,0,64)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",8,50)
		Goto EndDeath
	}
}
Actor QCBFGChargeShot4 : QCBFGChargeShot
{
	Scale 0.64
	Damage (random(80,120)*4)
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage. Beware
		TNT1 A 0 A_RadiusThrust(88,64,RTF_AFFECTSOURCE|RTF_THRUSTZ)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(720,384,0,0,64)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",10,55)
		Goto EndDeath
	}
}
Actor QCBFGChargeShot5 : QCBFGChargeShot
{
	Scale 0.72
	Damage (random(80,120)*5)
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage. Beware
		TNT1 A 0 A_RadiusThrust(88,64,RTF_AFFECTSOURCE|RTF_THRUSTZ)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(900,448,0,0,64)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",12,60)
		Goto EndDeath
	}
}
Actor QCBFGChargeShot6 : QCBFGChargeShot
{
	Scale 1.0
	Damage (random(6,10)*200)	//massive direct hit damage
	+FORCERADIUSDMG
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage. Beware
		TNT1 A 0 A_RadiusThrust(88,64,RTF_AFFECTSOURCE|RTF_THRUSTZ)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(1080,512,0,0,64)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",20,60)
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	EndDeath:
		TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Explo",7,1,0,0.75)
		TNT1 A 0 A_Quake(4,20,0,1500)
		TNT1 A 0 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExploSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 2
		TNT1 AAA 2 A_SpawnItemEx("BFGExploLightningSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 6 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 6 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		Stop
	}
}




// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCBFGDM : QCBFG
{
	+WEAPON.NOAUTOAIM
	+CHEATNOTWEAPON
	Tag "$TAG_QCBFG" // "BFG 10k (DM)"
	Weapon.SelectionOrder 666 // highest
	Weapon.AmmoGive 10
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 5
}


Actor QCBFGShot_DM	//No AOE while flying
{
	Projectile
	Speed 45
	RenderStyle Add
	Alpha 0.99
	Scale 0.27
	Radius 6
	Height 12
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	+SEEKERMISSILE
	Obituary "$OB_QCBFGShot" // "%o felt the power of argent energy at %k's fingertips."
	Decal "D4BFGScorchSmall"
	DamageType "BFGDamage"
	States
	{
	Spawn:
		DB48 A 0 NODELAY A_PlaySound("QC/Weapons/BFG/BallFlying",7,0.6,true,2.5)
		DB48 ABCDEFGHIJKLMNOPQRSTUVWX 1 Bright Light("GreenBigPoint3")
		Goto Spawn+1

	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMax",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		//Medals start
		TNT1 A 0 A_GiveToTarget("BFGDirectDamageToken",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE|GIF_SKIPOWNER)
		//Medals end
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,180)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont

		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			//Medals start
			TNT1 A 0 A_GiveToTarget("BFGSplashDamageToken",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE|GIF_SKIPOWNER)
			//Medals end
			Goto DeathCont

	DeathCont:
		TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Explo",7,0.85,0,0.75)
		TNT1 A 0 A_Quake(3,15,0,768)
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE,0,20)	//self damage. Beware
		TNT1 A 0 A_RadiusThrust(88,64,RTF_AFFECTSOURCE|RTF_THRUSTZ)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(70,128,0,0,32)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExploSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 2
		TNT1 AAA 1 A_SpawnItemEx("BFGExploLightningSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		Stop
	}
}
