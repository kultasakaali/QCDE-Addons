// --------------------------------------------------------------------------
//
// BFG10K 
// Weapon and Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor BFGPickup : CustomInventory 15608
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "BFG 10k"
	Height 48
	Radius 20
	Scale 0.65
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCBFG" // "Picked up the BFG10k."
	Tag "$TAG_QCBFG" // "BFG 10k"
	Inventory.RespawnTics 2100	//60sec
	
	// Zandronum's client-server disagreement over whether the #dress is under 
	// the balcony or on the ledge has really wasted quite a bit of my time.
	// Here's to 2018, and here's to like maybe 16 hours I spent in both D4T 
	// and QCDE trying to get BFG to show its pickup where you can get it,
	// despite pickup being one actor.
	//
	// This flag is here to keep us from it until it's fixed in engine.
	// In some cases... :joy::gun:
	//
	// ~Michaelis
	+NOGRAVITY
	
	States
	{
	Spawn:
		QCG8 Y 0 NoDelay ACS_NamedExecuteWithResult("QCDE_RemoveIfDuel")
		QCG8 Y 35 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCG8 Z 0 A_ChangeFlag("DROPPED", false)
		QCG8 Z 70 Bright ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",22,args[4])
		QCG8 Z 0 A_ChangeFlag("NOGRAVITY", false)
		QCG8 Z -1 Bright
		Stop
	Idle:
		QCG8 Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",21,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCBFG")
		TNT1 A 0 
		Stop
	DMWeaponStay: // BFG doesn't stay even if it wants to in DM
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 A_GiveInventory("QCBFGDM")
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("QCBFG")
		TNT1 A 0
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCBFG",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCBFG", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	}
}

Actor QCBFG : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 2.6
	Weapon.SelectionOrder 6666 // lowest
	Weapon.AmmoUse 1
	Weapon.AmmoGive 8
	Weapon.AmmoType "QCBFGAmmo"
	Obituary "$OB_QCBFG" // "%o was melted by %k's BFG."
	+WEAPON.WIMPY_WEAPON
	Inventory.PickupMessage "$GOT_QCBFG" // "Picked up the BFG 10k."
	Tag "$TAG_QCBFG" // "BFG 10k"
	Scale 0.65
	States
	{
	Spawn:
		QCG8 Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		"####" "#" 0 ACS_NamedExecuteAlways("QCDE_RemoveIfDuel")
		QCG8 ZZZZZZZZZZZZZZZ 35 Bright
		Stop
	Idle:
		QCG8 Y -1
		Wait
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		QCG8 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG8 A 0 A_PlaySound("QC/Weapons/BFG/Idle",5,0.7,1)
		QCG8 A 0 A_JumpIfInventory("DMToken",1,"AltFireReady")
		QCG8 A 0 A_JumpIfInventory("BFG_UG_Charge",1,"AltFireReady")
		QCG8 BCDEFG 3 A_WeaponReady(WRF_NOSECONDARY)			//doesnt allow zoom, handled here to continue idle animation
		QCG8 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG8 HAAAAA 3 A_WeaponReady(WRF_NOSECONDARY)	
		Loop
		
	AltFireReady:
		QCG8 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG8 A 0 A_PlaySound("QC/Weapons/BFG/Idle",5,0.7,1)
		QCG8 BCDEFG 3 A_WeaponReady
		QCG8 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG8 HAAAAA 3 A_WeaponReady
		Loop
		
		ReadyCantFire:
			QCG8 A 0 A_StopSound(CHAN_WEAPON)
			QCG8 A 0 A_StopSound(6)
			"####" "#" 0 A_ZoomFactor(1.0)
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
			"####" "#" 0 A_TakeInventory("BFG_Charges",32767)
		RCFLooper:
			QCG8 A 4 A_WeaponReady(WRF_NOFIRE)
			QCG8 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		QCG8 A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		QCG8 A 0 A_PlaySound("QC/Weapons/BFG/Raise",6,0.75,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" "#" 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		QCG8 A 1 A_Raise
		"####" "###" 0 A_Raise
		Loop
		
	Deselect:
		"####" "#" 0 A_StopSound(CHAN_WEAPON)
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:
		QCG8 A 1 A_Lower
		"####" "###" 0 A_Lower
		Loop
		
	//-----------------------
	// Fire Seq
	//-----------------------
	Fire: 
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		Goto FIRAH
		
		CheckIfUpgrade:
			TNT1 A 0 A_JumpIfInventory("BFG_UG_Charge",1,"StartCharge")
			Goto FIRAH
		
	//-----------
	//Regular fire
	FIRAH:
		QCG8 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG8 A 0 A_StopSound(6) //stops raise sound
		QCG8 I 1 Bright offset(7,47) A_GunFlash("PrimaryFlash")
		QCG8 IJ 1 Bright 
		QCG8 K 1 offset(8,48)
		QCG8 K 1 offset(7,46)
		QCG8 K 1 offset(6,44)
		QCG8 K 1 offset(5,42)
		QCG8 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		QCG8 A 1 offset(4,40) A_Refire("FIRAH")
		QCG8 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		QCG8 A 1 offset(3,38) A_Refire("FIRAH")
		QCG8 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		QCG8 A 1 offset(2,36) A_Refire("FIRAH")
		QCG8 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"CheckIfUpgrade")
		QCG8 A 1 offset(1,32) A_Refire("FIRAH")
		Goto Ready


	PrimaryFlash: //Attacks doer
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"DM_Flash")
		Goto SP_Flash
			
		SP_Flash:
			TNT1 A 0 A_FireCustomMissile("QCBFGShot",0, true,1,5)
			Goto FlashCheckSound
		DM_Flash:
			TNT1 A 0 A_FireCustomMissile("QCBFGShot_DM",0, true,1,5)
			Goto FlashCheckSound
			
		FlashCheckSound:
			TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"QuadSound")
			Goto NormalSound
			
			NormalSound:
				TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Fire",CHAN_WEAPON)
				Goto FlashEnder
			QuadSound:
				TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire",CHAN_WEAPON)
				Goto FlashEnder
			
	FlashEnder:
		TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
		TNT1 A 0 A_SetBlend("Green",0.14,20)
		TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"FlashEnderZoomed")
		TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"FlashEnderNoRecoil")
		TNT1 A 0 A_SetPitch(pitch-2.5)
		TNT1 A 2
		TNT1 A 1 A_SetPitch(pitch+0.5)
		TNT1 A 1 A_SetPitch(pitch+0.5)
		TNT1 A 1 A_SetPitch(pitch+0.5)
		TNT1 A 1 A_SetPitch(pitch+0.5)
		TNT1 A 1 A_SetPitch(pitch+0.5)
		Stop
		
			FlashEnderNoRecoil:
				TNT1 A 1
				Stop
		
	FlashEnderZoomed:
		TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"FlashEnderNoRecoil")
		TNT1 A 0 A_SetPitch(pitch-1.25)
		TNT1 A 2
		TNT1 A 1 A_SetPitch(pitch+0.25)
		TNT1 A 1 A_SetPitch(pitch+0.25)
		TNT1 A 1 A_SetPitch(pitch+0.25)
		TNT1 A 1 A_SetPitch(pitch+0.25)
		TNT1 A 1 A_SetPitch(pitch+0.25)
		Stop
		
		
	//-----------
	//Charge fire
	StartCharge:
		QCG8 A 0 A_PlaySound("QC/Weapons/BFG/Charge",CHAN_WEAPON,1)
		QCG8 A 0 A_StopSound(CHAN_WEAPON) //stops looper so that they dont overlap
	Part1:
		QCG8 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG8 B 1 Bright Offset(1,34)
		QCG8 C 1 Bright Offset(2,33)
		QCG8 D 1 Bright Offset(1,33)
		QCG8 E 1 Bright Offset(1,34)
		QCG8 A 0 A_Refire("Part2")
		Goto Release
	Part2:
		QCG8 F 1 Bright Offset(2,33)
		QCG8 G 1 Bright Offset(1,33) 
		QCG8 H 1 Bright Offset(1,34)
		QCG8 A 1 Bright Offset(2,34)
		QCG8 A 0 A_GiveInventory("BFG_Charges",1)
		QCG8 A 0 A_JumpIfInventory("BFG_Charges",15,"MaxCharge")
		QCG8 A 0 A_Refire("Part1")
		Goto Release
	MaxCharge:
		QCG8 E 0 A_PlaySound("QC/Weapons/BFG/ChargeLoop",CHAN_WEAPON,0.5,1)
		QCG8 L 1 Bright Offset(1,33)
		QCG8 L 1 Bright Offset(2,34)
		QCG8 A 0 A_Refire("MaxCharge2")
		Goto Release
	MaxCharge2:
		QCG8 L 1 Bright Offset(1,34)
		QCG8 L 1 Bright Offset(2,33)
		QCG8 A 0 A_Refire("MaxCharge")
		Goto Release
	
	Release:
		QCG8 I 1 Bright offset(7,47) A_GunFlash("ChargedFlash")
		QCG8 IJ 1 Bright 
		QCG8 K 1 offset(8,48)
		QCG8 K 1 offset(7,46)
		QCG8 K 1 offset(6,44)
		QCG8 K 1 offset(5,42)
		QCG8 A 1 offset(4,40) 
		QCG8 A 1 offset(3,38)
		QCG8 A 1 offset(2,36) 
		QCG8 A 1 offset(1,32)
		Goto Ready
		

	ChargedFlash: //Attacks doer
			TNT1 A 0 A_JumpIfInventory("BFG_Charges",15,"FirePower6")
			TNT1 A 0 A_JumpIfInventory("BFG_Charges",12,"FirePower5")
			TNT1 A 0 A_JumpIfInventory("BFG_Charges",9,"FirePower4")
			TNT1 A 0 A_JumpIfInventory("BFG_Charges",6,"FirePower3")
			TNT1 A 0 A_JumpIfInventory("BFG_Charges",3,"FirePower2")
			Goto FirePower1
			
		FirePower1:	
			TNT1 A 0 A_FireCustomMissile("QCBFGChargeShot",0, true,1,5)
			Goto GenericFlashChecks
		FirePower2:
			TNT1 A 0 A_FireCustomMissile("QCBFGChargeShot2",0, true,1,5)
			TNT1 A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			TNT1 A 0 A_TakeInventory("QCBFGAmmo",1,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks
		FirePower3:	
			TNT1 A 0 A_FireCustomMissile("QCBFGChargeShot3",0, true,1,5)
			TNT1 A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			TNT1 A 0 A_TakeInventory("QCBFGAmmo",2,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks
		FirePower4:	
			TNT1 A 0 A_FireCustomMissile("QCBFGChargeShot4",0, true,1,5)
			TNT1 A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			TNT1 A 0 A_TakeInventory("QCBFGAmmo",3,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks
		FirePower5:
			TNT1 A 0 A_FireCustomMissile("QCBFGChargeShot5",0, true,1,5)
			TNT1 A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			TNT1 A 0 A_TakeInventory("QCBFGAmmo",4,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks
		FirePower6:
			TNT1 A 0 A_FireCustomMissile("QCBFGChargeShot6",0, true,1,5)
			TNT1 A 0 A_JumpIfInventory("PowerInfin",1,"GenericFlashChecks")
			TNT1 A 0 A_TakeInventory("QCBFGAmmo",5,TIF_NOTAKEINFINITE)
			Goto GenericFlashChecks
			
		GenericFlashChecks:
			TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"ChargeQuadSound")
			Goto ChargeNormalSound
			ChargeNormalSound:
				TNT1 A 0 A_JumpIfInventory("BFG_Charges",15,"PlayChargeMaxSound")
				TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Fire",CHAN_WEAPON)
				Goto ChargeFlashEnder
			PlayChargeMaxSound:
				TNT1 A 0 A_PlaySound("QC/Weapons/BFG/ChargedShot",CHAN_WEAPON)
				Goto ChargeFlashEnder
			ChargeQuadSound:
				TNT1 A 0 A_JumpIfInventory("BFG_Charges",15,"PlayChargeMaxQuadSound")
				TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Quad/Fire",CHAN_WEAPON)
				Goto ChargeFlashEnder
				PlayChargeMaxQuadSound:
					TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Quad/ChargedShot",CHAN_WEAPON)
					Goto ChargeFlashEnder
	ChargeFlashEnder:

		TNT1 A 0 A_TakeInventory("BFG_Charges",32767)
		TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
		TNT1 A 0 A_SetBlend("Green",0.14,20)
		TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"FlashEnderNoRecoil")
		TNT1 A 0 A_SetPitch(pitch-2.5)
		TNT1 A 2
		TNT1 A 1 A_SetPitch(pitch+0.5)
		TNT1 A 1 A_SetPitch(pitch+0.5)
		TNT1 A 1 A_SetPitch(pitch+0.5)
		TNT1 A 1 A_SetPitch(pitch+0.5)
		TNT1 A 1 A_SetPitch(pitch+0.5)
		Stop
	}
}


Actor QCBFGShot
{
	Projectile 
	Speed 45
	RenderStyle Add
	Alpha 0.99
	Scale 0.27
	Radius 4
	Height 8
	Damage (random(80,120))
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	+MTHRUSPECIES
	+SEEKERMISSILE
	+EXTREMEDEATH
	+DONTBLAST
	Obituary "$OB_QCBFGShot" // "%o felt the power of argent energy at %k's fingertips."
	Decal "D4BFGScorchSmall"
	DamageType "BFGDamage"
	States
	{
	Spawn:
		DB48 A 0 NODELAY A_PlaySound("QC/Weapons/BFG/BallFlying",7,0.6,true,2.5)
		DB48 AB 1 Bright Light("GreenBigPoint3")
		DB48 CD 1 Bright Light("GreenBigPoint3")
		DB48 EF 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB48 GH 1 Bright Light("GreenBigPoint3")
		DB48 IJ 1 Bright Light("GreenBigPoint3")
		DB48 KL 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB48 MN 1 Bright Light("GreenBigPoint3")
		DB48 OP 1 Bright Light("GreenBigPoint3")
		DB48 QR 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB48 ST 1 Bright Light("GreenBigPoint3")
		DB48 UV 1 Bright Light("GreenBigPoint3")
		DB48 WX 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		Goto Spawn+1
		
	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMax",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,268)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont
			
	DeathCont:
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(180,192,0,0,64)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage. Beware
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",5,40)
		Goto EndDeath
		
	EndDeath:	
		TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Explo",7,1,0,0.75)
		TNT1 A 0 A_Quake(4,20,0,1500)
		TNT1 A 0 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExploSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 2
		TNT1 AAA 1 A_SpawnItemEx("BFGExploLightningSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		Stop
	}
}





Actor BFGExploRingSmall //shockwave
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 1.0
	Translation "0:255=%[0,0,0]:[0.0,1.8,0.0]"
	States
	{
	Spawn:
		X060 C 1 Bright A_SetScale(0.15)
		X060 C 1 Bright A_SetScale(0.45)
	Looper:	
		X060 C 1 Bright A_SetScale(scalex*1.2)
		TNT1 A 0 A_FadeOut(0.1)
		Loop
	}
}

Actor BFGClassicExploSmall  //explosion
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.99
	scale 0.9
	States
	{
	Spawn:
		DB49 A 1 Bright Light("GreenSmallPoint1") A_SetScale(scalex*1.05)
		DB49 C 1 Bright Light("GreenMediumPoint3") A_SetScale(scalex*1.05)
		DB49 G 1 Bright Light("GreenMediumPoint2") A_SetScale(scalex*1.05)
		DB49 I 1 Bright Light("GreenMediumPoint1") A_SetScale(scalex*1.05)
		DB49 I 1 Bright Light("GreenBigPoint3") A_SetScale(scalex*1.05)
		DB49 M 1 Bright Light("GreenMediumPoint1") A_SetScale(scalex*1.05)
		DB49 O 1 Bright Light("GreenMediumPoint2") A_SetScale(scalex*1.05)
		DB49 S 1 Bright Light("GreenMediumPoint3") A_SetScale(scalex*1.05)
		DB49 U 1 Bright Light("GreenSmallPoint1") A_SetScale(scalex*1.05)
		DB49 Y 1 Bright Light("GreenSmallPoint2") A_SetScale(scalex*1.05)
		Stop
	}
}

Actor BFGExploLightningSmall
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	RenderStyle Add
	Alpha 0.99
	scale 0.7
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"T1","T2","T3","T4","T5")
	T1:	DLI1 A 0 
		Goto PlayOff
	T2:	DLI2 A 0 
		Goto PlayOff
	T3:	DLI3 A 0 
		Goto PlayOff
	T4:	DLI4 A 0 
		Goto PlayOff
	T5:	DLI5 A 0 
		Goto PlayOff
		
	PlayOff:
		"####" A 0 A_Jump(128,"FromL")
	FromA:
		"####" ABCDEFGHIJK 1 Bright
		Stop
	FromL:
		"####" LMNOPQRSTUV 1 Bright
		Stop
		
	}
}

Actor BFGfogLightning 
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	var int user_counter;
	RenderStyle Add
	Alpha 0.99
	scale 0.65
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(200,2,3,4,5,6,7,8)	//for sliiiigh randomization of when it spawns
		TNT1 AAAAAAAAAA 1
		TNT1 A 0 A_Jump(256,"T1","T2","T3","T4","T5")
		
		T1:	DLI1 A 0 
			Goto PlayOff
		T2:	DLI2 A 0 
			Goto PlayOff
		T3:	DLI3 A 0 
			Goto PlayOff
		T4:	DLI4 A 0 
			Goto PlayOff
		T5:	DLI5 A 0 
			Goto PlayOff
		
	PlayOff:
		"####" A 0 A_Jump(128,"FromL")
		
		FromA:
			"####" ABCDE 1 Bright
			"####" FGHIJK 1 Bright
			Goto Repeater
		FromL:
			"####" LMNOP 1 Bright
			"####" QRSTUV 1 Bright
			Goto Repeater
		
	Repeater:
		TNT1 A 0 A_JumpIf(user_counter>=2,"Destroy")
		TNT1 A 0 A_SetUserVar(user_counter,user_counter+1)
		Goto Spawn
		
	Destroy:
		TNT1 A 5
		Stop
		
	}
}


// ==========================================================================
// ==========================================================================

//	Upgrades

// ==========================================================================
// ==========================================================================

Actor BFG_UG_Rays    : Token {}
Actor BFG_UG_Charge  : Token {}

Actor BFG_Charges : Inventory
{
	+SERVERNETID
	Inventory.MaxAmount 15
	Inventory.InterHubAmount 0
} 

Actor QCBFGChargeShot
{
	Projectile 
	Speed 45
	RenderStyle Add
	Alpha 0.99
	Scale 0.4
	Radius 4
	Height 8
	Damage (random(80,120))
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	+MTHRUSPECIES
	+SEEKERMISSILE
	+EXTREMEDEATH
	+DONTBLAST
	Obituary "$OB_QCBFGChargeShot" // "%o felt the power of argent energy at %k's fingertips."
	Decal "D4BFGScorchSmall"
	DamageType "BFGDamage"
	States
	{
	Spawn:
		DB67 A 0 NODELAY A_PlaySound("QC/Weapons/BFG/BallFlying",7,0.6,true,2.5)
		DB67 AB 1 Bright Light("GreenBigPoint3")
		DB67 CD 1 Bright Light("GreenBigPoint3")
		DB67 EF 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB67 GH 1 Bright Light("GreenBigPoint3")
		DB67 IJ 1 Bright Light("GreenBigPoint3")
		DB67 KL 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB67 MN 1 Bright Light("GreenBigPoint3")
		DB67 OP 1 Bright Light("GreenBigPoint3")
		DB67 QR 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB67 ST 1 Bright Light("GreenBigPoint3")
		DB67 UV 1 Bright Light("GreenBigPoint3")
		DB67 WX 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB67 YZ 1 Bright Light("GreenBigPoint3")
		DB68 AB 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		DB68 CD 1 Bright Light("GreenBigPoint3")
		DB68 EF 1 Bright Light("GreenBigPoint3")
		TNT1 A 0 A_Explode(16,128,0)
		Goto Spawn+1
		
	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMax",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,268)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont
			
	DeathCont:
		TNT1 A 0 A_Explode(180,192,0,0,64)
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage. Beware
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",5,40)
		Goto EndDeath
		
	EndDeath:	
		TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Explo",7,1,0,0.75)
		TNT1 A 0 A_Quake(4,20,0,1500)
		TNT1 A 0 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExploSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 2
		TNT1 AAA 1 A_SpawnItemEx("BFGExploLightningSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		Stop
	}
}


Actor QCBFGChargeShot2 : QCBFGChargeShot
{
	Scale 0.48	//original is 0.4
	Damage (random(80,120)*2)
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(360,256,0,0,64)
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",5,45)	
		Goto EndDeath
	}
}

Actor QCBFGChargeShot3 : QCBFGChargeShot
{
	Scale 0.56
	Damage (random(80,120)*3)
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(540,320,0,0,64)
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",8,50)
		Goto EndDeath
	}
}
Actor QCBFGChargeShot4 : QCBFGChargeShot
{
	Scale 0.64
	Damage (random(80,120)*4)
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(720,384,0,0,64)
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",10,55)
		Goto EndDeath
	}
}
Actor QCBFGChargeShot5 : QCBFGChargeShot
{
	Scale 0.72
	Damage (random(80,120)*5)
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(900,448,0,0,64)
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",12,60)
		Goto EndDeath
	}
}
Actor QCBFGChargeShot6 : QCBFGChargeShot
{
	Scale 1.0
	Damage (random(6,10)*200)	//massive direct hit damage
	+FORCERADIUSDMG
	States
	{
	DeathCont:
		TNT1 A 0 A_Explode(1080,512,0,0,64)	//some really nice explosion damage
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE)	//self damage
		TNT1 A 0 A_JumpIfInTargetInventory("BFG_UG_Rays",1,"DoRays")
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	DoRays:
		TNT1 A 0 A_BFGSpray("BFGfogLightning",20,60)
		TNT1 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	EndDeath:	
		TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Explo",7,1,0,0.75)
		TNT1 A 0 A_Quake(4,20,0,1500)
		TNT1 A 0 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExploSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 2
		TNT1 AAA 2 A_SpawnItemEx("BFGExploLightningSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 6 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 6 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		Stop
	}
}




// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCBFGDM : QCBFG
{	
	+WEAPON.NOAUTOAIM	
	+CHEATNOTWEAPON
	Tag "$TAG_QCBFG" // "BFG 10k (DM)"
	Weapon.SelectionOrder 666 // highest
	Weapon.AmmoGive 10
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 5
}


Actor QCBFGShot_DM	//No AOE while flying
{
	Projectile 
	Speed 45
	RenderStyle Add
	Alpha 0.99
	Scale 0.27
	Radius 6
	Height 12
	+FORCERADIUSDMG
	+FORCEXYBILLBOARD
	+SEEKERMISSILE
	Obituary "$OB_QCBFGShot" // "%o felt the power of argent energy at %k's fingertips."
	Decal "D4BFGScorchSmall"
	DamageType "BFGDamage"
	States
	{
	Spawn:
		DB48 A 0 NODELAY A_PlaySound("QC/Weapons/BFG/BallFlying",7,0.6,true,2.5)
		DB48 ABCDEFGHIJKLMNOPQRSTUVWX 1 Bright Light("GreenBigPoint3")
		Goto Spawn+1
		
	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMax",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		//Medals start
		TNT1 A 0 A_GiveToTarget("BFGDirectDamageToken",1)
		//Medals end
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,180)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			//Medals start
			TNT1 A 0 A_GiveToTarget("BFGSplashDamageToken",1) 
			//Medals end
			Goto DeathCont
			
	DeathCont:
		TNT1 A 0 A_PlaySound("QC/Weapons/BFG/Explo",7,0.85,0,0.75)
		TNT1 A 0 A_Quake(3,15,0,768)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(70,128,0,0,32)
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_Explode(40,64,XF_HURTSOURCE,0,20)	//self damage. Beware
		TNT1 A 0 A_SpawnItemEx("BFGExploRingSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 0 A_SpawnItemEx("BFGClassicExploSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		TNT1 A 2
		TNT1 AAA 1 A_SpawnItemEx("BFGExploLightningSmall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SKIPOWNER|SXF_NOUNLAGGED)
		Stop
	}
}
