// --------------------------------------------------------------------------
//
// Nailgun
// Weapon and Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

// This pickup is only for consistency -- it never gives weapon.
Actor NailgunPickup : CustomInventory
{
	Height 48
	Radius 20
	Scale 0.5
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCNailgun" // "Picked up a Nailgun."
	Tag "$TAG_QCNailgun" // "Nailgun"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		Stop
	Idle:
		TNT1 A 0
		Stop
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 //A_GiveInventory("QCNailgun")
		TNT1 A 0
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 //A_GiveInventory("QCNailgunDM")
		TNT1 A 0
		TNT1 A 0 //A_TakeInventory("QCNailgun")
		TNT1 A 0
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCNailgun",1,"Stay")
		TNT1 A 0 //A_SpawnItemEx("QCNailgun", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_TakeInventory("QCNailgun")
		TNT1 A 0 A_JumpIfInventory("QCNailgunDM",1,"Stay")
		TNT1 A 0 //A_SpawnItemEx("QCNailgunDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto HideDoomish+1
	}
}



Actor QCNailgun : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 3.4
	Weapon.Kickback 30
	Weapon.SelectionOrder 2000
	Weapon.AmmoUse 1
	Weapon.AmmoGive 50
	Weapon.AmmoType "QCNailAmmo"
	+INVENTORY.UNDROPPABLE
	Obituary "$OB_QCNailgun" // "%o was nailed by %k."
	Inventory.PickupMessage "$GOT_QCNailgun" // "Picked up a Nailgun."
	Tag "$TAG_QCNailgun" // "Nailgun"
	Scale 0.5
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_ChangeFlag("NOBLOCKMAP",1)	//in DM, if a player drops this weapon, cant be picked
		TNT1 A 3	//and disappears
		Stop
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 6, "Ready.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 4, "Ready.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 2, "Ready.Quake2")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 1, "Ready.Quake1")
	Ready.Default:
		QCG4 A 0
		Goto ReadyCont
	Ready.Quake1:
		Q1G4 A 0
		Goto ReadyCont
	Ready.Quake2:
		Q2G4 A 0
		Goto ReadyCont
	Ready.Quake4:
		Q4G4 A 0
		Goto ReadyCont
	Ready.QCDE1:
		Q0G4 A 0
		Goto ReadyCont

	ReadyCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" AAA 1 A_WeaponReady
		Loop

		ReadyCantFire:
			"####" A 0 A_StopSound(CHAN_WEAPON)
			"####" A 0 A_StopSound(6)
			"####" A 0 A_ZoomFactor(1.0)
			"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			"####" A 4 A_WeaponReady(WRF_NOFIRE)
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 6, "Select.QCDE1")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 4, "Select.Quake4")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 2, "Select.Quake2")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 1, "Select.Quake1")
	Select.Default:
		QCG4 A 0
		Goto SelectCont
	Select.Quake1:
		Q1G4 A 0
		Goto SelectCont
	Select.Quake2:
		Q2G4 A 0
		Goto SelectCont
	Select.Quake4:
		Q4G4 A 0
		Goto SelectCont
	Select.QCDE1:
		Q0G4 A 0
		Goto SelectCont

	SelectCont:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		"####" A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3) //Not in Weapon.UpSound because it interrupts CHAN_WEAPON sounds
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops any weapon looping sound
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		"####" A 1 A_Raise
		"####" AAA 0 A_Raise
		Loop

	Deselect:
		"####" A 0 A_StopSound(CHAN_WEAPON) //stops the looping fire sound
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:
		"####" A 1 A_Lower
		"####" AAA 0 A_Lower
		Loop

	//-----------------------
	// Fire Seq
	//-----------------------
	Fire:
		"####" A 0 A_JumpIfInventory("PowerQuad",1,"FireQuadSound")
		Goto FireNormalSound

		FireNormalSound:
			"####" A 0 A_JumpIfInventory("QCNailgunSkin", 6, "FireNormalSound.Default")
			"####" A 0 A_JumpIfInventory("QCNailgunSkin", 4, "FireNormalSound.Quake4")
			"####" A 0 A_JumpIfInventory("QCNailgunSkin", 2, "FireNormalSound.Quake2")
			"####" A 0 A_JumpIfInventory("QCNailgunSkin", 1, "FireNormalSound.Quake1")
		FireNormalSound.Default:
			"####" A 0 A_PlaySound("QC/Weapons/Nailgun/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireNormalSound.Quake1:
			"####" A 0 A_PlaySound("QC/Weapons/Nailgun/FireStartLoop_Quake1",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireNormalSound.Quake2:
			"####" A 0 A_PlaySound("QC/Weapons/Nailgun/FireStartLoop_Quake2",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireNormalSound.Quake4:
			"####" A 0 A_PlaySound("QC/Weapons/Nailgun/FireStartLoop_Quake4",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck

		FireQuadSound:
			"####" A 0 A_JumpIfInventory("QCNailgunSkin", 6, "FireQuadSound.Default")
			"####" A 0 A_JumpIfInventory("QCNailgunSkin", 4, "FireQuadSound.Quake4")
			"####" A 0 A_JumpIfInventory("QCNailgunSkin", 2, "FireQuadSound.Quake2")
			"####" A 0 A_JumpIfInventory("QCNailgunSkin", 1, "FireQuadSound.Quake1")
		FireQuadSound.Default:
			"####" A 0 A_PlaySound("QC/Weapons/Nailgun/Quad/FireStartLoop_Default",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireQuadSound.Quake1:
			"####" A 0 A_PlaySound("QC/Weapons/Nailgun/Quad/FireStartLoop_Quake1",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireQuadSound.Quake2:
			"####" A 0 A_PlaySound("QC/Weapons/Nailgun/Quad/FireStartLoop_Quake2",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck
		FireQuadSound.Quake4:
			"####" A 0 A_PlaySound("QC/Weapons/Nailgun/Quad/FireStartLoop_Quake4",CHAN_WEAPON,1,1)
			Goto FirePostSoundCheck

	FirePostSoundCheck:
		"####" A 0 A_AlertMonsters
		"####" A 0 A_JumpIfInventory("DMToken",1,"DM_FireLooper")
		Goto FireLooper

		//SP fire, two stages, one uses ammo, the other doesnt
		FireLooper:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_GunFlash("SP_Flash")
			"####" B 1 Bright 	Offset(2,33)
			"####" E 1 		 	Offset(3,35)
			"####" F 1 			Offset(0,32)
			"####" A 0 A_ReFire("FireLooperNA")
			Goto EndFireLooper
		FireLooperNA:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_GunFlash("SP_FlashNA")
			"####" C 1 Bright 	Offset(2,33)
			"####" E 1 		 	Offset(3,35)
			"####" F 1 			Offset(0,32)
			"####" A 0 A_ReFire("FireLooper")
			Goto EndFireLooper


		//DM fire
		DM_FireLooper:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_GunFlash("DM_Flash")
			"####" B 1 Bright 	Offset(2,33)
			"####" E 1 		 	Offset(3,35)
			"####" F 1 			Offset(0,32)
			"####" A 0 A_ReFire("DM_FireLooper2")
			Goto EndFireLooper
		DM_FireLooper2:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 0 A_GunFlash("DM_Flash")
			"####" C 1 Bright 	Offset(2,33)
			"####" E 1 		 	Offset(3,35)
			"####" F 1 			Offset(0,32)
			"####" A 0 A_ReFire("DM_FireLooper")
			Goto EndFireLooper

	EndFireLooper:
		"####" A 0 A_JumpIfInventory("QCNailgunSkin", 6, "EndFireLooper.Default")
		"####" A 0 A_JumpIfInventory("QCNailgunSkin", 4, "EndFireLooper.Quake4")
		"####" A 0 A_JumpIfInventory("QCNailgunSkin", 2, "EndFireLooper.Quake2")
		"####" A 0 A_JumpIfInventory("QCNailgunSkin", 1, "EndFireLooper.Quake1")
	EndFireLooper.Default:
		"####" A 0 A_PlaySound("QC/Weapons/Nailgun/FireEnd_Default",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.Quake1:
		"####" A 0 A_PlaySound("QC/Weapons/Nailgun/FireEnd_Quake1",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.Quake2:
		"####" A 0 A_PlaySound("QC/Weapons/Nailgun/FireEnd_Quake2",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont
	EndFireLooper.Quake4:
		"####" A 0 A_PlaySound("QC/Weapons/Nailgun/FireEnd_Quake4",CHAN_WEAPON,1,0)
		Goto EndFireLooperCont

	EndFireLooperCont:
		"####" GHIGGHHIIGGGHHHIII 1 A_WeaponReady
		Goto Ready


	//-----------------------
	// Attack doers
	//-----------------------

	//Single player
	SP_Flash:
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 6, "SP_Flash.Default")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 4, "SP_Flash.Default")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 2, "SP_Flash.Quake2")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 1, "SP_Flash.Quake1")
	SP_Flash.Default:
		TNT1 A 0 A_FireCustomMissile("QCNailWeak",0,true,1,5)
		Goto EndFlash
	SP_Flash.Quake1:
		TNT1 A 0 A_FireCustomMissile("QCNailWeak_Quake1",0,true,1,5)
		Goto EndFlash
	SP_Flash.Quake2:
		TNT1 A 0 A_FireCustomMissile("QCNailWeak_Quake2",0,true,1,5)
		Goto EndFlash

	SP_FlashNA:
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 6, "SP_FlashNA.Default")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 4, "SP_FlashNA.Default")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 2, "SP_FlashNA.Quake2")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 1, "SP_FlashNA.Quake1")
	SP_FlashNA.Default:
		TNT1 A 0 A_FireCustomMissile("QCNailWeak",0, false,1,5) //doesnt use ammo
		Goto EndFlash
	SP_FlashNA.Quake1:
		TNT1 A 0 A_FireCustomMissile("QCNailWeak_Quake1",0, false,1,5) //doesnt use ammo
		Goto EndFlash
	SP_FlashNA.Quake2:
		TNT1 A 0 A_FireCustomMissile("QCNailWeak_Quake2",0, false,1,5) //doesnt use ammo
		Goto EndFlash

	//Deathmatch
	DM_Flash:
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 6, "DM_Flash.Default")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 4, "DM_Flash.Default")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 2, "DM_Flash.Quake2")
		TNT1 A 0 A_JumpIfInventory("QCNailgunSkin", 1, "DM_Flash.Quake1")
	DM_Flash.Default:
		TNT1 A 0 A_FireCustomMissile("QCNailWeakDM",0,true,1,5)
		Goto EndFlash
	DM_Flash.Quake1:
		TNT1 A 0 A_FireCustomMissile("QCNailWeakDM_Quake1",0,true,1,5)
		Goto EndFlash
	DM_Flash.Quake2:
		TNT1 A 0 A_FireCustomMissile("QCNailWeakDM_Quake2",0,true,1,5)
		Goto EndFlash

		EndFlash:
			TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
			TNT1 A 0 A_SetBlend("Yellow",0.03,3)
			TNT1 A 0 A_FireCustomMissile("MachinegunSmokeSpawner",0, false,6,2)
			TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"EndFlashNoRecoil")
			TNT1 A 1 A_SetPitch(Pitch-0.6)
			TNT1 AA 1 A_SetPitch(Pitch+0.3)
			Stop
		EndFlashNoRecoil:
			TNT1 A 1
			Stop
	}
}

//----------------------------------------------------------------------------------------

Actor QCNailWeakDeathFX
{
	+NOINTERACTION
	+FORCEXYBILLBOARD
	+NONETID
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	Speed 0
	Scale 0.45

	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(256,"T1","T2","T3","T4")

	T1:
		DB09 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	T2:
		DB10 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	T3:
		DB11 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath
	T4:
		DB12 A 0 A_Jump(255,"EndDeath")
		Goto EndDeath

	EndDeath:
		"####" ABCDEF 1 Light("YellowSmallFlicker3") Bright
		"####" GH 1 Bright
		"####" IJKLMNOPQRST 2 Bright
		Stop
	}
}

Actor QCNailWeak : FastProjectile
{
	Projectile
	+FORCEXYBILLBOARD
	+BLOODSPLATTER
	+MTHRUSPECIES
	Obituary "$OB_QCNailgun" // "%o was nailed by %k."
	Speed 52
	Radius 3
	Height 6
	Damage (random(5,10))
	Scale 0.34
	Decal "BulletChipBlack"
	+DONTBLAST
	States
	{
	Spawn:
		TNT1 A 1
		NAII A 1 Bright
		Wait

	XDeath:
		TNT1 A 10 A_GiveToTarget("HitBeepLow",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE)
		//Medals start
		TNT1 A 5 A_GiveToTarget("NGDamageToken",1,AAPTR_DEFAULT,GIF_FORCESERVERSIDE|GIF_SKIPOWNER)
		//Medals end
		Stop

	Death:
		TNT1 A 0 A_PlaySound("QC/Weapons/Nailgun/Impact_Default",CHAN_AUTO,0.6,0,1.7,AAPTR_TARGET)
		TNT1 A 0 A_SpawnItemEx("QCNailWeakDeathFX", 0,0,0, 0,0,0, 0, SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Explode(3,32,XF_HURTSOURCE,0,32)
		TNT1 A 0 A_RadiusThrust(21,32,RTF_AFFECTSOURCE|RTF_THRUSTZ,32)	//extra thrust because damage thrust alone is not enough
		TNT1 A 0 A_SetScale(0.45)
		Stop
	}
}

Actor QCNailWeak_Quake1 : QCNailWeak
{
	States
	{
	Spawn:
		TNT1 A 1
		NA1I A 1 Bright
		Wait
	}
}

Actor QCNailWeak_Quake2 : QCNailWeak
{
	States
	{
	Spawn:
		TNT1 A 1
		NA2I A 1 Bright Light("YellowSmallPoint2")
		Wait
	Death:
		TNT1 A 0 A_PlaySound("QC/Weapons/Nailgun/Impact_Quake2",CHAN_AUTO,0.6,0,1.7,AAPTR_TARGET)
		Goto Super::Death+1
	}
}

Actor QCNailWeakDM : QCNailWeak
{
	Damage (12)
	-MTHRUSPECIES
	-DONTBLAST
}

Actor QCNailWeakDM_Quake1 : QCNailWeakDM
{
	States
	{
	Spawn:
		TNT1 A 1
		NA1I A 1 Bright
		Wait
	}
}

Actor QCNailWeakDM_Quake2 : QCNailWeakDM
{
	States
	{
	Spawn:
		TNT1 A 1
		NA2I A 1 Bright Light("YellowSmallPoint2")
		Wait
	Death:
		TNT1 A 0 A_PlaySound("QC/Weapons/Nailgun/Impact_Quake2",CHAN_AUTO,0.6,0,1.7,AAPTR_TARGET)
		Goto Super::Death+1
	}
}


// ==========================================================================
// ==========================================================================
//	Weapon tokens
// ==========================================================================
// ==========================================================================

Actor QCNailgunSkin	: Token { Inventory.MaxAmount 20 }


// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCNailgunDM : QCNailgun
{
	+WEAPON.NOAUTOAIM
	+CHEATNOTWEAPON
	Tag "$TAG_QCNailgun" // "Nailgun (DM)"
	Weapon.AmmoGive 100
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 10
}