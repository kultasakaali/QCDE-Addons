// --------------------------------------------------------------------------
//
// Rocket Launcher
// Weapon Sprite by Franco Tieppo
// Pickup sprite by FrancoTieppo
// Weapon code by DBT
//
// --------------------------------------------------------------------------

Actor RocketLauncherPickup : CustomInventory 15605
{
	//$NotAngled
	//$Category "QCDE Weapons"
	//$Color 14
	//$Title "Rocket Launcher"
	Height 48
	Radius 20
	Scale 0.69
	Inventory.PickupSound "QC/Items/WeaponPick"
	Inventory.PickupMessage "$GOT_QCRocketLauncher" // "Picked up a Rocket Launcher."
	Tag "$TAG_QCRocketLauncher" // "RocketLauncher"
	Inventory.RespawnTics 35
	States
	{
	Spawn:
		QCG5 Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		"####" "#" 0 A_ChangeFlag("DROPPED", 0)
		QCG5 Z -1 Bright ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",16,args[4])
		Stop
	Idle:
		QCG5 Y -1 ACS_NamedExecuteWithResult("QCDE_WeaponSpawned",15,args[4])
		Wait
	Pickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMPickup")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"WeaponStay")
		TNT1 A 0 A_GiveInventory("QCRocketLauncher")
		TNT1 A 0 
		Stop
	DMPickup:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("WeaponStayCheck",0,0,0,0),"DMWeaponStay")
		TNT1 A 0 A_GiveInventory("QCRocketLauncherDM")
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("QCRocketLauncher")
		TNT1 A 0
		Stop
	WeaponStay:
		TNT1 A 0 A_JumpIfInventory("QCRocketLauncher",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCRocketLauncher", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	DMWeaponStay:
		TNT1 A 0 A_TakeInventory("QCRocketLauncher")
		TNT1 A 0 A_JumpIfInventory("QCRocketLauncherDM",1,"Stay")
		TNT1 A 0 A_SpawnItemEx("QCRocketLauncherDM", velx,vely,velz, velx,vely,velz, 0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEPOSITION)
		Fail
	Stay:
		TNT1 A 0
		Fail
	HideIndefinitely:
		TNT1 A 1 A_SetTics(CallACS("QCDE_SetWeaponRespawnTics"))
		Goto HideDoomish+1
	}
}


Actor QCRocketLauncher : QCWeapon
{
	Height 48
	Weapon.SwaySpeed 1.3
	Weapon.Kickback 0	//to allow rocket jumping that doesnt mess with your forward movement too much
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.AmmoType "QCRocketAmmo"
	Inventory.PickupMessage "$GOT_QCRocketLauncher" // "Picked up a Rocket Launcher."
	Obituary "$OB_QCRocketLauncher" // "%o embraced %k's rockets."
	Tag "$TAG_QCRocketLauncher" // "Rocket Launcher"
	Scale 0.69
	States
	{
	Spawn:
		QCG5 Y 35 NoDelay A_JumpIf(ACS_NamedExecuteWithResult("MultiplayerCheck",0,0,0,0) == 2,"DMSpawn")
		Goto Idle
	DMSpawn:
		QCG5 ZZZZZZZZZZZZZZZ 35 Bright
		Stop
	Idle:
		QCG5 Y -1
		Wait
		
	//-----------------------
	// Ready Seq
	//-----------------------
	Ready:
		QCG5 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		QCG5 A 4 A_WeaponReady
		Loop
		
		ReadyCantFire:
			QCG5 A 0 A_StopSound(5)
			QCG5 A 0 A_StopSound(6)
			QCGC A 0 A_StopSound(CHAN_WEAPON)
			"####" "#" 0 A_ZoomFactor(1.0)
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		RCFLooper:
			QCG5 A 4 A_WeaponReady(WRF_NOFIRE)
			QCG5 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"RCFLooper")
			Goto Ready
	//-----------------------
	// Select/Deselect Seq
	//-----------------------
	Select:
		QCG5 A 0 A_JumpIfInventory("NoWeaponFireToken",1,2)
		QCG5 A 0 A_PlaySound("QC/Weapons/Switch",6,1,0,3)
		"####" "#" 0 A_StopSound(5) //stops any weapon looping sound
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	SelectLooper:
		QCG5 A 1 A_Raise
		"####" "###" 0 A_Raise
		Loop
		
	Deselect:
		//Medals start
		"####" "#" 0 A_GiveInventory("ComboSwitchRLGiver")
		//Medals end
		"####" "#" 0 A_ZoomFactor(1.0)
		"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
	DeselectLooper:
		QCG5 A 1 A_Lower
		"####" "###" 0 A_Lower
		Loop
		
	//-----------------------
	// Fire Seq
	//-----------------------
	Fire:	//28 tics, 0.8sec
		QCG5 A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_GunFlash("PrimaryFlash")
		
		QCG5 B 1 Offset(20,29) Bright
		QCG5 C 1 Offset(30,23) Bright 
		QCG5 A 1 Offset(33,17) Bright 
		
		QCG5 ADEFG 1 Offset(37,14)
		QCG5 HIJKKLLMM 1 Offset(37,14)
		
		QCG5 M 1 Offset(37,14)
		QCG5 N 1 Offset(37,14)
		QCG5 N 1 Offset(31,16)
		QCG5 O 1 Offset(23,18)
		QCG5 P 1 Offset(17,20)
		QCG5 A 1 Offset(12,22)
		QCG5 A 1 Offset(10,24)
		
		QCG5 A 1 Offset(7,26)
		QCG5 A 1 Offset(4,28)
		QCG5 A 1 Offset(2,30)
		QCG5 A 1 Offset(1,31)
		Goto Ready
		
	PrimaryFlash:
		TNT1 A 0 A_JumpIfInventory("DMToken",1,"PerformDM")
		Goto PerformSP
		
		PerformSP:
			TNT1 A 0 A_JumpIfInventory("RL_UG_Control",1,"CheckIfZoomed")
			TNT1 A 0 A_FireCustomMissile("QCRocketPlayer", 0, true, 1, 5)
			Goto FlashCheckSound
			
			CheckIfZoomed:
				TNT1 A 0 A_JumpIfInventory("WeaponZoomedToken",1,"FireControlledRocket")
				Goto PerformSP+1
			FireControlledRocket:
				TNT1 A 0 A_FireCustomMissile("RocketControlled", 0, true, 1, 5)
				TNT1 A 0 SetPlayerProperty(0,1,PROP_TOTALLYFROZEN) // to remove possibility of refire and such
				TNT1 A 0 A_GiveInventory("PowerControllerProtection") // because player is otherwise defenseless
				TNT1 A 0 A_ZoomFactor(1.0) 	//immediately unzooms and removes zoom token
				TNT1 A 0 A_TakeInventory("WeaponZoomedToken",32767)
				Goto FlashCheckSound
			
		PerformDM:
			TNT1 A 0 A_JumpIfInventory("RocketArena",1,"PerformRA")
			//Medals start
			TNT1 A 0 A_GiveInventory("RLFiredGiver",1)
			//Medals end
			TNT1 A 0 A_FireCustomMissile("QCRocketPlayerDM", 0, true, 1, 5)
			Goto FlashCheckSound
			PerformRA:
				TNT1 A 0 A_FireCustomMissile("QCRocketPlayerDM", 0, false, 1, 5) // maybe shoot a no-self-damage rocket?
				Goto FlashCheckSound
			
		FlashCheckSound:
			TNT1 A 0 A_JumpIfInventory("PowerQuad",1,"QuadSound")
			Goto NormalSound
			
			NormalSound:
				TNT1 A 0 A_PlaySound("QC/Weapons/RocketLauncher/Fire", CHAN_WEAPON, 1.0, 0)
				Goto FlashEnder
			QuadSound:
				TNT1 A 0 A_PlaySound("QC/Weapons/RocketLauncher/Quad/Fire", CHAN_WEAPON, 1.0, 0)
				Goto FlashEnder
			
	FlashEnder:
		TNT1 A 0 A_JumpIfInventory("WepNoFlashToken",1,2)
		TNT1 A 0 A_SetBlend("Orange",0.15,12)
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("WepNoRecoilToken",1,"FlashEnderNoRecoil")
		TNT1 A 7 A_SetPitch(pitch-3.6)
		TNT1 AAAAAA 1 Offset(29,62) A_SetPitch(pitch+0.6)
		Stop
	FlashEnderNoRecoil:
		TNT1 A 1
		Stop
		
	//-----------------------
	// Fire Seq / Zoom
	//-----------------------
	AltFire:
		QCG5 A 0 A_JumpIfInventory("DMToken",1,"StartZoomChecks")	//in Deathmatch, allow zoom checks by Default
		"####" A 0 A_JumpIfInventory("RL_UG_Control", 1, "StartZoomChecks")	//also if upgrade present, allow checks
	NoZoom:
		"####" A 2 A_WeaponReady(WRF_NOSECONDARY)	//dummy state in case player is holding "alt fire" all along
		"####" A 0 A_Refire("NoZoom")
		Goto Ready
	StartZoomChecks:
		"####" A 0 A_JumpIfInventory("WeaponZoomedToken", 1, "ZoomOut")
	ZoomIn:
		"####" AAA 1
		"####" A 0 A_JumpIfInventory("DMToken",1,"ZoomDM")
		"####" "#" 0 A_PlaySound("QC/Weapons/Upgrade/RLBeep",CHAN_WEAPON)
		"####" "#" 0 ACS_NamedExecuteAlways("QCDE_ModSelectedText",0,15)
		"####" "#" 0 A_ZoomFactor(1.1)
		Goto ContinueZoom
	ZoomDM:
		"####" "#" 0 A_ZoomFactor(1.8)
	ContinueZoom:
		"####" "#" 0 A_GiveInventory("WeaponZoomedToken",1)
	ZoomInHeld:
		"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
		"####" A 1 
		"####" "#" 1 A_Refire("ZoomInHeld")
		Goto Ready
		
		ZoomOut:
			"####" "#" 0 A_ZoomFactor(1.0) 
			"####" "#" 0 A_TakeInventory("WeaponZoomedToken",32767)
		ZoomOutHeld:
			"####" A 0 A_JumpIfInventory("NoWeaponFireToken",1,"ReadyCantFire")
			"####" A 1 
			"####" "#" 1 A_Refire("ZoomOutHeld")
			Goto Ready
	}
}


Actor QCRocketPlayer
{
	Projectile
	+RANDOMIZE
	+DEHEXPLOSION
	+EXTREMEDEATH
	-ROCKETTRAIL
	+SEEKERMISSILE //Not actually used for anything other than checking radius.
	+MTHRUSPECIES
	Radius 3
	Height 6
	Speed 34
	Damage (random(7,11)*10)
	+DONTBLAST
	Scale 0.6
	Obituary "$OB_QCRocketPlayer" // "%o rode %k's rocket."
	SeeSound ""
	DeathSound "QC/Weapons/RocketLauncher/RocketExplo"
	Decal "D4ExploScorch"
	DamageType "RocketDamage"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay ACS_NamedExecuteAlways("QCDE_AttachWhoosh")
		//Medals start
		//TNT1 A 0 ACS_NamedExecuteAlways("QCDE_RocketStuff",0) //we are no longer executing this from here as we do the checks on frag event
		//Medals end
		MICR A 2 Light("YellowMediumPoint2") Bright //no trails for a bit
	Looper:
		MICR A 1 Light("YellowMediumPoint2") Bright A_SpawnItemEx("PlayerRocketTrailFire",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		MICR A 0 A_SpawnItemEx("RocketTrailSmoke",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		//Medals start
		MICR A 0 A_GiveInventory("PreciseCounter")
		//Medals end
		Loop

	Xdeath:	//Direct hit close range
		//Medals start
		TNT1 A 0 A_JumpIfInventory("PreciseCounter",10,"XDeathPrecise") 
		//Medals end
		TNT1 A 0 A_GiveToTarget("HitBeepBig",1)
		//Medals start
		TNT1 A 0 A_GiveToTarget("RLDirectDamageToken",1)
		TNT1 A 0 A_GiveToTarget("RLHitGiver",1) 
		//Medals end
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
	//Medals start
	XDeathPrecise: //Direct hit long range
        TNT1 A 0 A_GiveToTarget("DirectRocketToken",1)
        TNT1 A 0 A_GiveToTarget("HitBeepBig",1)
        TNT1 A 0 A_GiveToTarget("RLDirectDamageToken",1)
        TNT1 A 0 A_GiveToTarget("RLHitGiver",1)
        TNT1 A 0 A_Jump(256,"DeathCont")
		Goto DeathCont
	//Medals end
		
	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,180)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepMid",1)
			//Medals start
			TNT1 A 0 A_GiveToTarget("RLSplashDamageToken",1)
			TNT1 A 0 A_GiveToTarget("RLHitGiver",1)
			//Medals end
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont
		
	DeathCont:
		TNT1 A 0 A_JumpIfInTargetInventory("RL_UG_Cluster",1,"PerformCluster")
		TNT1 A 0 A_StopSound(7)
		TNT1 A 0 A_Explode(50,128,1|XF_QROCKETJUMP)	//max damage taken by self rocket (rocket jump) is 50
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(78,128,XF_QROCKETJUMP)	//The rest doesnt deal damage to player, adds to 128
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_SpawnItemEx("QCRocketExploDebris",-1,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 AAAAA 0 A_SpawnItemEx("QCRocketExploParticle",3,0,0,frandom(3,5.5),frandom(3,5.5),frandom(1,6),random(0,359),SXF_NOCHECKPOSITION)
		TNT1 A 10 A_SpawnItemEx("QCRocketExplosion",-2,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	PerformCluster:
		TNT1 A 0 A_CheckFloor("PerformPlane")
		TNT1 A 0 A_CheckCeiling("PerformPlane")
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 150,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 210,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 110,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 250,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto DeathCont+1
	PerformPlane:
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, -45,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0,  45,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 135,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0,-135,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto DeathCont+1		
	}
}



Actor QCRocketPlayerDM : QCRocketPlayer
{
	Damage (16)
	+FORCERADIUSDMG
	-MTHRUSPECIES
	-DONTBLAST
	Obituary "$OB_QCRocketPlayer" // "%o rode %k's rocket."
	States
	{
	DeathCont:
		TNT1 A 0 A_StopSound(7)
		TNT1 A 0 A_Explode(50,128,1|XF_QROCKETJUMP,0,18)	//max damage taken by self rocket (rocket jump) is 50
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", true)
		TNT1 A 0 A_Explode(34,128,XF_QROCKETJUMP,0,18)	//then another 34 extra damage for impacting a player + (16)dmg on the projectile =100dmg
		TNT1 A 0 A_ChangeFlag("NODAMAGETHRUST", false)
		TNT1 A 0 A_SpawnItemEx("QCRocketExploDebrisDM",-1,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 AAAAA 0 A_SpawnItemEx("QCRocketExploParticle",3,0,0,frandom(3,5.5),frandom(3,5.5),frandom(1,6),random(0,359),SXF_NOCHECKPOSITION)
		TNT1 A 10 A_SpawnItemEx("QCRocketExplosionDM",-2,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		//Medals start
		TNT1 A 0 A_TakeFromTarget("DirectRocketToken",1)
		//Medals end
		Stop
	}
}

Actor QCRocketExplosion
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	+DONTBLAST
	Scale 0.15
	States
	{
	Spawn:
		X125 A 0
		X125 A 1 Light("YellowSmallFlicker2") 	Bright A_SetScale(scalex+0.1125)
		X125 A 1 Light("YellowMediumFlicker3") 	Bright A_SetScale(scalex+0.1125)
		X125 B 1 Light("YellowMediumFlicker1") 	Bright A_SetScale(scalex+0.075)
		X125 B 1 Light("YellowBigFlicker3") 	Bright A_SetScale(scalex+0.075)
		X125 C 1 Light("YellowBigFlicker3") 	Bright A_SetScale(scalex+0.0525)
		X125 C 1 Light("YellowBigFlicker3") 	Bright A_SetScale(scalex+0.0525)
		X125 D 1 Light("YellowBigFlicker3") 	Bright
		X125 D 1 Light("YellowBigFlicker3") 	Bright
		X125 E 1 Light("YellowBigFlicker3")		Bright A_SetScale(scalex*1.05)
		X125 E 1 Light("YellowMediumFlicker1")	Bright 
		X125 F 1 Light("YellowMediumFlicker1")	Bright A_SetScale(scalex*1.05)
		X125 F 1 Light("YellowMediumFlicker2")	Bright 
		X125 G 1 Light("YellowMediumFlicker2")	Bright A_SetScale(scalex*1.05)
		X125 G 1 Light("YellowMediumFlicker3")	Bright 
		X125 H 0 Light("YellowSmallFlicker1")	Bright A_SetScale(scalex*1.05)
		X125 H 1 Light("YellowSmallFlicker1")	Bright A_FadeOut(0.075)
		X125 H 1 Light("YellowSmallFlicker2")	Bright A_FadeOut(0.075)
		X125 I 0 Light("YellowSmallFlicker3")	Bright A_SetScale(scalex*1.05)
		X125 I 1 Light("YellowSmallFlicker3")	Bright A_FadeOut(0.075)
		X125 I 1 Bright A_FadeOut(0.075)
		X125 J 0 Bright A_SetScale(scalex*1.05)
		X125 J 1 Bright A_FadeOut(0.075)
		X125 J 1 Bright A_FadeOut(0.075)
		X125 K 0 Bright A_SetScale(scalex*1.05)
		X125 K 1 Bright A_FadeOut(0.075)
		X125 K 1 Bright A_FadeOut(0.075)
		X125 L 0 Bright A_FadeOut(0.075)
		X125 L 1 Bright A_SetScale(scalex*1.05) 
		X125 L 1 Bright A_FadeOut(0.075)
		X125 M 0 Bright A_FadeOut(0.075)
		X125 M 1 Bright A_SetScale(scalex*1.05) 
		X125 M 1 Bright A_FadeOut(0.075)
		X125 N 0 Bright A_FadeOut(0.075)
		X125 N 1 Bright A_SetScale(scalex*1.05) 
		X125 N 1 Bright A_FadeOut(0.075)
		X125 O 0 Bright A_FadeOut(0.075)
		X125 O 1 Bright A_SetScale(scalex*1.05) 
		X125 O 1 Bright A_FadeOut(0.075)
		X125 P 0 Bright A_FadeOut(0.075)
		X125 P 1 Bright A_SetScale(scalex*1.05) 
		X125 P 1 Bright A_FadeOut(0.075)
		X125 Q 0 Bright A_FadeOut(0.075)
		X125 Q 1 Bright A_SetScale(scalex*1.05) 
		X125 Q 1 Bright A_FadeOut(0.075)
		X125 R 0 Bright A_FadeOut(0.075)
		X125 R 1 Bright A_SetScale(scalex*1.05) 
		X125 R 1 Bright A_FadeOut(0.075)
		Stop
	}
}

Actor QCRocketExplosionDM : QCRocketExplosion	//fades out much faster
{
	States
	{
	Spawn:
		X125 A 0
		X125 A 1 Light("YellowSmallFlicker2") 	Bright A_SetScale(scalex+0.1125)
		TNT1 A 0 A_FadeOut(0.04)
		X125 A 1 Light("YellowMediumFlicker3") 	Bright A_SetScale(scalex+0.1125)
		TNT1 A 0 A_FadeOut(0.04)
		X125 B 1 Light("YellowMediumFlicker1") 	Bright A_SetScale(scalex+0.075)
		TNT1 A 0 A_FadeOut(0.04)
		X125 B 1 Light("YellowBigFlicker3") 	Bright A_SetScale(scalex+0.075)
		TNT1 A 0 A_FadeOut(0.04)
		X125 C 1 Light("YellowBigFlicker3") 	Bright A_SetScale(scalex+0.0525)
		TNT1 A 0 A_FadeOut(0.04)
		X125 C 1 Light("YellowBigFlicker3") 	Bright A_SetScale(scalex+0.0525)
		TNT1 A 0 A_FadeOut(0.04)
		X125 D 1 Light("YellowBigFlicker3") 	Bright
		TNT1 A 0 A_FadeOut(0.04)
		X125 D 1 Light("YellowBigFlicker3") 	Bright
		TNT1 A 0 A_FadeOut(0.04)
		X125 E 1 Light("YellowBigFlicker3")		Bright A_SetScale(scalex*1.05)
		TNT1 A 0 A_FadeOut(0.04)
		X125 E 1 Light("YellowMediumFlicker1")	Bright 
		TNT1 A 0 A_FadeOut(0.04)
		X125 F 1 Light("YellowMediumFlicker1")	Bright A_SetScale(scalex*1.05)
		TNT1 A 0 A_FadeOut(0.04)
		X125 F 1 Light("YellowMediumFlicker2")	Bright 
		TNT1 A 0 A_FadeOut(0.04)
		X125 G 1 Light("YellowMediumFlicker2")	Bright A_SetScale(scalex*1.05)
		TNT1 A 0 A_FadeOut(0.04)
		X125 G 1 Light("YellowMediumFlicker3")	Bright 
		TNT1 A 0 A_FadeOut(0.04)
		X125 H 0 Light("YellowSmallFlicker1")	Bright A_SetScale(scalex*1.05)
		TNT1 A 0 A_FadeOut(0.04)
		X125 H 1 Light("YellowSmallFlicker1")	Bright A_FadeOut(0.075)
		TNT1 A 0 A_FadeOut(0.04)
		X125 H 1 Light("YellowSmallFlicker2")	Bright A_FadeOut(0.075)
		TNT1 A 0 A_FadeOut(0.04)
		X125 I 0 Light("YellowSmallFlicker3")	Bright A_SetScale(scalex*1.05)
		TNT1 A 0 A_FadeOut(0.04)
		X125 I 1 Light("YellowSmallFlicker3")	Bright A_FadeOut(0.075)
		X125 I 1 Bright A_FadeOut(0.03)
		X125 J 0 Bright A_SetScale(scalex*1.05)
		X125 J 1 Bright A_FadeOut(0.03)
		X125 J 1 Bright A_FadeOut(0.03)
		X125 K 0 Bright A_SetScale(scalex*1.05)
		X125 K 1 Bright A_FadeOut(0.03)
		X125 K 1 Bright A_FadeOut(0.03)
		X125 L 0 Bright A_FadeOut(0.03)
		X125 L 1 Bright A_SetScale(scalex*1.05) 
		X125 L 1 Bright A_FadeOut(0.03)
		X125 M 0 Bright A_FadeOut(0.03)
		X125 M 1 Bright A_SetScale(scalex*1.05) 
		X125 M 1 Bright A_FadeOut(0.03)
		X125 N 0 Bright A_FadeOut(0.03)
		X125 N 1 Bright A_SetScale(scalex*1.05) 
		X125 N 1 Bright A_FadeOut(0.03)
		X125 O 0 Bright A_FadeOut(0.03)
		X125 O 1 Bright A_SetScale(scalex*1.05) 
		X125 O 1 Bright A_FadeOut(0.03)
		X125 P 0 Bright A_FadeOut(0.03)
		X125 P 1 Bright A_SetScale(scalex*1.05) 
		X125 P 1 Bright A_FadeOut(0.03)
		X125 Q 0 Bright A_FadeOut(0.03)
		X125 Q 1 Bright A_SetScale(scalex*1.05) 
		X125 Q 1 Bright A_FadeOut(0.03)
		X125 R 0 Bright A_FadeOut(0.03)
		X125 R 1 Bright A_SetScale(scalex*1.05) 
		X125 R 1 Bright A_FadeOut(0.03)
		Stop
	}
}


Actor QCRocketExplosionBigger
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	Scale 0.2
	States
	{
	Spawn:
		X125 AA 1 NODELAY Bright A_SetScale(scalex+0.15)
		X125 BB 1 Bright A_SetScale(scalex+0.1)
		X125 CC 1 Bright A_SetScale(scalex+0.07)
		X125 DEFGHIJKLMNOPQR 2 Bright A_SetScale(scalex*1.05)
		Stop
	}
}

//*****************
// trails of rocket
Actor PlayerRocketTrailFire 
{
	+CLIENTSIDEONLY +NONETID
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+DONTBLAST
	RenderStyle Add
	Alpha 0.5
	Scale 0.12
	States
	{
	Spawn:
		TNT1 A 2
		TNT1 A 0 A_Jump(256,"One","Two")
	One:
		X007 CDE 1 Bright 
		X007 FGHIJ 1 Bright 
		Stop
	Two:
		X007 MNO 1 Bright 
		X007 PQRST 1 Bright 
		Stop		
	}
}
Actor RocketTrailSmoke	//centered smoke
{
	+CLIENTSIDEONLY +NONETID
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+DONTBLAST
	RenderStyle Translucent
	Alpha 0.2
	Scale 0.0
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,1,2,3,4)
		X037 A 0 A_SetScale(frandom(0,0.2)) Goto Missile
		X037 B 0 A_SetScale(frandom(0,0.2)) Goto Missile
		X037 C 0 A_SetScale(frandom(0,0.2)) Goto Missile
		X037 D 0 A_SetScale(frandom(0,0.2)) Goto Missile
	Missile:
		"####" "#" 1 A_SetScale(ScaleX+0.007)	"####" "#" 0 A_FadeIn(0.02)
		"####" "#" 1 A_SetScale(ScaleX+0.007)	"####" "#" 0 A_FadeIn(0.02)
		"####" "#" 1 A_SetScale(ScaleX+0.007)	"####" "#" 0 A_FadeIn(0.02)
		"####" "#" 1 A_SetScale(ScaleX+0.007)	"####" "#" 0 A_FadeIn(0.02)
		"####" "#" 1 A_SetScale(ScaleX+0.007)	"####" "#" 0 A_FadeIn(0.02)
		"####" "#" 1 A_SetScale(ScaleX+0.007)	"####" "#" 0 A_FadeIn(0.02)
		"####" "#" 1 A_SetScale(ScaleX+0.007)	"####" "#" 0 A_FadeIn(0.02)
		"####" "#" 1 A_SetScale(ScaleX+0.007)	"####" "#" 0 A_FadeIn(0.02)
		Goto Continue
	Continue:
		"####" "#" 1 A_FadeOut(0.025)
		"####" "#" 1 A_SetScale(ScaleX+0.007)
		Loop
	}
}


Actor QCRocketExploDebris
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	+DONTBLAST
	scale 1.4
	States
	{
	Spawn:
		DB01 A 0 NODELAY A_Jump(256,"T1","T2","T3","T4","T5","T6","T7","T8")
		Goto PlayOff
	T1:	DB01 A 0 
		Goto PlayOff
	T2:	DB02 A 0 
		Goto PlayOff
	T3:	DB03 A 0 
		Goto PlayOff
	T4:	DB04 A 0 
		Goto PlayOff
	T5:	DB05 A 0 
		Goto PlayOff
	T6:	DB06 A 0 
		Goto PlayOff
	T7:	DB07 A 0 
		Goto PlayOff
	T8:	DB08 A 0 
		Goto PlayOff
	PlayOff:
		"####" ABCDEFGHIJKLL 1 Bright 
		"####" MMNNOOPPQQRRSSTTUUVVWWXXYYZZ 1 Bright A_FadeOut(0.07)
		"####" Z 1 Bright A_FadeOut(0.05)
		Wait
	}
}


Actor QCRocketExploDebrisDM : QCRocketExploDebris	//fades out much faster
{
	+NOINTERACTION +NOTONAUTOMAP
	+CLIENTSIDEONLY +NONETID
	+FORCEXYBILLBOARD
	+DONTBLAST
	scale 1.4
	States
	{
	Spawn:
		DB01 A 0 NODELAY A_Jump(256,"T1","T2","T3","T4","T5","T6","T7","T8")
		Goto PlayOff
	T1:	DB01 A 0 
		Goto PlayOff
	T2:	DB02 A 0 
		Goto PlayOff
	T3:	DB03 A 0 
		Goto PlayOff
	T4:	DB04 A 0 
		Goto PlayOff
	T5:	DB05 A 0 
		Goto PlayOff
	T6:	DB06 A 0 
		Goto PlayOff
	T7:	DB07 A 0 
		Goto PlayOff
	T8:	DB08 A 0 
		Goto PlayOff
	PlayOff:
		"####" ABCDEFGHIJKLL 1 Bright A_FadeOut(0.035) 
		"####" MNOPQRSTUVWXYZ 1 Bright A_FadeOut(0.02)
		"####" Z 1 Bright A_FadeOut(0.05)
		Wait
	}
}

Actor QCRocketExploParticle
{
	Projectile
	Speed 10
	-NOGRAVITY
	+THRUACTORS
	+PAINLESS
	+DOOMBOUNCE
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY +NONETID
	+NOTONAUTOMAP
	+DONTBLAST
	Radius 2
	Height 5
	Gravity 0.2
	BounceFactor 0.2
	WallBounceFactor 1.0
	RenderStyle Add
	Translation "0:255=%[0,0,0]:[1.5,0.78,0]"
	Scale 0.05
	states
	{
	Spawn:
		FX01 A 15 Bright
		FX01 A 1 Bright A_FadeOut(0.11)
		Wait
	}
}






// ==========================================================================
// ==========================================================================

//	Upgrades

// ==========================================================================
// ==========================================================================

Actor RL_UG_Cluster : Token {}
Actor RL_UG_Control : Token {}


//--------------------------------------------------------------------------------
//--------------------------------------------------------------------------------
//	Upgrade related actors




//-----------------------------------------------------------
// Controllable Rocket
Actor RocketControlled
{
	var int user_counter;
	+NOTIMEFREEZE
	+EXTREMEDEATH
	+SEEKERMISSILE //Not actually used for anything other than checking radius.
	+MTHRUSPECIES
	+FORCERADIUSDMG
	+DONTBLAST
	Obituary "$OB_RocketControlled" // "%o rode %k's rocket."
	radius 3
	height 6
	speed 15
	Damage (random(7,11)*30)
	seesound ""
	deathsound ""
	PROJECTILE
	ReactionTime 560
	+SKYEXPLODE
	Decal "D4ExploScorch"
	DamageType "RocketDamage"
	states
	{
	Spawn:
		TRCR A 0 
		TRCR A 0 A_PlaySound("QC/Weapons/RocketLauncher/Fly",CHAN_5|CHAN_NOPAUSE,1.0,true)
		MICR A 1 bright ACS_NamedExecuteWithResult("QCDE_RocketControl")	//Projectile control and whatnot
		TRCR A 0 bright ACS_NamedExecuteWithResult("QCDE_TunnelVision")	//Black Screen effect
		TNT1 A 0 A_SetUserVar(user_counter,1)
	Looper:
		MICR A 0 A_JumpIfInventory("ImDead",1,"DieTime")
		MICR A 0 A_SpawnItemEx("PlayerRocketTrailFire", -15)
		MICR A 0 A_SpawnItemEx("RocketTrailSmoke", -15)
		MICR A 0 A_SpawnItemEx("ControlledRocketTrail", -15,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION, 128)
		MICR A 1 Bright Light("YellowMediumPoint2") A_CountDown
		Loop
		
	DieTime:
		TNT1 A 0 A_Die
		Goto Death
		
	//--------------------------------------------------------------
	//  Direct hit, checks for beeps, then proceeds to common death 
	Xdeath:	
		// All these are the checks done in procs but since this is the rocket
		// and not the player, the procs don't work. So it needs to do all checks
		// manually in the actor itself
			TNT1 A 0 A_JumpIfInTargetInventory("PlayHitBeep",1,"PlayBeep1")
			TNT1 A 0 A_JumpIfInTargetInventory("PlayHitMark",1,"DoMark1")
			TNT1 A 0 A_Jump(255,"DeathCont")
			Goto DeathCont
		PlayBeep1:
			TNT1 A 0 A_JumpIfInTargetInventory("PowerQuad",1,"PlayStrong1")
			TNT1 A 0 A_PlaySound("QC/UI/HitBeepBig",CHAN_7|CHAN_UI,0.85,0,140)
			TNT1 A 0 A_JumpIfInTargetInventory("PlayHitMark",1,"DoMark1")
			TNT1 A 0 A_Jump(255,"DeathCont")
			Goto DeathCont
		PlayStrong1:
			TNT1 A 0 A_PlaySound("QC/UI/HitBeepMax",CHAN_7|CHAN_UI,0.7,0,140)
			TNT1 A 0 A_JumpIfInTargetInventory("PlayHitMark",1,"Mark")
			TNT1 A 0 A_Jump(255,"DeathCont")
			Goto DeathCont
		DoMark1:
			TNT1 A 0 ACS_NamedExecuteAlways("QCDE_HitMark",0)
			TNT1 A 0 A_Jump(255,"DeathCont")
			Goto DeathCont
			
	//--------------------------------------------------------------
	//  In-direct hit, checks for beeps, then proceeds to common death 
	Death:
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,224)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
		RadiusHit:	//splash damage beep
			// Same as above, only this version is for playing a weaker hitbeep 
			// when they're not hit by a direct, but just the explosion
				TNT1 A 0 A_JumpIfInTargetInventory("PlayHitBeep",1,"PlayBeep2")
				TNT1 A 0 A_JumpIfInTargetInventory("PlayHitMark",1,"DoMark2")
				TNT1 A 0 A_Jump(255,"DeathCont")
				Goto DeathCont
			PlayBeep2:
				TNT1 A 0 A_JumpIfInTargetInventory("PowerQuad",1,"PlayStrong2")
				TNT1 A 0 A_PlaySound("QC/UI/HitBeepMid",CHAN_7|CHAN_UI,0.85,0,140)
				TNT1 A 0 A_JumpIfInTargetInventory("PlayHitMark",1,"DoMark2")
				TNT1 A 0 A_Jump(255,"DeathCont")
				Goto DeathCont
			PlayStrong2:
				TNT1 A 0 A_PlaySound("QC/UI/HitBeepBig",CHAN_7|CHAN_UI,0.7,0,140)
				TNT1 A 0 A_JumpIfInTargetInventory("PlayHitMark",1,"DoMark2")
				TNT1 A 0 A_Jump(255,"DeathCont")
				Goto DeathCont
			DoMark2:
				TNT1 A 0 ACS_NamedExecuteAlways("QCDE_HitMark",0)
				TNT1 A 0 A_Jump(255,"DeathCont")
				Goto DeathCont
			
	DeathCont:
		TNT1 A 0 A_JumpIf(user_counter>=1,"SkipForceUnfreeze")	//if it didnt live at least one tic to set everything up
		TNT1 A 0 A_GiveToTarget("UnfreezerProc",1)				//then force unfreeze on player
	SkipForceUnfreeze:
		TNT1 A 0 A_GiveInventory("ImDead")
		TNT1 A 0 A_JumpIfInTargetInventory("RL_UG_Cluster",1,"PerformCluster")
	AfterClusterCheck:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_PlaySound("QC/Weapons/RocketLauncher/RocketExplo",CHAN_5|CHAN_NOPAUSE)
		TNT1 A 0 ACS_NamedExecuteWithResult ("QCDE_ResetRocketFov", 0, 0, 0, 0)	//fov back to 90 in the camera
		TNT1 A 0 A_Explode(180,160,1|XF_QROCKETJUMP,0,64)
		TNT1 A 0 A_SpawnItemEx("QCRocketExploDebris",-1,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 AAAAA 0 A_SpawnItemEx("QCRocketExploParticle",3,0,0,frandom(3,5.5),frandom(3,5.5),frandom(1,6),random(0,359),SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("QCRocketExplosion",-2,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SetPitch(0)
		TNT1 AAAA 1 A_Recoil(10)							//to see the explosion
		TNT1 A 30 A_Stop
		stop
	PerformCluster:
		TNT1 A 0 A_CheckFloor("PerformPlane")
		TNT1 A 0 A_CheckCeiling("PerformPlane")
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 150,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 210,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 110,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 250,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto AfterClusterCheck
	PerformPlane:
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, -45,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0,  45,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0, 135,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("RLClusterRocket", 0,0,0, 10,0,0,-135,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		Goto AfterClusterCheck	
	}
}

Actor PowerControllerProtection : PowerProtection {	DamageFactor "Normal", 0.1 }

Actor UnfreezerProc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 SetPlayerProperty(0,0,PROP_TOTALLYFROZEN) // To forcibly unfreeze player in case rocket died too soon
		Stop
	}
}


Actor RLClusterRocket
{
	Projectile
	+FORCERADIUSDMG
	+EXTREMEDEATH
	+SEEKERMISSILE
	+LOOKALLAROUND
	Radius 2
	Height 3
	Speed 15
	Damage 10
	SeeSound ""
	Scale 0.18
	Decal "Scorch"
	DamageType "RocketDamage"
	+FRIENDLY
	+DONTBLAST
	ReactionTime 175	//175*2=350 tics, 10 seconds
	+MTHRUSPECIES
	States
	{
	Spawn:
		TNT1 A 0 A_SpawnItemEx("NetGuardRocketTrailFire",-4,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		QROK A 1 A_SeekerMissile(15,16,SMF_LOOK|SMF_PRECISE ,0,0)
		QROK A 1 
		QROK A 0 A_CountDown
		Loop
	Xdeath:	//Direct hit
		TNT1 A 0 A_GiveToTarget("HitBeepMid",1)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
	Death: //crashed somewhere and checks for splash
		TNT1 A 0 A_ChangeFlag("NOINTERACTION",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SeekerMissile(0,0,SMF_LOOK|SMF_CURSPEED,256)
		TNT1 A 0 A_JumpIfTargetInLOS("RadiusHit",360,JLOSF_PROJECTILE,112)
		TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
		Goto DeathCont
		
		RadiusHit:	//splash damage beep
			TNT1 A 0 A_GiveToTarget("HitBeepLow",1)
			TNT1 A 0 A_Jump(256,"DeathCont") //for virtual jump to inherited versions
			Goto DeathCont
		
	DeathCont:
		TNT1 A 0 A_Explode(64,80,XF_QROCKETJUMP,0,40)
		TNT1 A 0 A_PlaySound("quake4/Misc/RocketExplo",5,0.4,0,1.5)
		TNT1 A 3 A_SpawnItemEx("Q4GBSwarmExplo",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		Stop
	}
}

Actor ControlledRocketTrail
{
	+CLIENTSIDEONLY +NONETID
	+NOINTERACTION +NOTONAUTOMAP
	+FORCEXYBILLBOARD
	+DONTBLAST
	Alpha 0.8
	Scale 0.2
	States	
	{
	Spawn:
		TNT1 A 0 NODELAY A_Jump(255,"T1","T3","T3","T4")
	T1:
		TNT1 A 0 A_SetScale(0.2,0.2)
		Goto PlayOff
	T2:
		TNT1 A 0 A_SetScale(-0.2,0.2)
		Goto PlayOff
	T3:
		TNT1 A 0 A_SetScale(0.2,-0.2)
		Goto PlayOff
	T4:
		TNT1 A 0 A_SetScale(-0.2,-0.2)
		Goto PlayOff
	PlayOff:
		YELE BCDEF 3 Bright
		Stop
	}
}



// --------------------------------------------------------------------------
//	For competitive DM, weapons dont autoaim
// --------------------------------------------------------------------------
Actor QCRocketLauncherDM : QCRocketLauncher
{	
	+WEAPON.NOAUTOAIM	
	+CHEATNOTWEAPON
	Tag "$TAG_QCRocketLauncher" // "Rocket Launcher (DM)"
	Weapon.AmmoGive 10
	Weapon.AmmoDmScale 1.0
	Weapon.AmmoDmRefill 1
}

