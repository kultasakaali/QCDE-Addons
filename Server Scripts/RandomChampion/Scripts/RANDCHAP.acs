#library "RANDCHAP"
#include "zcommon.acs"
#import "QCDE.bcs"

global int 48: numUsedChamps;
global int 49: usedChamps[];

str champPool[NUMBER_OF_CLASSES] = {	"RANGER", "DOOMSLAYER", "CORVUS", "KANE", "CALEB", "MENELKIR", "TERMINATOR", "ZEDEK", "INQUISITOR", "BITTERMAN",
										"GALEN", "NYX", "SORGAUL", "DUKE NUKEM", "DURANDAL", "KEEL", "SARGE", "LO WANG", "GRAYSON", "ERADICATOR", "BLAZKOWICZ",
										"FREEMAN", "VORMATUR", "ELEENA", "LUCIENNE", "MAJOR", "HUNTER", "SAM", "POSTAL", "PAINKILLER", "INTRUDER", "ORBB"	};

script "roll" (void) {
	int champ = random(0, NUMBER_OF_CLASSES - 1);
	
	for(int i = 0; i < NUMBER_OF_CLASSES; i++) {
		if(usedChamps[i] == champ)
			restart;
	}
		
	for(int j = 0; j < NUMBER_OF_CLASSES; j++) {
		SetCVar(StrParam(s:"_bc_", d:j), 1);
	}
		
	SetCVar(StrParam(s:"_bc_", d:champ), 0);
	SetCVar("_forcedClass", champ);
	
	usedChamps[numUsedChamps] = champ;
	numUsedChamps++;
}

function void initUsedArray() {
	
	numUsedChamps = 0;
	
	for(int i = 0; i < NUMBER_OF_CLASSES; i++)
		usedChamps[i] = -1;
}

script "InitMap" OPEN {
	if(GetCVar("sv_randomChampMode") == 0)
		terminate;
	
	if(numUsedChamps == 0 || numUsedChamps == NUMBER_OF_CLASSES)
		initUsedArray();
	
	if(GetCVar("_forcedClass") == 999) {
		ACS_NamedExecuteAlways("roll", 0);
		Delay(210);
		SetCVar("_sameMap", true);
	}
}

script "ClearChamp" UNLOADING {		//Handle game modes with warmup with these,
	SetCVar("_forcedClass", 999);	//so that we don't end up endlessly picking
	SetCVar("_sameMap", false);		//a new champ every time warmup ends
}

script "setClass" (void) CLIENTSIDE {	
	int class = GetCvar("_forcedClass");
	
	ConsoleCommand(StrParam(s:"playerclass \"", s:champPool[class], s:"\" "));
}

script "RoundEnds" (int evtType, int arg1, int arg2) EVENT {
	
	if(evtType == 6 && GetCVar("sv_randomChampMode") == 2) {
	
		ACS_NamedExecuteAlways("roll", 0);
		Delay(35);
		ACS_NamedExecuteAlways("setClass", 0);
	}
}

script "ForceChoiceMap" OPEN CLIENTSIDE {						//for first time joiners and spectators, keep server selection updated
	if(GetCVar("sv_randomChampMode") == 0) terminate;
	if(!PlayerIsSpectator(ConsolePlayerNumber())) terminate;
	
	if(!GetCVar("_sameMap") || GetCVar("_firstTime")) {

		HudMessage(s:"Setting your class...\n\cgPlease wait!";
			HUDMSG_PLAIN, 99, CR_WHITE, 1.5, 0.5, 3.0);
	
		Delay(70);
		
		ACS_NamedExecuteAlways("setClass", 0);
		
		Delay(35);
		HudMessage(s:"You can now join the game";
			HUDMSG_PLAIN, 99, CR_GREEN, 1.5, 0.5, 1.5);
		
		SetUserCVar(ConsolePlayerNumber(), "_firstTime", false);
	}
}
